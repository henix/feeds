<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<title>从砖块到大厦：Monadic Combinatorial Parser 简介</title>
</head>
<body>
<p><a href="https://zhuanlan.zhihu.com/p/25867784">原文</a></p>
<div class="title-image"><img src="https://pic1.zhimg.com/v2-7f683b067f8f82875a1bea01cdc0cfc8_r.png" alt=""></div><h1>Intro</h1>
提示
<p>本篇假设读者对基本的 Haskell 语法有所了解，主要包括：类型匹配，参数化类型，lambda 表达式，类型构造器，类型类等。不需要读者熟悉 Monad 类型。</p><p><b>何为 Combinator</b></p><p>在 Parser 构造过程中，将目标 Parser 分解为多个小的 Parser 再组合起来是一种常用的构造方式。将大的问题分解为多个小目标，再把对这些小目标构造出的各种简单 Parser 像乐高积木一样组合，便能最终得到一个复杂而精美的 Parser，这便是“分解——组合”思想的精妙之处。 </p><p>Combinators 将 Parser 的“组合”抽象成一个 Domain Specific Language (DSL) 。用统一的语言来表示这样的组合过程，并提供了一些最基本的 Parser ，这就使得构造 Parser 变成了一个非常简单而易读（ readable） 的过程。而且各种 Parser 都可复用同样的函数，也很符合代码复用的目标。</p><p><b>何为 Monadic</b></p><p>Monad是函数式编程中常用的一类抽象数据类型/代数概念，这里暂且不提其抽象性质，只谈一谈其在Parsing过程中的用途。</p><p>假设已经有了一个简单的 Parser 记为<strong>p</strong>，对于给定输入（这里类型不妨为 String ，后同），<strong>p</strong> 要么将其解析出一个类型为 a 的结果（和剩余的未解析的输入），要么给出一个错误信息。我们可以将 <strong>p</strong> 的类型其表示为以下用 Haskell 代码表示的形式。</p><code lang="haskell">data Parser a = 
     P { runParser : Input -&gt; ParseResult a }

data ParseResult a = Err ParseErr | Res Input a
</code><p>使用 monad （或者在 Haskell 语境下，将 Parser 定义为 Monad 类型类实例），可以达到这样的效果：</p><ol><li><p>如果成功解析，可以把解析出的数据传递下去并被后续的解析过程利用，利用高阶函数，可以充分重用解析结果类型 a 可用的各类函数而不必一一手动提升</p></li><li><p>如果解析失败，可以把错误信息传递下去而并不需要特殊处理，并在最后用统一的错误处理过程来解决出错信息，这样相比直接 error 跳出或显式地传递错误信息对重构代码要友好得多</p></li><li><p>还可以为解析结果附加更多的有用信息，具体做法可以是给 Parser a 类型再套一个 Logger 类型类。值得一提的是，这么做的时候，只需要修改 Parser 的定义和添加一个统一的 Logger 信息处理过程，而几乎不必修改其他部分代码。不过这里还是用当前不带 Logger 的 Parser 类型，目的是只关注本篇主题。</p></li></ol><p>Monad 到底能带来多大的益处，可以通过后面的讲解来详细展示。</p><h1>基本组合子的构造</h1><p>就像乐高积木一样，Parser Combinator 也会提供一些最简单的砖块以供使用者来构造更复杂的结构。</p><p><b>固定读取一个字符的 Parser</b></p><p>就跟字面意思一样，这个 Parser 固定从输入流头部取出一个字符作为结果；当输入为空时，就返回错误信息。</p><code lang="haskell">anyChar :: Parser Char
anyChar = P $ \inp -&gt;
    case inp of
      [] -&gt; Err UnexpectedEof
      x:xs -&gt; Res xs x</code><p><b>永远返回错误信息的 Parser</b></p><p>由于错误信息有多种，所以需要构造多个对应的 Parser，这里举两个例子，有需要的时候可以自己添加更多错误信息到 ParseError 类型中，并构造出对应的组合子。</p><code lang="haskell">unexpectedChar :: Char -&gt; Parser a
unexpectedChar c = P $ \_ -&gt; Err (UnexpectedChar c)

failed :: Parser a
failed = P $ \_ -&gt; Err Failed</code><p><b>不解析但返回一个值</b></p><p>这个 Parser 实际上并不解析任何东西，构造这个组合子的目的在于将把基本类型的计算过程引入到 Parser 里。具体用法后面会再出现。</p><code lang="haskell">pureParser :: a -&gt; Parser a
pureParser x = P $ \inp -&gt; Res inp x
</code><p>这就是全部所需的基本 Parser 。就如同所看到的那样，非常简洁。可以类比为这就是一个代数系统的生成元，配合给出的运算子，就可以组合出全部可能的 Parser 。</p><h1>定义基本运算</h1><p>以上组合子，实际发生解析的只有第一个 anyChar 解析器，要怎样从找一个简陋至极的解析器出发构造更多的解析器呢？这就需要一些运算来调整 anyChar 了。</p><p><b>将基本函数映射到 Parser 上</b></p><p>需要一个“提升”操作，使得原本作用在 a 类型上的函数应用到 Parser a 上。</p><code lang="text">mapParser :: (a -&gt; b) -&gt; Parser a -&gt; Parser b
mapParser f p = P $ \inp -&gt;
     case runParser p inp of
       Res inp' x -&gt; Res inp' (f x)
       Err err -&gt; Err err
</code><p>这里，我们用一个基本函数和一个 Parser 构造出一个产生不同结果的 Parser 。如果原 Parser 会解析错误，那么新 Parser 也返回同样的解析错误；如果原 Parser 解析出一个结果，那么新 Parser 就会给出函数作用在结果后的返回值。</p><p><b>将解析结果提供给之后的解析过程</b></p><p>如果我们需要把解析出来的结果提供给后续的解析过程使用，比如构造带有复杂数据结构的结果，又或者是上下文敏感文法（context sensitive）需要，就需要一个操作来“取出”解析结果。</p><code lang="haskell">bindParser :: 
   (a -&gt; Parser b) -&gt; Parser a -&gt; Parser b
bindParser bf pa = P $ \inp -&gt;
     case runParser pa inp of
       Res inp' x -&gt; runParser (bf x) inp'
       Err err -&gt; Err err
</code><p>这个函数的含义是，给定一个由一个参数决定的 Parser ，和一个 Parser ，将后者的解析结果作为参数提供给前者，得到一个新的 Parser ，——这与之前的“取出结果提供给后续过程”是等价的。同之前的 mapParser 一样，如果第一个 Parser 就解析错误，那后续过程也不会继续，而是直接返回解析错误。</p><p><strong><em>提示</em></strong> 为方便书写，后面使用交换两个参数位置的 flbindParser 。</p><code lang="haskell">flbindParser :: 
     Parser a -&gt; (a -&gt; Parser b) -&gt; Parser b
flbindParser = flip bindParser</code><p><b>定义为类型类实例</b></p><p>事实上，已经定义的这些运算已经足以定义 Parser 为类型类实例。这样做的目的在于，一来仅定义几个最基本的函数就可以充分利用类型类中已定义的衍生出的函数；二来可以用统一的 API 来提供给一个，比如说，对 Monad 已经有所了解的使用者，让TA几乎不看文档就可以直接利用 Monad 类型类的操作符来操作 Parser 。</p><code lang="haskell">instance Functor Parser where
     fmap :: (a -&gt; b) -&gt; Parser a -&gt; Parser b
     fmap = mapParser

instance Applicative Parser where
     pure :: a -&gt; Parser a
     pure = pureParser  
     (&lt;*&gt;) :: Parser (a -&gt; b) -&gt; Parser a -&gt; Parser b
     pf &lt;*&gt; pa = flbindParser pf $ \f -&gt;
                 flbindParser pa $ \x -&gt;
                 pureParser $ f x

instance Monad Parser where
     (=&lt;&lt;) :: (a -&gt; Parser b) -&gt; Parser a -&gt; Parser b
     (=&lt;&lt;) = bindParser
</code><p>感兴趣的读者可以自行验证在这里 Functor law ，Applicative law 和 Monad law 都得到了满足。这也是我们标题中第一个单词Monadic的来源。由类型类导出的函数会在后面使用时再做出对应解释。</p><p><b>串联解析器</b></p><p>把两个解析器串联起来，前一个解析完了让第二个继续解析。这样的操作都已经由 Applicative 类型类提供了对应的操作符，分别是 &lt;*&gt; 将前一个解析结果应用到后一个解析结果上作为返回值，*&gt; 只返回后一个 Parser 的解析结果，&lt;* 只返回前一个 Parser 的解析结果。</p><p><b>并联解析器</b></p><p>如果第一个 Parser 解析失败，那么就尝试用第二个 Parser 解析。</p><code lang="haskell">(&lt;|&gt;) :: Parser a -&gt; Parser a -&gt; Parser a
p &lt;|&gt; q = P $ \inp -&gt;
  case runParser p inp of
    Err err -&gt; runParser q inp
    res -&gt; res
</code><p>有了并联操作之后，最有趣的一点在于我们可以借此实现解析数量不定的元素的 Parser ，实现正则表达式中符号 * / +，或 EBNF 表达式中符号 的功能。</p><code lang="haskell">manyP :: Parser a -&gt; Parser [a]
manyP pa = someP pa &lt;|&gt; pure []

someP :: Parser a -&gt; Parser [a]
someP pa = pa &gt;&gt;= \x -&gt;
           manyP pa &gt;&gt;= \xs -&gt;
           pure $ x:xs
</code><p>这是一个间接递归调用的例子，many返回包含0或多个元素的列表，some返回包含至少一个元素的列表。</p><p>在定义了并联操作和 some ，many 操作后，实际上我们已经使得 Parser 满足了一个称为 MonadPlus 的类型类 / 代数结构的要求——也就是目前所看到的这些要求。</p><h1>更多操作</h1><p>通过之前定义的这些基本操作，我们可以定义出更多更复杂的操作。</p><p><b>只接受限定字符</b></p><p>对 anyChar 解析出的字符进行筛选。</p><code lang="haskell">satisfyP :: (Char -&gt; Bool) -&gt; Parser Char
satisfyP p = anyChar &gt;&gt;= \c -&gt;
     if p c 
       then pure c
       else unexpectedChar c
</code><p><strong><em>注</em></strong> &gt;&gt;= 操作符就是 flbindParser 的中缀版本。</p><p>类似的，可以把这里的 Char 类型替换为更一般的类型变量，不过就不能使用 unexpectedChar 作为解析错误信息了。</p><p>通过替换这里的判定函数 p ，也可以构造许多特定的筛选函数。如：</p><code lang="haskell">isP :: Char -&gt; Parser Char
isP c = satisfyP (== c)

digitP :: Parser Char
digitP :: satisfy isDigit</code><p><b>按顺序调用一列解析器</b></p><p>把同一类型的 Parser 放在一个列表中，按序调用后返回一个解析结果列表，这样可以简化不少重复劳动。</p><code lang="haskell">seqParser :: [Parser a] -&gt; Parser [a]
seqParser [] = pure []
seqParser (p:ps) = p &gt;&gt;= \x -&gt;
                   seqParser ps &gt;&gt;= \xs -&gt;
                   pure $ x:xs
</code><p>这样，我们可以直接由一个字符串生成一个解析指定字符串的 Parser：</p><code lang="haskell">stringP :: String -&gt; Parser String
stringP s = seqParser $ map isP s</code><p><b>解析被特定符号包裹的数据</b></p><p>这样的 Parser 的应用场景可以是比如读取一个 HTML tag 。</p><code lang="haskell">betweenP :: 
  Parser b -&gt; Parser c -&gt; Parser a -&gt; Parser a
betweenP pl pr pm = pl *&gt; pm &lt;* pr
</code><p>上面这段代码含义就是左中右顺序解析，并忽略掉两侧解析特殊符号的结果，只返回中间的解析结果。</p><p><b>解析被特定符号分割的数据</b></p><p>使用在解析比如一列逗号分割的字符串中，如 CSV 格式。</p><code lang="haskell">sepByP :: Parser b -&gt; Parser a -&gt; Parser [a]
sepByP pb pa = ( pa &gt;&gt;= h -&gt;
                 many (pb *&gt; pa) &gt;&gt;= t -&gt;
                 pure $ h : t
               ) &lt;|&gt; pure []
</code><p>上面这段代码，首先读取 1 个元素，再识别 0 个或多个前面包含分隔符的元素，并把所有解析出元素串起来。如果一个元素都没有识别到，就返回空列表。</p><p><b>更多更复杂的 Parser</b></p><p>可以看到，在定义基本操作之后，定义其他 Parser 就不再涉及具体的 Parser 类型类的定义，类似可以定义非常丰富的 Parser 。作为 Parser Combinator 的使用者，并不需要知道 Parser a 的具体结构，只需要知道 Parser Combinator 提供的基本组合子和操作符，就可以搭建出一个解析出特定结构的 Parser ，这也就是为什么这些 Combinators 可以被称之为 Embedded Domain Specific <em>Language</em> 而不仅仅是一个 Library 。</p><h1>一个 JSON Parser 的例子</h1><p>下面，就用以 JSON 格式为例，看我们如何方便地构造将字符串解析成用 Haskell 抽象数据结构表示的 JSON 类型的。[^1]</p><code lang="haskell">jsonString :: Parser Chars
jsonString =
  between
    (is $ fromSpecialCharacter DoubleQuote)
    (charTok $ fromSpecialCharacter DoubleQuote)
    (list (spacialHex &lt;|&gt; noneof (listh "\\\"")))
  where
    spacialHex = is '\\' *&gt; (hexu ||| special)
    special =
      do
        c &lt;- character
        case toSpecialCharacter c of
            Full sc -&gt; valueParser 
              $ fromSpecialCharacter sc
            Empty -&gt; unexpectedCharParser c

jsonNumber :: Parser Rational
jsonNumber = P $ \inp -&gt;
  case readFloats inp of
      Full (num, inp') -&gt; Result inp' num
      Empty -&gt; ErrorResult $
                case inp of
                  Nil -&gt; UnexpectedEof
                  c :. _ -&gt; UnexpectedChar c

jsonTrue :: Parser Chars
jsonTrue = stringTok "true"

jsonFalse :: Parser Chars
jsonFalse = stringTok "false"

jsonNull :: Parser Chars
jsonNull = stringTok "null"

jsonArray :: Parser (List JsonValue)
jsonArray = betweenSepbyComma '[' ']' jsonValue

jsonObject :: Parser Assoc
jsonObject = betweenSepbyComma '{' '}' singleObject
  where
    singleObject =
      do
        spaces
        s &lt;- jsonString
        spaces
        is ':'
        v &lt;- jsonValue
        spaces
        return (s, v)

jsonValue :: Parser JsonValue
jsonValue = spaces *&gt; (
  (pure JsonNull &lt;* jsonNull) &lt;|&gt;
  (pure JsonTrue &lt;* jsonTrue) &lt;|&gt;
  (pure JsonFalse &lt;* jsonFalse) &lt;|&gt;
  (JsonArray &lt;$&gt; jsonArray) &lt;|&gt;
  (JsonString &lt;$&gt; jsonString) &lt;|&gt;
  (JsonObject &lt;$&gt; jsonObject) &lt;|&gt;
  (JsonRational False &lt;$&gt; jsonNumber)
  ) &lt;* spaces

readJsonValue :: Filename -&gt; IO (ParseResult JsonValue)
readJsonValue filename =
  do
    inp &lt;- readFile filename
    return (parse jsonValue inp)
</code><p>这个 Parser 的构造仅需要数十行（注，这里略去了一些小的操作函数的定义，但从其名字中也很容易看出来是怎么用组合子构造的。此外还略去了对特殊字符的转换函数）。相比之下，一个用 C 语言手写的递归下降解释器就比这复杂得多了，可读性和代码复用性也要差上不少。</p><p>值得一提的是这里对 JSON Number Parser 的构造。我们并没有用已有的组合子，而是自己重新构造了一个 Parser ，因为可以直接利用已有的 read 函数。于是这又体现出 <em>Embedded</em> DSL 的另一个特性，即可以与宿主语言无缝结合。</p><h1>更复杂的问题</h1><p>之前的 JSON Parser 是非常简单的，因为这是一个 LL(1) 文法，简单的递归下降解释器就能完成解析。对于更复杂的问题，Parser Combinators 同样可以解决。</p><p><b>上下文相关性</b></p><p>如前所述，利用 Monad 的 bind 功能可以将 Parser 的解析结果取出提供给后续的解析过程，这也就解决了上下文相关的问题。当然，这也是递归下降解释器都能做到的事情</p><p><b>任意向前看文法</b></p><p>之前定义的 &lt;|&gt; 函数实际上已经隐含了无限向前看的能力。第一个 Parser 可以尝试解析前方任意长字符串，失败了就交给第二个 Parser 重新。</p><p><b>左递归处理</b></p><p>实际上，Parser Combinator 如何处理左递归 left-recursion 直到 2008 年才解决，在 Parser Combinator 概念第一次提出的 19 年后。[^2]</p><p>对于左递归的处理，可以参考 parsec 库<a href="http://aslatter/parsec" data-editable="true" data-title="^3">^3</a>的做法，这里直接引用其中的代码：</p><code lang="haskell">chainl1 :: (Stream s m t) =&gt; ParsecT s u m a 
     -&gt; ParsecT s u m (a -&gt; a -&gt; a) 
     -&gt; ParsecT s u m a
chainl1 p op = do{ x &lt;- p; rest x }
     where
       rest x = do{ f &lt;- op
                  ; y &lt;- p
                  ; rest (f x y)
                  }
                &lt;|&gt; return x

chainr1 :: (Stream s m t) =&gt; ParsecT s u m a 
     -&gt; ParsecT s u m (a -&gt; a -&gt; a) 
     -&gt; ParsecT s u m a
chainr1 p op = scan
     where
       scan = do{ x &lt;- p; rest x }
       rest x = do{ f &lt;- op
                  ; y &lt;- scan
                  ; return (f x y)
                  }
                &lt;|&gt; return x</code><p><b>二义性文法</b></p><p>对于二义性，最直接的方法就是将所有可能结果存放在一个列表中，将 Parser 定义改为 P (Input -&gt; [ParseResult]) （这样的改动并不会带来大范围重构），用一个类似于 &lt;|&gt; 操作符的函数将所有可能结果合并起来。但这样的代价就是可能会导致复杂度急剧上升，用 Memoization 可以使得复杂度降至多项式时间[^4]。</p><h1>总结</h1><p>Parser Combinators 是一项非常简洁的自顶向下 Top-Down 语法分析器构造技术，利用这项技术可以极大减小编写语法分析器时的痛苦。利用一些高效的基础数据结构，可以使得 Parser Combinators 有着可与自底向上解析器一比的效率。</p><p><br><br>Reference:<br><br>1 基于 data61/fp-course 的代码。<a href="https://github.com/data61/fp-course" data-editable="true" data-title="data61/fp-course">data61/fp-course</a><a href="file:///C:/Users/Neuromancer/OneDrive/Courses/Practice%20of%20Compilation/Intro%20to%20Monadic%20Combinatorial%20Parser.html#ref-footnote-1">↩</a><br><br>2 Frost, Richard A.; Hafiz, Rahmatullah; Callaghan, Paul (2008). “Parser Combinators for Ambiguous Left-Recursive Grammars”.<a href="file:///C:/Users/Neuromancer/OneDrive/Courses/Practice%20of%20Compilation/Intro%20to%20Monadic%20Combinatorial%20Parser.html#ref-footnote-2">↩</a><br><br>3 <a href="https://github.com/aslatter/parsec" data-editable="true" data-title="aslatter/parsec">aslatter/parsec</a><a href="file:///C:/Users/Neuromancer/OneDrive/Courses/Practice%20of%20Compilation/Intro%20to%20Monadic%20Combinatorial%20Parser.html#ref-footnote-3">↩</a></p><p>4 Frost, Richard A.; Szydlowski, Barbara (1996). “Memoizing Purely Functional Top-Down Backtracking Language Processors”<a href="file:///C:/Users/Neuromancer/OneDrive/Courses/Practice%20of%20Compilation/Intro%20to%20Monadic%20Combinatorial%20Parser.html#ref-footnote-4"><u>↩</u></a></p><p>5 Daan Leijen. Parsec, a fast combinator parser, 2001.</p><p>6 Graham Hutton and Erik Meijer. Monadic parser combinators, 1996. <br><br><br>​</p>
<script async defer="defer" src="https://www.googletagmanager.com/gtag/js?id=UA-7909075-5"></script>
<script>
window.dataLayer = window.dataLayer || [];
function gtag(){ dataLayer.push(arguments); }
gtag('js', new Date());
gtag('config', 'UA-7909075-5');
</script>
<script>
var _hmt = _hmt || [];
</script>
<script async defer="defer" src="https://hm.baidu.com/hm.js?e3d40295e416616ddc21287da9646d31"></script>
</body>
</html>
