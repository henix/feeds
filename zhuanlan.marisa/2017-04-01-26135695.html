<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<title>愉♂悦的scheme之旅（6）-用宏构建DSL</title>
</head>
<body>
<p><a href="https://zhuanlan.zhihu.com/p/26135695">原文</a></p>
<div class="title-image"><img src="https://pic4.zhimg.com/v2-e40c805d9f958b22235f0bf03200c3af_r.png" alt=""></div><b>前言</b><p>写完这篇文章之后，我需要休息一段时间，太累了。叫上基友：<a href="http://www.zhihu.com/people/67ebe1bd8e4a63f17a42572e40afaac7" data-hash="67ebe1bd8e4a63f17a42572e40afaac7" class="member_mention" data-title="@大笨蛋千里冰封" data-editable="true" data-hovercard="p$b$67ebe1bd8e4a63f17a42572e40afaac7">@大笨蛋千里冰封</a>，<a href="http://www.zhihu.com/people/0251012c87c2d3f56ac34de7d71cdcbc" data-hash="0251012c87c2d3f56ac34de7d71cdcbc" class="member_mention" data-editable="true" data-title="@雾雨魔理沙" data-hovercard="p$b$0251012c87c2d3f56ac34de7d71cdcbc">@雾雨魔理沙</a>，<a href="http://www.zhihu.com/people/388eb7e90e64f0f9895ef8b1456c8e07" data-hash="388eb7e90e64f0f9895ef8b1456c8e07" class="member_mention" data-editable="true" data-title="@Glavo" data-hovercard="p$b$388eb7e90e64f0f9895ef8b1456c8e07">@Glavo</a>。</p><p><b>尚未解决的问题</b></p><p>在上一篇文章的结尾，我们还残留了一个问题，那么究竟在什么情况下pmatch会出现不正确的结果呢？</p><code lang="text">(define-syntax ppat
  (syntax-rules (cons else const )
    [(_ fctx val (const cs) res) (if (equal? cs val)
                                     res
                                     fctx)]
    [(_ fctx val else res) res]
    [(_ fctx val (cons a b) res) (if (pair? val)
                                 (ppat fctx (car val) a
                                       (ppat fctx (cdr val) b res))
                                 fctx)]
    [(_ fctx val var res) (let ((var val))
                            res)]))

(define-syntax pmatch
  (syntax-rules ()
    [(_ val) (error "Pattern Matching Error")]
    [(_ val (pattern res) rest ...)
         (let ((t val))
           (ppat (pmatch t rest ...)
                 t pattern res))]))

(let ([t 2])
  (pmatch '(1 2 3)
          [(cons t (cons x (const 1))) t]
          [(cons x y) t]))</code><p>我们期望返回2，却返回了1，是因为第一次匹配虽然失败了，但是已经把t shadow掉了，我们必须保证两条匹配之间不发生任何关系。</p><p>这样就很简单了，我们要把fctx修改成一个闭包，而不是表达式。</p><code lang="text">(define-syntax ppat
  (syntax-rules (cons else const )
    [(_ fctx val (const cs) res) (if (equal? cs val)
                                     res
                                     (fctx))]
    [(_ fctx val else res) res]
    [(_ fctx val (cons a b) res) (if (pair? val)
                                 (ppat fctx (car val) a
                                       (ppat fctx (cdr val) b res))
                                (fctx))]
    [(_ fctx val var res) (let ((var val))
                            res)]))

(define-syntax pmatch
  (syntax-rules ()
    [(_ val) (error "Pattern Matching Error")]
    [(_ val (pattern res) rest ...)
         (let* ((t val)(closure (lambda () (pmatch t rest ...))))
           (ppat closure
                 t pattern res))]))

(let ([t 2])
  (pmatch '(1 2 3)
          [(cons t (cons x (const 1))) t]
          [(cons x y) t]))</code><br><p><b>用syntax-case 构建DSL</b></p><p>syntax-rules因为没有办法写非卫生宏，所以我们很多情况下都不会去用他，相比之下，syntax-case更好用，举个例子。</p><code lang="text">(define-syntax block
  (syntax-rules ()
    [(_ exps ...) (call/cc (lambda (e)
                       (let ((break (lambda () (e (void)))))
                         exps ...)))]))
(block (break)
       1)</code><p>我们提示的错误是没有定义break这个变量，为什么呢，我们来手动展开这个表达式试试看。</p><code lang="text">(&lt;block,block,{}&gt; (&lt;break,break,{}&gt;) 1)

(&lt;call/cc,call/cc,{}&gt; (&lt;lambda,lambda,{}&gt; (&lt;e,e,{}&gt;)
         (&lt;let,let,{}&gt; ((&lt;break,break,{}&gt; (&lt;lambda,lambda,{}&gt; () (&lt;e,e,{}&gt; (&lt;void,void,{}&gt;)))))
              (&lt;break,break,{m1}&gt;) 1)))

(&lt;call/cc,call/cc,{m1}&gt; (&lt;lambda,lambda,{m1}&gt; (&lt;e,e,{m1}&gt;)
         (&lt;let,let,{m1}&gt; ((&lt;break,break,{m1}&gt; (&lt;lambda,lambda,{m1}&gt; () (&lt;e,e,{m1}&gt; (&lt;void,void,{m1}&gt;)))))
              (&lt;break,break,{}&gt;) 1)))

(call/cc (&lt;lambda,lambda,{m1}&gt; (&lt;e,e,{m1}&gt;)
         (&lt;let,let,{m1}&gt; ((&lt;break,break,{m1}&gt; (&lt;lambda,lambda,{m1}&gt; () (&lt;e,e,{m1}&gt; (&lt;void,void,{m1}&gt;)))))
              (&lt;break,break,{}&gt;) 1)))

(call/cc (lambda (g1)
         (&lt;let,let,{m1}&gt; ((&lt;break,break,{m1}&gt; (&lt;lambda,lambda,{m1}&gt; () (g1 (&lt;void,void,{m1}&gt;)))))
              (&lt;break,break,{}&gt;) 1)))

(call/cc (lambda (g1)
         (let ((g2 (lambda () (g1 (void)))))
              (&lt;break,break,{}&gt;) 1)))

(call/cc (lambda (g1)
         (let ((g2 (lambda () (g1 (void)))))
              (break) 1)))


</code><p>展开之后发现一个break有m1，而一个没有，导致联系不上，我们必须使marks保持一致。</p><p>(datum-&gt;syntax #'id 'break) 这样我们就把id的marks塞进break里面。</p><p>(datum-&gt;syntax #'id 'break) =&gt; &lt;break,break,{m1,m2,...}&gt;。<br></p><code lang="text">(define-syntax block
  (lambda (stx)
  (syntax-case stx ()
    [(k exps ...) #`(call/cc (lambda (e)
                       (let ((#,(datum-&gt;syntax #'k
                                               'break) (lambda () (e (void)))))
                         exps ...)))])))

(block 
      (break)
       1)</code><p>现在可以工作了。</p><p><b>syntax-parameterize</b></p><p>但是我依然不推荐这样写，因为这样一旦宏写得比较长，就会很乱，而且会导致一些莫名其妙的错误，因此，syntax-parameterize是一个极好的解决方案。</p><code lang="text">(require racket/stxparam)
(define-syntax-parameter break (syntax-rules ()))
(define-syntax block
  (syntax-rules ()
    [(_ exps ...) (call/cc (lambda (e)
                             (syntax-parameterize
                                 ([break (syntax-rules ()
                                           [(_) (e (void))])])
                               exps ...)))]))

(block (break)
       1)</code><p>看起来清爽多了喵。</p><p><b>alternative way</b></p><p>但是，有一些scheme实现，既不支持syntax-parameterize，又不支持syntax-case，就比较尴尬了，但是还是有解决办法的。</p><code lang="text">(define break (lambda () (error "out of the block")))
(define-syntax block
  (syntax-rules ()
    [(_ exps ...) (call/cc (lambda (e)
                       (let ((obreak break))
                         (set! break (lambda ()
                                     (set! break obreak)
                                     (e (void)))))
                         exps ...))]))
(block (break) 1       )</code><br><p>这种构建dsl的办法其实我在前面已经用过很多次了，只看你有没有理解，注意结束时要break恢复成原来的,或者直接上dynamic-wind或者parameterize。</p><p><b>最后</b></p><p>迄今为止的六篇文章：<a href="https://zhuanlan.zhihu.com/p/25748480" data-title="愉悦的scheme之旅（1）--动态作用域" class="" data-editable="true">愉悦的scheme之旅（1）--动态作用域</a></p><p><a href="https://zhuanlan.zhihu.com/p/25749077" data-editable="true" data-title="愉悦的scheme之旅（2）--用callcc合成控制流" class="">愉悦的scheme之旅（2）--用callcc合成控制流</a><br></p><p><a href="https://zhuanlan.zhihu.com/p/25806246" data-editable="true" data-title="愉悦的scheme之旅（3）-scheme卫生宏系统的实现" class="">愉悦的scheme之旅（3）-scheme卫生宏系统的实现</a></p><p><a href="https://zhuanlan.zhihu.com/p/25864185" data-editable="true" data-title="愉悦的scheme之旅(4)--Delimited Continuations" class="">愉悦的scheme之旅(4)--Delimited Continuations</a></p><p><a href="https://zhuanlan.zhihu.com/p/26000780" data-editable="true" data-title="某愉悦的scheme之旅（5）--从零开始实现模式匹配宏" class="">某愉悦的scheme之旅（5）--从零开始实现模式匹配宏</a></p><p>祝大家愚人节快乐。</p>
<script async defer="defer" src="https://www.googletagmanager.com/gtag/js?id=UA-7909075-5"></script>
<script>
window.dataLayer = window.dataLayer || [];
function gtag(){ dataLayer.push(arguments); }
gtag('js', new Date());
gtag('config', 'UA-7909075-5');
</script>
<script>
var _hmt = _hmt || [];
</script>
<script async defer="defer" src="https://hm.baidu.com/hm.js?e3d40295e416616ddc21287da9646d31"></script>
</body>
</html>
