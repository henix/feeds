<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<title>幻想中的Haskell - Compiling Combinator</title>
</head>
<body>
<p><a href="https://zhuanlan.zhihu.com/p/22231273">原文</a></p>
<div class="title-image"><img src="https://pic3.zhimg.com/95e9aa10b1d60d4c9d046070ba30b702_r.jpg" alt=""></div><p><b><a class="" data-title="【東方】「みこみこ魔理沙」イラスト/相生青唯 [pixiv]" data-editable="true" href="http://www.pixiv.net/member_illust.php?mode=medium&amp;illust_id=55345610">【東方】「みこみこ魔理沙」イラスト/相生青唯 [pixiv]</a><br></b></p><p><b>abstract：利用Finally Tagless，我们可以把HOAS的变种编译上SKI Combinator，同时保留type safety还有extensibility。</b></p><p><b>前置知识：HOAS</b></p><p>如果你要写<a data-title="Lambda calculus" data-editable="true" class="" href="https://en.wikipedia.org/wiki/Lambda_calculus">Lambda calculus</a>解释器，你一定会遇到一个问题：如何表示一个binding?</p><p>最简单的办法（用string表示variable，substitution就是对所有相等String进行替换）有很大的局限性：</p><code lang="text">data Expr = Var String | Abs String Expr | App Expr Expr deriving (Show, Eq)

eval :: Expr -&gt; Expr
eval (Var n) = Var n
eval (Abs n x) = Abs n (eval x)
eval (App l@(Var _) r) = App l (eval r)
eval (App l@(App _ _) r) = eval (App (eval l) (eval r))
eval (App (Abs n l) r) = 
    eval (subst l) where
    subst (Var n') | n == n' = r
    subst (Var n') | n /= n' = Var n'
    subst (Abs n' x) | n == n' = Abs n' x
    subst (Abs n' x) | n /= n' = Abs n' (subst x)
    subst (App ll lr) = App (subst ll) (subst lr)

term = Abs "y" (App (Abs "x" (Abs "y" (Var "x"))) (Var "y"))
main = putStr $ show $ eval term
</code><p>在(\y (\x \y x) y)下做替换，就会成为(\y y)，改变了语义。</p><p>没有这个问题的替换，叫做capture-avoiding substitution（CAS）。 <br></p><p>一个挺不错的是CAS的做法是，我们可以借用metalanguage的binding来完成这个功能-这叫Higher Order Abstract Syntax</p><code lang="text">data Expr = Lam (Expr -&gt; Expr) | App Expr Expr
</code><p>有了这功能以后，我们不止借去了binding，也借去了语法-我们定义Expr跟定义普通lambda function长得很像，下面就是Y的定义：</p><code lang="text">y = Lam (\f -&gt; let e = Lam (\x -&gt; App f (App x x)) in App e e)
</code><p> 可以看到，我们可以在Lam里面使用let，而不需要在object language有let。<br></p><p>Evaluator 也一样可以借过来：</p><code lang="text">eval (Lam l) = Lam l
eval (App (Lam l) r) = eval (l r)
eval (App l r) = eval (App (eval l) r)</code><p>有了上面这几行代码，我们就‘实现’了一个UTLC的解释器， 跟我念，EDSL（Embedded DSL）大法好（</p><p>这问题还有很多奇奇怪怪的做法，比如鸵鸟法（在Simply Typed Lambda Calculus下只要替换的两边都是close term就不需要管），有兴趣的可以看<a class="" data-title="Bound - School of Haskell" data-editable="true" href="https://www.schoolofhaskell.com/user/edwardk/bound">Bound - School of Haskell</a></p><p><b>问题：</b>如果你用了HOAS，你就无法对你的Expression做很多操作，比如PrettyPrint/Optimization/Serialization - 你甚至不能把Expression reduce到normal form，因为HOAS不是first order的：你无法“看进”一个函数。我们能不能保留HOAS的易用性的同时支持这些操作？</p><p><b>前置知识：Finally Tagless </b></p><p>我们现在先把Lambda Calculus放一边，看另一个问题：</p><code lang="text">data Arith = Lit Int | Plus Arith Arith

eval (Lit i) = i
eval (Plus l r) = eval l + eval r
</code>我们可以很简单的为Arith增加一个函数（比如pretty printing），但是我们不能为Arith增加一个Constructor（比如Minus），同时保持extensibility跟type safety-如果你增加了一个Constructor，以前的对Arith进行pattern matching的代码就不是exhaustive的了-在这，就是你eval并没有处理Minus的情况<p>在Java中，这个问题就刚好反过来，我们可以增加constructor，但是不能增加函数（比如现在你就不能加入pretty print了）<br></p><code lang="java">abstract class Arith {
	abstract int eval();
}

class Lit extends Arith {
	int i;
	Lit(int i) {
		this.i = i;
	}
	int eval() {
		return i;
	}
}

class Plus extends Arith {
	Arith l, r;
	Plus(Arith l, Arith r) {
		this.l = l;
		this.r = r;
	}
	int eval() {
		return l.eval() + r.eval();
	}
}
</code><p>这叫做Expression Problem。</p><p>这个问题的一种解法，叫Finally Tagless：我们把Arith的每个Constructor表示成函数，但是abstract掉这些函数的返回类型</p><code lang="haskell">class Arith repr where
    lit :: Int -&gt; repr
    plus :: repr -&gt; repr -&gt; repr

instance Arith Int where
    lit i = i
    plus l r = l + r

example :: Arith repr =&gt; repr
example = plus (lit 12) (lit 450)
</code><p>我们现在可以在两边扩展了。</p><code lang="haskell">class Minus repr where
    minus :: repr -&gt; repr -&gt; repr

instance Arith String where
    lit = show
    plus l r = "(" ++ l ++ " + " ++ r ++ ")"

instance Minus Int where
    minus l r = l - r

instance Minus String where
    minus l r = "(" ++ l ++ " - " ++ r ++ ")"

example :: Arith repr =&gt; Minus repr =&gt; repr
example = plus (lit 12) (minus (lit 45) (lit 0))

main :: IO ()
main = putStrLn example
</code><p>输出(12 + (45 - 0))，很好。</p><p>看到这，细心的朋友可能会有一个问题：如果我想在Arith这语言里面加入double（同时保留type safety跟extensibility）怎么办？</p><p>最简单的做法是，我们把repr从Type改成Type -&gt; Type。</p><code lang="text">class Arith repr where
    litI :: Int -&gt; repr Int
    plusI :: repr Int -&gt; repr Int -&gt; repr Int
    litD :: Double -&gt; repr Double
    plusD :: repr Double -&gt; repr Double -&gt; repr Double
</code><p><b>回到问题上: </b></p><code lang="text">问题：如果你用了HOAS，你就无法对你的Expression做很多操作，比如PrettyPrint/Optimization/Serialization - 你甚至不能把Expression reduce到normal form，因为HOAS不是first order的：你无法“看进”一个函数。我们能不能保留HOAS的易用性的同时支持这些操作？
</code><p>我们有一个typeclass：</p><code lang="text">class Language repr where
    app :: repr (a -&gt; b) -&gt; repr a -&gt; repr b
    ??? --we might need more method
</code><p>我们需要一个函数，lam：</p><code lang="text">Language repr =&gt; (repr a -&gt; repr b) -&gt; repr (a -&gt; b)
</code><p>而其中，Language中没有接受函数的Constructor。或者说，我们可以实现Instance Language PrettyPrint。</p><p>很明显，这不可行 - 我们没有任何办法获得一个repr (a -&gt; b)。如果要一个证明，可以看<a data-title="Theorems for Free!" data-editable="true" class="" href="http://ecee.colorado.edu/ecen5533/fall11/reading/free.pdf">Theorems for Free!</a><br></p><p>所以我们需要在一定程度上改变类型签名。</p><p>在考虑该如何更改的时候，我们先做出另一个observation：不止我们没办法生成repr (a -&gt; b)，因为我们没办法生成repr a，我们没办法使用(repr a -&gt; repr b)！再说，就算我们可以使用，repr b的类型也跟repr (a -&gt; b)的类型不一样！</p><p>我们再对类型观察一下，发现另一件事情：由于用户需要的就是一个repr (a -&gt; b)，所以这个类型不能改。</p><p>把三个条件组合在一起，可以推出，我们需要把签名改成(unknown a -&gt; unknown b) -&gt; repr (a -&gt; b)。而其中我们可以生成unknown a, 并且unknown b可以转换为repr (a -&gt; b)。</p><p>在这之下，unknown的类型呼之若出：unknown x = repr (a -&gt; x)。<br></p><p>我们可以生成一个unknown a - 因为这是repr (a -&gt; a)，identity。 <br></p><p>我们也可以从unknown b生成repr (a -&gt; b) - 因为两者相等。</p><code lang="text">newtype Next repr a b = Next { unNext :: repr (a -&gt; b) }

lam :: SKI repr =&gt; (Next repr a a -&gt; Next repr a b) -&gt; repr (a -&gt; b) 
lam f = unNext $ f (Next i)
</code><p> 现在剩下的问题是，unknown（我们改名做Next，提高可读性）是不是Language的instance。如果不是，使用Next会跟使用其他Language不一样，就会大大降低易用性。
</p><p>我们现在可以考虑下，Language需要支持什么操作。也就是，???是什么</p><blockquote><p>我们可以生成一个unknown a - 因为这是repr (a -&gt; a)，identity。 </p></blockquote><code lang="text">class Language repr where
    app :: repr (a -&gt; b) -&gt; repr a -&gt; repr b
    i :: repr (a -&gt; a)
    ???
</code><p>然后，我们需要支持conv :: repr a -&gt; Next repr b a（因为用户需要在lam中用原本的term）</p><p>换句话说，我们需要repr a -&gt; repr (b -&gt; a)：这是const。</p><code lang="text">class Language repr where
    app :: repr (a -&gt; b) -&gt; repr a -&gt; repr b
    i :: repr (a -&gt; a)
    k :: repr (a -&gt; b -&gt; a)
    ???</code><p> 现在，我们可以先试下实现Next</p><code lang="text">instance Language repr =&gt; Language (Next repr a) where
    app :: Next repr a (b -&gt; c) -&gt; Next repr a b -&gt; Next repr a c
    app = undefined
    i = conv i
    k = conv k

conv :: Language repr =&gt; repr a -&gt; Next repr b a
conv x = Next (app k x)
</code><p>因为我们需要转换函数conv，把原本的term变成Next的term，所以就加入了k</p><p>现在正好就可以用conv来把原本的基本construct lift到Next里面去</p><p>但是，app无法lift进去：我们要好好考虑怎么做<br></p><p>如果我们unwrap掉app的类型，我们会得到</p><code lang="text">repr (a -&gt; b -&gt; c) -&gt; repr (a -&gt; b) -&gt; repr (a -&gt; c)
</code><p>如果repr是Eval的话（换句话说(a -&gt; b -&gt; c) -&gt; (a -&gt; b) -&gt; (a -&gt; c)），这可以实现：\abc -&gt; \ab -&gt; \a -&gt; abc a (ab a)。所以我们只需要把这个类型作为基础method加入language，然后就可以（这个函数在Language内只需要用conv实现） <br></p><p>如果你有好好的学Combinatorial Logic的话，就会一眼认出这是SKI的S。换句话说，Language需要支持S。这样，整个language就是SKI Combinator（注意：因为是typed的，所以这并不是图灵完备的。手动加入一个Y就能解决这个情况）</p><code lang="haskell">{-# LANGUAGE
    MultiParamTypeClasses,
    RankNTypes,
    ScopedTypeVariables,
    FlexibleInstances,
    FlexibleContexts,
    UndecidableInstances,
    IncoherentInstances,
    PolyKinds #-}

class SKI repr where
    app :: repr (a -&gt; b) -&gt; repr a -&gt; repr b
    s :: repr ((a -&gt; b -&gt; c) -&gt; (a -&gt; b) -&gt; a -&gt; c)
    k :: repr (a -&gt; b -&gt; a)
    i :: repr (a -&gt; a)

newtype ShowTerm x = ShowTerm { showTerm :: String }

instance SKI ShowTerm where
    app f x = ShowTerm $ "(" ++ showTerm f ++ " " ++ showTerm x ++ ")"
    s = ShowTerm "S"
    k = ShowTerm "K"
    i = ShowTerm "I"

newtype Next repr a b = Next { unNext :: repr (a -&gt; b) }

conv :: SKI repr =&gt; repr a -&gt; Next repr b a
conv = Next . app k

instance SKI repr =&gt; SKI (Next repr a) where
    app f x = Next $ app (app s $ unNext f) $ unNext x
    s = conv s
    k = conv k
    i = conv i

lam :: SKI repr =&gt; (Next repr a a -&gt; Next repr a b) -&gt; repr (a -&gt; b)
lam f = unNext $ f (Next i)

c :: SKI repr =&gt; repr ((a -&gt; b -&gt; c) -&gt; b -&gt; a -&gt; c)
c = lam (\abc -&gt; lam (\b -&gt; lam (\a -&gt; app (app (conv $ conv abc) a) $ conv b)))

main :: IO ()
main = putStrLn $ showTerm c
</code><p>我们额外的加入了一个show，来表示这的确是first order的。<br></p><p>代码过Type Check了。一切都解决了吗？不！</p><p>上述代码输出了：</p><code lang="text">((S ((S (K S)) ((S ((S (K S)) ((S (K K)) (K S)))) ((S ((S (K S)) ((S ((S (K S)) ((S (K K)) (K S)))) ((S ((S (K S)) ((S (K K)) (K K)))) ((S (K K)) I))))) ((S (K K)) (K I)))))) ((S ((S (K S)) ((S (K K)) (K K)))) (K I)))
</code><p>这个什么鬼。。。</p><p>被刷屏的原因是，</p><code lang="text">    app f x = Next $ app (app s $ unNext f) $ unNext x
</code><p>会把每一个app变成两个app，成为了指数增长。</p><p>解决方案也很简单：在大部分的时候，一颗AST（HOAS的）的大部分subtree都没有跟Var有任何联系，这时候我们只需要保存原tree，最后k一次就好</p><code lang="text">data Next repr a b = Fast (repr b) | Slow (repr (a -&gt; b))

unNext :: SKI repr =&gt; Next repr a b -&gt; repr (a -&gt; b)
unNext (Slow x) = x
unNext (Fast x) = app k x

instance SKI repr =&gt; SKI (Next repr a) where
    app (Fast f) (Fast x) = Fast $ app f x
    app (Slow f) (Slow x) = Slow $ app (app s f) x
    app (Slow f) (Fast x) = app (Slow f) (Slow $ app k x)
    app (Fast f) (Slow x) = app (Slow $ app k f) (Slow x)
    s = Fast s
    k = Fast k
    i = Fast i

lam :: SKI repr =&gt; (Next repr a a -&gt; Next repr a b) -&gt; repr (a -&gt; b)
lam f = unNext $ f (Slow i)
</code><p>输出：</p><code lang="text">((S ((S (K S)) ((S (K K)) ((S (K S)) ((S ((S (K S)) ((S (K K)) I))) (K I)))))) (K ((S (K K)) I)))
</code><p>现在这货勉强能看，并且如果你用S (K X) Y = compose X Y这条规则简化下的话，还是知道他是在说什么的。。。</p><p>我们再做点微小的工作吧：需要用户手动填conv（在优化后叫Fast）实在太不人道了，我们试试看用subtyping自动的做转换。</p><code lang="text">class NT l r where
    conv :: l t -&gt; r t

instance NT l r =&gt; NT l (Next r a) where
    conv = Fast . conv

instance NT x x where
    conv = id

lam :: forall repr a b. SKI repr =&gt;
 ((forall k. NT (Next repr a) k =&gt; k a) -&gt; (Next repr a) b) -&gt; repr (a -&gt; b)
lam f = unNext $ f (conv (Slow i :: Next repr a a))

c :: SKI repr =&gt; repr ((a -&gt; b -&gt; c) -&gt; b -&gt; a -&gt; c)
c = lam (\abc -&gt; lam (\b -&gt; lam (\a -&gt; app (app abc a) b)))
</code><p>我们的唯一改动，就是Next repr a a变成了Next repr a a的所有Supertype-这样当我们需要‘向上转换’的时候，haskell就会自动完成。<br></p><p>另：NT是Natrual Transformation的简写，应该看成subkinding，直接subtype会报错</p><p>这个方法是type safe的（trivial），同时也是可扩展的：我们这就加入Arith，并且加入eval功能</p><code lang="haskell">class Arith repr where
    int :: Int -&gt; repr Int
    add :: repr (Int -&gt; Int -&gt; Int)

instance Arith ShowTerm where
    int x = ShowTerm $ show x
    add = ShowTerm "add"

newtype Eval x = Eval { eval :: x }

instance SKI Eval where
    app f x = Eval $ eval f $ eval x
    s = Eval (\abc ab a -&gt; abc a (ab a))
    k = Eval const
    i = Eval id

instance Arith Eval where
    int = Eval
    add = Eval (+)

instance Arith repr =&gt; Arith (Next repr a) where
    int x = Fast (int x)
    add = Fast add

double :: SKI repr =&gt; Arith repr =&gt; repr (Int -&gt; Int)
double = lam (\i -&gt; app (app add i) i)

main :: IO ()
main = putStrLn $ showTerm double
</code><p><b>练习：</b></p><ol><li>在HOAS构造出一个在UTLC（Untyped Lambda Calculus）中没对应的Term</li><li>c的输出还可以优化。加入更多的基本combinator，然后缩短c的pretty print的长度</li><li>有很多表示LambdaCalculus的方法， SKI只是一种，试下用其他的方法表达</li><li>Expression Problem也有很多解法，比如<a data-title="Object Algebra" data-editable="true" class="" href="https://www.cs.utexas.edu/~wcook/Drafts/2012/ecoop2012.pdf">Object Algebra</a>，<a class="" data-title="psu.edu 的页面" data-editable="true" href="http://citeseerx.ist.psu.edu/viewdoc/summary?doi=10.1.1.101.4131">Data Type a la carte</a>，换一种表示法 </li><li>用自己喜欢的语言重写上面的代码，看看那个语言更好（划掉）</li></ol><p> 另外：</p><p>感谢<a data-title="@王瑞康" data-editable="true" class="member_mention" href="https://www.zhihu.com/people/510c8b17a63363f80da934ff7af10cea" data-hash="510c8b17a63363f80da934ff7af10cea" data-hovercard="p$b$510c8b17a63363f80da934ff7af10cea">@王瑞康</a> ，最终版lam是他写的，当初脑抽了，一定要肝minimal type，肝了两天，然而Haskell不认QAQ</p><p><a class="" data-title="GitHub - MarisaKirisame/CompilingCombinator: Code for my blog" data-editable="true" href="https://github.com/MarisaKirisame/CompilingCombinator">GitHub - MarisaKirisame/CompilingCombinator: Code for my blog</a>这是代码</p>
<script async defer="defer" src="https://www.googletagmanager.com/gtag/js?id=UA-7909075-5"></script>
<script>
window.dataLayer = window.dataLayer || [];
function gtag(){ dataLayer.push(arguments); }
gtag('js', new Date());
gtag('config', 'UA-7909075-5');
</script>
<script>
var _hmt = _hmt || [];
</script>
<script async defer="defer" src="https://hm.baidu.com/hm.js?e3d40295e416616ddc21287da9646d31"></script>
</body>
</html>
