<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<title>[Parsing] 1. Prolog</title>
</head>
<body>
<p><a href="https://zhuanlan.zhihu.com/p/20013565">原文</a></p>
用Prolog语法来写Parser是一件很自然的事。比BNF要好的多。BNF 只是描述了语法，用Prolog写直接就把Parser给写完了。比如 <br><code lang="text">&lt;plus_exp&gt; ::= &lt;mul_exp&gt; | &lt;mul_exp&gt; &lt;plus&gt; &lt;plus_exp&gt;
&lt;mul_exp&gt; ::= &lt;num&gt; | &lt;num&gt; &lt;mul&gt; &lt;mul_exp&gt;
&lt;plus&gt; ::= "plus"
&lt;mul&gt; ::= "mul"
&lt;num&gt; ::= &lt;integer&gt;</code><p>Prolog:</p><code lang="text">plus(S0, S) :-
  S0 = [plus|S].

mul(S0, S) :-
  S0 = [mul|S].

num(N, S0, S) :-
  S0 = [N|S],
  integer(N).

plus_exp(Expr, S0, S) :-
  mul_exp(Expr, S0, S).

plus_exp(Expr, S0, S) :-
  mul_exp(E1, S0, S1),
  plus(S1, S2),
  plus_exp(E2, S2, S),
  Expr =.. [plus, E1, E2].

mul_exp(Expr, S0, S) :-
  num(Expr, S0, S).

mul_exp(Expr, S0, S) :-
  num(E1, S0, S1),
  mul(S1, S2),
  mul_exp(E2, S2, S),
  Expr =.. [mul, E1, E2].
</code><p>问题是虽然Prolog这语法看上去很好，真的很难用来写程序，动不动就不知道回溯到哪里去了，一cut又傻了，碰到左递归就死循环了。长的像一阶逻辑，其实完全没有逻辑可言。这也能叫Prolog ? <br></p><p>即便无视这些缺点，对于写Parser来说，Prolog依旧是太强大了。Prolog可是图灵完全的。很容易构造出有无数个结果的查询</p><code lang="text">p([]).
p(L) :-
  L = [a|X],
  p(X).</code><p> 为了避免这个问题，就不能有复合类型。这样就没法用List来表示输入的字符串了。当然List还有另外一个问题就是，按这么写，必须一次提供完整的输入，而不是一次一个字符。</p><p>这是可以做到的，比如 <br></p><code lang="text">[1,plus,2]
</code><p> 可以表示成</p><code lang="text">token(1, 0, 1).
token(plus, 1, 2).
token(2, 2, 3).
</code><p> 语法规则就可以写成 (先忽略生成的语法树)<br></p><code lang="text">plus(S0, S) :-
  token(plus, S0, S).

mul(S0, S) :-
  token(mul, S0, S).

num(S0, S) :-
  token(N, S0, S),
  integer(N).

plus_exp(S0, S) :-
  mul_exp(S0, S).

plus_exp(S0, S) :-
  mul_exp(S0, S1),
  plus(S1, S2),
  plus_exp(S2, S).

mul_exp(S0, S) :-
  num(S0, S).

mul_exp(S0, S) :-
  num(S0, S1),
  mul(S1, S2),
  mul_exp(S2, S).</code><p>这就变成Datalog了嘛。</p><p>接下来就来看Datalog</p>
<script async defer="defer" src="https://www.googletagmanager.com/gtag/js?id=UA-7909075-5"></script>
<script>
window.dataLayer = window.dataLayer || [];
function gtag(){ dataLayer.push(arguments); }
gtag('js', new Date());
gtag('config', 'UA-7909075-5');
</script>
<script>
var _hmt = _hmt || [];
</script>
<script async defer="defer" src="https://hm.baidu.com/hm.js?e3d40295e416616ddc21287da9646d31"></script>
</body>
</html>
