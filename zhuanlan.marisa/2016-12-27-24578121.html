<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<title>Programming Languages: Application and Interpretation【译1-4】</title>
</head>
<body>
<p><a href="https://zhuanlan.zhihu.com/p/24578121">原文</a></p>
<div class="title-image"><img src="https://pic3.zhimg.com/v2-b71b632ed7974538bb2e812bcd20914e_r.jpg" alt=""></div><p>原书地址：<a href="http://cs.brown.edu/courses/cs173/2012/book/index.html" data-editable="true" data-title="Programming Languages: Application and Interpretation">Programming Languages: Application and Interpretation</a></p><p>声明：</p><ul><li>翻译未经原作者校审</li><li>内容不保证完全忠于原书，如想要读本书原汁原味的版本请点击上面链接阅读原文</li></ul><p>更新，后续译文地址：</p><ul><li><a href="https://zhuanlan.zhihu.com/p/24720187" data-editable="true" data-title="Programming Languages: Application and Interpretation【译5】">Programming Languages: Application and Interpretation【译5】</a></li></ul><h1>引言</h1><h2>本书使用的语言</h2><p>本书使用的主要语言是 Racket 。然而跟很多操作系统一样，Racket 支持很多编程语言，所以你必须显式的告诉 Racket 你在使用什么语言进行编程。在 Unix 系统的 shell 脚本中你需要在脚本开头添加下面这样一行来指明解释器：</p><br><code lang="text">#!/bin/sh</code><p>类似的，Racket 需要你声明你要使用的语言。 Racket语言 可能使用和 Racket 一样的括号语法，但是有不同的语义； 或语义相同语法不同；或者有不同的语法和语义。因此每个 Racket 程序以 #lang &lt;语言名字&gt; 开头。默认的语言为 Racket（名字为 racket）。这本书中我们几乎总是使用语言：</p><br><code lang="text">plai-typed</code><p>使用该语言时，在程序的第一行添加（本书后面例子代码中请假定我们添加了该行）：</p><br><code lang="racket">#lang plai-typed</code><p>Typed PLAI 语言和传统的 Racket 最主要的不同是它是静态类型的。它还给你提供了一些有用的的东西 （constructs）： define-type 、 type-case 和 test 。下面是他们的使用实例。创建新的数据类型：</p><br><code lang="text">(define-type MisspelledAnimal
  [cmal (humps : number)]
  [yacc (height : number)])</code><p>它做的事情类似于在 Java 中：创建一个抽象类 MisspelledAnimal ，它有两个实体子类： caml 和 yacc ， 它们的构造参数分别为 humps 和 height 。</p><p>该语言中，我们通过下面方式可以创建实例：</p><br><code lang="racket">(caml 2)
(yacc 1.9)</code><p>定义数据类型后，语言会自动给我生成类型判断函数和字段选择器：</p><br><code lang="racket">;; 类型判断函数
(caml? (caml 2)) ;; true
(caml? (yacc 2)) ;; false

(yacc? (yacc 2)) ;; true
(yacc? (caml 2)) ;; false

;; 字段选择
(caml-humps (caml 2))   ;; 2
(yacc-height (yacc 10)) ;; 10</code><p>同名字暗示的一样， define-type 创建一个给定名字的类型。我们可以将实例绑定到名字：</p><br><code lang="racket">(define ma1 : MisspelledAnimal (caml 2))
(define ma2 : MisspelledAnimal (yacc 1.9))</code><p>事实上这里你并不需要显式的声明类型，因为 Typed PLAI 会进行类型推测。因此上面的代码可以写成：</p><br><code lang="racket">(define ma1 (caml 2))
(define ma2 (yacc 1.9))</code><p>但是出于使函数易于理解使用的原因，我们倾向于对函数的返回类型进行显式的声明。</p><p>类型的名字可以递归的使用，本书会经常使用这种方式。</p><p>该语言为我们提供了模式匹配，例如对于函数体：</p><br><code lang="racket">(deifne (good? [ma : MisspelledAnimal]) : boolean
        (type-case MisspelledAnimal ma
                   [caml (humps) (&gt;= humps 2)]
                   [yacc (height) (&gt; height 2.1)]))</code><p>对于其中的表达式 (&gt;= humps 2) ， humps 是要匹配的 caml 实例的构造参数。</p><p>最后，你可以使用 test 写测试。</p><br><code lang="racket">(test (good? ma1) #t)
(test (good? ma2) #f)</code><p>当你运行上面的代码时，该语言会告诉你两个测试都通过了。对于未通过的代码，它会提示错误出现在哪。 要了解更多请阅读文档。</p><p>上面有些东西可能比较费解。我们在模式匹配中为了匹配数据字段时使用了和数据定义时相同的名字 humps （和 height ）。 这是完全没有必要的，完全可以使用其它名字：</p><br><code lang="racket">(define (good? [ma : MisspelledAnimal]) : boolean
  (type-case MisspelledAnimal ma
             [caml (h) (&gt;= h 2)]
             [yacc (h) (&gt; h 2.1)]))</code><p>因为每个 h 仅在对应的匹配分支中可见，所以上面的代码没有问题。</p><p>不使用模式匹配的话，也可以使用上面提到的类型判断函数和字段选择器也可以实现上面的函数：</p><br><code lang="racket">(define (good? [ma : MisspelledAnimal]) : boolean
  (cond
    [(caml? ma) (&gt;= caml-humps ma) 2]
    [(yacc? ma) (&gt; (yacc-height ma) 2.1)]))</code><blockquote><p>如果给函数传入了错误的数据类型会发生什么？如：</p><p>(good? "Should be a MisspelledAnimal, but a string")</p></blockquote><h1>本书有关 Parsing 的东西</h1><p>Parsing 是将输入字符流转换成结构化内部表示的过程。常见的内部表示是树 ，可以使用程序递归的处理树这种数据结构。 例如，给定输入流：</p><br><code lang="racket">23 + 5 - 6</code><p>我们可以将其转换成一颗根节点为加法，左边节点表示数字 23 ，右边节点是用树表示 5-6 的树。 parser 是用于实现这种转换的程序。</p><img src="https://pic2.zhimg.com/v2-611f3b1b9f9b74369074135a5e689770_r.png" data-rawwidth="228" data-rawheight="206"><p>Parsing 本身是一个比较复杂，且由于歧义的存在，还远没有被解决的问题。例如上面的例子， 你还可以将其转换成根节点为减法，左边为表示 23+5 的树，右边为数字 6 的树。</p><img src="https://pic1.zhimg.com/v2-048dcf0d31b3183ce6911453badb101c_r.png" data-rawwidth="244" data-rawheight="212"><p>我们还需要考虑操作符（ + ， - ）的优先性、两个操作数的交换性等问题。要 parse 一个羽翼丰满的语言（说的不是自然语言），要考虑的问题只会越来越多、越来越复杂。</p><h2>轻量级的，内建的 Parser</h2><p>这些问题使得 parsing 本身适合当作一个单独主题来讲，本书主题不专注于该方面。从我们的角度来说， parsing 是一层抽象，因为我们想学习的是编程语言的各个部分而不是 parsing 本身。因此， 我们使用 Racket 一个有用的特性来将输入流转换成树： read 。 read 和该语言的括号语法形式紧密关联。 它将括号形式转换成内部树形式。运行 (read) 然后输入 ——</p><br><code lang="text">(+ 23 (- 5 6))</code><p>——会产出一个列表，其第一个元素是符号 '+ ，第二个元素是数字 23 ，第三个元素是一个列表； 该列表其第一个元素是符号 '- ，第二个元素是数字 5 ，第三个元素是数字 6。</p><h2>Parsing 得到的类型</h2><p>在 Typed PLAI 中， (read) 返回值类型为 s-expression 。</p><br><code lang="text">&gt; (read)
- s-expression
[type in (+ 23 (- 5 6))]
'(+ 23 (- 5 6))</code><h2>写个简单的 parser</h2><p>考虑后面章节算术的表示中对于算术表达式的数据结构定义：</p><br><code lang="racket">(define-type ArithC
  [numC (n : number)]
  [plusC (l : ArithC) (r : ArithC)]
  [multC (l : ArithC) (r : ArithC)])</code><p>目标是将 racket 给我们初步解析得到 s-expression 解析成该数据类型，代码简单直接：</p><br><code lang="racket">(define (parse [s : s-expression])
  (cond
    [(s-exp-number? s) (numC (s-exp-&gt;number s))]
    [(s-exp-list? s)
     (let ([sl (s-exp-&gt;list s)])
       (case (s-exp-&gt;symbol (first sl))
         [(+) (plusC (parse (second sl)) (parse (third sl)))]
         [(*) (multC (parse (second sl)) (parse (third sl)))]
         [else (error 'parse "invalid list input")]))]
    [else (error 'parse "invalid input")]))</code><p>注意，为简便起见，这里的代码有些东西没有考虑，比如我们只考虑了两位数的加法和乘法， 且没有对传入参数的个数 进行校验。简单运行如下：</p><br><code lang="text">&gt; (parse (read))
- ArithC
(+ (* 2 3) (+ 3 6))
(plusC (multC (numC 2) (numC 3)) (plusC (numC 3) (numC 6)))</code><p>到此我们完成一个简单的 parser！虽然它的大部分工作是由 read 函数替我们做的， 但是我们还是成功的将输入程序解析成了定义好的内部表示。</p><h1>解释器初窥</h1><p>现在有了程序的表示，我们有很多方式可以用来操纵它们。我们可能想把程序打印的漂亮点（pretty-print）， 将其转换成其它格式的代码（编译），查看其是否符合特定属性（校验），等。现在， 我们专注于考虑得到其对应的值——计算（evaluation）——将程序规约成值。</p><p>现在来为我们的算术语言写个解释器形式的求值器。选择算术运算是出于下面三个主要原因：</p><p>你已经知道怎么计算加减乘除了，我们可以专注于其实现；基本上每门语言都会包含算术运算， 所以我们可以从它开始进行语言的扩展；该问题很简单，但是可以扩展出复杂的情况以展示我们要展示的观点。</p><h2>算术表达式的表示</h2><p>我们首先确认一下要使用的算术表达式的表示。现在我们只支持两个运算符——加法和乘法——而且是针对自然数的。</p><br><code lang="racket">(define-type ArithC
  [numC (n : number)]
  [plusC (l : ArithC) (r : ArithC)]
  [multC (l : ArithC) (r : ArithC)])</code><h2>写个解释器</h2><p>下面开始写该算术语言的解释器。首先我们考虑一下该解释器的类型：它的输入显然是 ArithC 值， 返回值的类型呢？ 当然是数啦。即我们的解释器是一个输入为 ArithC 输出为数的函数。</p><p>由于输入类型是一个递归定义的数据类型，我们可以很自然的在解释器进行对其进行递归的解释。形如：</p><br><code lang="racket">(define (interp [a : ArithC]) : number
  (type-case ArithC a
             [numC (n) n]
             [plusC (l r) ...]
             [multC (l r) ...]))</code><p>你很可能想当然的直接写出下面的代码：</p><br><code lang="racket">(define (interp [a : ArithC]) : number
  (type-case ArithC a
             [numC (n) n]
             [plusC (l r) (+ l r)]
             [multC (l r) (+ l r)]))</code><p>略微观察就能发现其错误。显然解释器代码应该形似下面这样（由于 l 和 r 都是 ArithC 类型的需要被解释）：</p><br><code lang="racket">(define (interp [a : ArithC]) : number
  (type-case ArithC a
             [numC (n) n]
             [plusC (l r) ... (interp l) ... (interp r) ...]
             [multC (l r) ... (interp l) ... (interp r) ...]))</code><p>填充必要部分得到解释器：</p><br><code lang="racket">(define (interp [a : ArithC]) : number
  (type-case ArithC a
             [numC (n) n]
             [plusC (l r) (+ (interp l) (interp r))]
             [multC (l r) (* (interp l) (interp r))]))</code><p>下面是完整代码：</p><br><code lang="racket">#lang plai-typed

(define-type ArithC
  [numC (n : number)]
  [plusC (l : ArithC) (r : ArithC)]
  [multC (l : ArithC) (r : ArithC)])

(define (parse [s : s-expression])
  (cond
    [(s-exp-number? s) (numC (s-exp-&gt;number s))]
    [(s-exp-list? s)
     (let ([sl (s-exp-&gt;list s)])
       (case (s-exp-&gt;symbol (first sl))
         [(+) (plusC (parse (second sl)) (parse (third sl)))]
         [(*) (multC (parse (second sl)) (parse (third sl)))]
         [else (error 'parse "invalid list input")]))]
    [else (error 'parse "invalid input")]))

(define (interp [a : ArithC]) : number
  (type-case ArithC a
             [numC (n) n]
             [plusC (l r) (+ (interp l) (interp r))]
             [multC (l r) (* (interp l) (interp r))]))

(interp (parse (read)))</code><p>这样，我们就完成了第一个解释器。</p><h1>A first taste of desugaring</h1><blockquote><p>语法糖的概念这条微博讲的比较清晰： <a href="http://weibo.com/1196343093/EmrdvbRDX" data-editable="true" data-title="寒冬winter的微博">寒冬winter的微博</a></p></blockquote><p>我们来给写好的解释器添加减法。由于我们的语言已经包含了数、加法和乘法，用这些操作就可以定义减法了：</p><br><code lang="racket">a - b = a + -1 × b</code><p>好的，这很简单！但是我们要怎样将它变成可运行的代码呢。首先，我们面临一个决定，将减法操作符放在哪？ 将其像其它两个操作符一样处理，在现有的 ArithC 数据类型中添加一条规则？——这种想法很诱人。</p><p>想一下：通过修改 ArithC 的这种做法有什么不好的地方呢？</p><p>这会导致几个问题：</p><ul><li>首先，显然地，我们将需要修改所有处理了 ArithC 的代码。就目前而言，还很简单，只涉及到了我们的 parser 和 解释器 。 但是如果在一个更为复杂的语言中，修改这种基本的数据结构会导致大量代码的修改。</li><li>其次，要添加的结构是可以用已实现的语法结构定义的，去修改已有数据结构的方式让人觉得代码不够模块化。 最后一点是修改 ArithC 这种行为让人感觉比较微妙，它有概念上的错误。因为 ArithC 是我们语言的核心部分。而减法 更应该是用户交互的部分，表层语言，添加它是为了让减法更容易使用，而不是添加了一个新的语言特性。</li></ul><p>因此，我们尝试定义一个新的数据类型来反应我们的表层语言语法结构：</p><br><code lang="racket">(define-type ArithS
  [numS (n : number)]
  [plusS (l : ArithS) (r : ArithS)]
  [bminusS (l : ArithS) (r : ArithS)]
  [multS (l : ArithS) (r : ArithS)])</code><p>它和 ArithC 看起来基本相同，遵从了相似的递归结构。</p><p>有了这个数据结构，我们需要做两件事。</p><p>修改 parser 去构造 ArithS （而不是 ArithC ）；实现 desugar 函数将 ArithS 转换成 ArithC 。</p><p>desugar 代码其实是比较容易的：</p><br><code lang="racket">(define (desugar [as : ArithS]) : ArithC
  (type-case ArithS as
             [numS (n) (numC n)]
             [plusS (l r) (plusC (desugar l)
                                 (desugar r))]
             [multS (l r) (multC (desugar l)
                                 (desugar r))]
             [bminusS (l r) (plusC (desugar l)
                                   (multC (numC -1) (desugar r)))]))</code><p>⚠️注意上面代码，常见错误是忘了递归的对 l 和 r 进行 desugar 操作。</p><p>而 parse 代码，读者可以仿照前面 parse 输入到 ArithC 的代码书写。下面贴出这部分完整代码：</p><br><code lang="racket">#lang plai-typed

(define-type ArithC
  [numC (n : number)]
  [plusC (l : ArithC) (r : ArithC)]
  [multC (l : ArithC) (r : ArithC)])

(define-type ArithS
  [numS (n : number)]
  [plusS (l : ArithS) (r : ArithS)]
  [bminusS (l : ArithS) (r : ArithS)]
  [multS (l : ArithS) (r : ArithS)])

(define (desugar [as : ArithS]) : ArithC
  (type-case ArithS as
    [numS (n) (numC n)]
    [plusS (l r) (plusC (desugar l)
                        (desugar r))]
    [multS (l r) (multC (desugar l)
                        (desugar r))]
    [bminusS (l r) (plusC (desugar l)
                          (multC (numC -1) (desugar r)))]))

(define (parse [s : s-expression]) : ArithS
  (cond
    [(s-exp-number? s) (numS (s-exp-&gt;number s))]
    [(s-exp-list? s)
     (let ([sl (s-exp-&gt;list s)])
       (case (s-exp-&gt;symbol (first sl))
         [(+) (plusS (parse (second sl)) (parse (third sl)))]
         [(*) (multS (parse (second sl)) (parse (third sl)))]
         [(-) (bminusS (parse (second sl)) (parse (third sl)))]
         [else (error 'parse "invalid list input")]))]
    [else (error 'parse "invalid input")]))

(define (interp [a : ArithC]) : number
  (type-case ArithC a
    [numC (n) n]
    [plusC (l r) (+ (interp l) (interp r))]
    [multC (l r) (* (interp l) (interp r))]))

(interp (desugar (parse (read))))</code><h2>扩展：取负数操作（unary negation）</h2><p>下面考虑一个新的扩展语法，取负数操作。它使得你需要对 parser 进行一定修整，当你读到 - 符号时， 你需要往前读（look ahead）以判断它是减法还是取负操作。这不是最有趣的部分！</p><p>实现取负操作可以有几种语法糖。很自然的我们会想到：</p><br><code lang="text">-b = 0 - b</code><p>或者还可以这样：</p><br><code lang="text">-b = 0 + -1 × b</code><p>你觉得这两种中哪个更好呢？为什么？</p><p>大家可能希望使用第一种方式，因为它看起来更为简单。使用该方式，首先扩展 ArithS 数据类型， 添加一个取负的规则：</p><br><code lang="racket">[uminusS (e : ArithS)]</code><p>对应 desugar 的实现也很直接：</p><br><code lang="racket">[(uminusS (e) (desugar (bminusS (numS 0) e)))]</code><p>检查看有没有类型错误。上面的式子是对的。要注意不要 desugar 表达式 e 了， bminusS 接受的两个参数都是 ArithS 而 e 已经是 ArithS 类型了。这种将输入形式递归地嵌入到其它形式中是 desugaring 工具一种常见模式；它被称为 macro （这里的 macro 就是 umiunsS 的定义）。</p><p>然而该定义存在两个问题：</p><p>首先，该递归是 generative 的，我们得对其进行特别关注。我们可能会希望使用下面这种方式来重写它：</p><br><code lang="racket">[uminusS (e) (bminusS (numS 0) (desugar e))]</code><p>它确实消除了 generativity 。</p><blockquote><p>如果你没听过 generative 递归，可以阅读 HTDP 的<a href="http://www.ccs.neu.edu/home/matthias/HtDP2e/part_five.html" data-editable="true" data-title="这一节">这一节</a>。 简单来说，generative 递归过程，递归调用的输入为函数完整的输入而不是一个子问题。</p></blockquote><p>⚠️注意：很不幸的是，上面的转换有问题，试着找出问题吧，找不出的话，运行一下试试。</p><p>第二个问题是，它依赖于 bminusS 的意义，如果 bminusS 的意义发生变化，我们的 bminusS 意义也就发生了变化。</p><p>你可能会说，减法的意义不可能发生改变；但并不是这样的，它的实现可能会改变。例如，开发者决定为减法操作打印日志。</p><p>很幸运，这个例子我们还可以使用下面这种展开：</p><br><code lang="text">-b = -1 × b</code>
<script async defer="defer" src="https://www.googletagmanager.com/gtag/js?id=UA-7909075-5"></script>
<script>
window.dataLayer = window.dataLayer || [];
function gtag(){ dataLayer.push(arguments); }
gtag('js', new Date());
gtag('config', 'UA-7909075-5');
</script>
<script>
var _hmt = _hmt || [];
</script>
<script async defer="defer" src="https://hm.baidu.com/hm.js?e3d40295e416616ddc21287da9646d31"></script>
</body>
</html>
