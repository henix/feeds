<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<title>柯里化的前生今世（五）：动态作用域</title>
</head>
<body>
<p><a href="https://zhuanlan.zhihu.com/p/34064286">原文</a></p>
<h2><b>关于</b></h2><p>本文是系列文章中的第五篇，发布在<a href="https://zhuanlan.zhihu.com/c_166576777">业余程序员的个人修养</a>这个专栏中：<br><a href="https://zhuanlan.zhihu.com/p/34060802">柯里化的前生今世（一）：函数面面观</a> <br><a href="https://zhuanlan.zhihu.com/p/34063576">柯里化的前生今世（二）：括号神教</a> <br><a href="https://zhuanlan.zhihu.com/p/34063805">柯里化的前生今世（三）：语言和同像性</a> <br><a href="https://zhuanlan.zhihu.com/p/34064164">柯里化的前生今世（四）：编译器与解释器</a></p><p>在上一篇中，我们介绍了编译器和解释器，抽象语法树与S表达式的关系，并且我们还打算写一个极简的<a href="https://zh.wikipedia.org/wiki/%E8%87%AA%E5%BE%AA%E7%92%B0%E7%9B%B4%E8%AD%AF%E5%99%A8">元循环解释器</a>。通过写这个解释器，一方面我们可以熟悉Racket语言，另一方面，可以帮助我们从实现角度来理解某些高级概念。</p><h2><b>概览</b></h2><p>（废话少说言归正传，一言不合就贴代码。。<br>这段代码我们用Racket实现了一个具有动态<a href="https://zh.wikipedia.org/wiki/%E4%BD%9C%E7%94%A8%E5%9F%9F">作用域</a>Lisp方言的解释器。<br>我们没有使用Racket的模式匹配<a href="https://docs.racket-lang.org/reference/match.html">match</a>，而是遵循一般教学用的常规写法，<br>把S表达式分为3种：符号，自求值表达式，列表（函数定义，函数调用）。</p><p>如果是符号，我们就得去“环境”中查找符号的值。<br>如果是自求值表达式，我们就直接返回它。（我们这里的自求值表达式只有数字<br>如果是函数定义，我们就用一个数据结构把参数和函数体存起来。<br>如果是函数调用，我们就先用实参“扩展”调用时的“环境”，然后让函数体在这个环境中求值。</p><code lang="racket">#lang racket

; tool

(struct function 
  (param body))

(define (create-frame)
  (make-hash))

(define (extend-frame frame key value)
  (hash-set! frame key value))

(define (extend-env env frame)
  (cons frame env))

(define (get-symbol-value env key)
  (let lookup-env
    ((env env))
    (if (null? env)
        (error 'get-symbol-value "failed to find symbol")
        (let ((head-frame (car env)))
          (if (hash-has-key? head-frame key)
              (hash-ref head-frame key '())
              (lookup-env (cdr env)))))))

(define (handle-decision-tree tree exp)
  (if (null? tree)
      (error 'handle-decision-tree "failed to make decision")
      (let* ((head (car tree))
             (predicator (car head))
             (decision (cadr head)))
        (if (predicator exp)
            (if (not (list? decision))
                (decision exp)
                (handle-decision-tree decision exp))
            (handle-decision-tree (cdr tree) exp)))))

; env

(define *env* `(,(create-frame)))

; main

(define (eval-exp exp)
  (handle-decision-tree 
   `((,is-symbol? ,eval-symbol)
     (,is-self-eval-exp? ,eval-self-eval-exp)
     (,is-list?
      ((,is-lambda? ,eval-lambda)
       (,is-function-call-list? ,eval-function-call-list))))
   exp))

(define (is-symbol? exp)
  (display "is-symbol?\n")
  (symbol? exp))

(define (eval-symbol exp)
  (display "eval-symbol\n")
  (get-symbol-value *env* exp))

(define (is-self-eval-exp? exp)
  (display "is-self-eval-exp?\n")
  (number? exp))

(define (eval-self-eval-exp exp)
  (display "eval-self-eval-exp\n")
  exp)

(define (is-list? exp)
  (display "is-list?\n")
  (list? exp))

(define (is-lambda? exp)
  (display "is-lambda?\n")
  (eq? (car exp) 'lambda))

(define (eval-lambda exp)
  (display "eval-lambda\n")
  (let ((param (caadr exp))
        (body (caddr exp)))
    (function param body)))

(define (is-function-call-list? exp)
  (display "is-function-call-list?\n")
  #t)

(define (eval-function-call-list exp)
  (display "eval-function-call-list\n")
  (let* ((fn (eval-exp (car exp)))
         (arg (eval-exp (cadr exp)))

         (body (function-body fn))
         (param (function-param fn))

         (frame (create-frame)))

    (extend-frame frame param arg)

    (let* ((env *env*)
           (value '()))
      (set! *env* (extend-env *env* frame))
      (set! value (eval-exp body))
      (set! *env* env)
      value)))</code><h2><b>测试</b></h2><code lang="racket">(display (eval-exp '1))

(display "\n\n")
(display (eval-exp '(lambda (x) x)))

(display "\n\n")
(display (eval-exp '((lambda (x) x) 
                     1)))

(display "\n\n")
(display (eval-exp '((lambda (x)
                       ((lambda (y) x)
                        2))
                     1)))

(display "\n\n")
(display (eval-exp '((lambda (x)
                       ((lambda (f)
                          ((lambda (x)
                             (f 3))
                           2))
                        (lambda (z) x)))
                     1)))</code><h2><b>名词解释</b></h2><p>今天这篇文章中提到了很多“耳熟能详”的概念名词，下面通过代码来解释它们。</p><p><b>环境</b></p><code lang="racket">(define *env* `(,(create-frame)))</code><p>环境是帧（frame）的列表，其中“ ` ”是反引用（<a href="https://en.wikipedia.org/wiki/Quasi-quotation">quasi-quotation</a>），<br>反引用是一种模板，便于构建列表。</p><code lang="racket">`(,(create-frame))
&lt;=&gt; (list (create-frame))</code><p><code class="inline">(create-frame)</code>创建了一个帧，然后把它放到列表中，该列表目前只有一个元素。<br>这个帧的列表就是环境。</p><p>我们这里定义了一个全局变量<code class="inline">*env*</code>，<br>任何一个函数调用和返回，都会影响<code class="inline">*env*</code>。</p><p><b>帧（frame）</b></p><p>帧是键值对的集合，表示了符号与值的映射关系，我们称之为绑定（binding）。</p><code lang="racket">(define (create-frame)
  (make-hash))

(define (extend-frame frame key value)
  (hash-set! frame key value))</code><p>具体实现中，我们使用了哈希表来存储键值对。<br>帧是可以被扩展的，<code class="inline">extend-frame</code>函数用新的键值对扩展了已有的帧。</p><p><b>环境的扩展</b></p><code lang="racket">(define (extend-env env frame)
  (cons frame env))</code><p>我们知道了，环境是帧的列表，环境还可以用新的帧来进行扩展。<br>这样环境中就可以包含很多个帧了，新的帧会放在列表头部。</p><p><b>求值</b></p><p>对一个符号进行求值，就是去环境中查找该符号所对应的值。<br>该符号与值的映射关系，可能体现在环境中的不同帧中，<br>我们取列表头部开始第一个映射关系。<br>即，我们认为后加入的帧，覆盖（shadow）了以前的绑定（binding）关系。</p><p>具体实现如下，我们递归的使用<code class="inline">lookup-env</code>进行查找。</p><code lang="racket">(define (get-symbol-value env key)
  (let lookup-env
    ((env env))
    (if (null? env)
        (error 'get-symbol-value "failed to find symbol")
        (let ((head-frame (car env)))
          (if (hash-has-key? head-frame key)
              (hash-ref head-frame key '())
              (lookup-env (cdr env)))))))</code><p><b>函数</b></p><p>函数是一个数据结构，</p><code lang="racket">(struct function 
  (param body))</code><p>该结构的每个实例都表示一个具体的函数，<br>其中包含两个字段，<code class="inline">param</code>表示参数列表，<code class="inline">body</code>表示函数体。</p><p>当我们遇到函数定义表达式时，我们这样定义了一个函数（数据结构）。</p><code lang="racket">(define (eval-lambda exp)
  (display "eval-lambda\n")
  (let ((param (caadr exp))
        (body (caddr exp)))
    (function param body)))</code><p>当我们求值一个函数调用表达式时，<br>（1）我们先把<code class="inline">param</code>和<code class="inline">body</code>字段提取出来<br>（2）创建一个新的帧，其中保存了形参到值的映射关系<br>（3）用新的帧扩展全局环境<br>（4）在全局环境中求值函数体<br>（5）函数返回后，恢复原来的环境</p><p>我们看到，这里有一个帧的添加和删除操作，<br>如果函数调用中还有另一个函数调用，就会在帧删除之前又添加一个帧。<br>实际上，这是一个入栈弹栈操作。</p><p>因此，我们说环境的具体实现形式是列表，但在数据结构上，它构成了一个栈。<br>帧在这种情况下，也成为栈帧（stack frame）。</p><code lang="racket">(define (eval-function-call-list exp)
  (display "eval-function-call-list\n")
  (let* ((fn (eval-exp (car exp)))
         (arg (eval-exp (cadr exp)))

         (body (function-body fn))
         (param (function-param fn))

         (frame (create-frame)))

    (extend-frame frame param arg)

    (let* ((env *env*)
           (value '()))
      (set! *env* (extend-env *env* frame))
      (set! value (eval-exp body))
      (set! *env* env)
      value)))</code><h2><b>求值过程</b></h2><p>现在我们对相关概念都进行了解释，我们来看看整个求值过程吧。</p><p>当我们遇到<code class="inline">(eval-exp '1)</code>时，会发现<code class="inline">1</code>是一个数字，我们断定为它是一个自求值表达式，直接返回它，因此<code class="inline">(eval-exp '1)</code>的值就是<code class="inline">1</code>了。</p><p>当我们遇到<code class="inline">(eval-exp '(lambda (x) x))</code>时，会发现这是一个函数定义表达式，我们拿到它所定义函数的参数列表<code class="inline">param =&gt; (x)</code>和函数体<code class="inline">body =&gt; x</code>，创建了一个结构——function，来存储它们。</p><p>当我们遇到<code class="inline">(display (eval-exp '((lambda (x) x) 1)))</code>，我们发现这是一个函数调用表达式（实际上，如果不是其他类型的表达式，我们都认为是函数调用），首先得到函数对象，然后再得到函数对象中保存的参数列表和函数体，在扩展中环境中求值函数体，因为这时环境中<code class="inline">x =&gt; 1</code>，所以<code class="inline">body =&gt; x =&gt; 1</code>，结果为<code class="inline">1</code>。</p><p>同理，我们可以分析其他的测试用例。</p><h2><b>动态作用域</b></h2><p>我们来看最后一个测试用例，</p><code lang="racket">(eval-exp '((lambda (x)
              ((lambda (f)
                 ((lambda (x)
                    (f 3))
                  2))
               (lambda (z) x)))
            1))</code><p>首先<code class="inline">x =&gt; 1</code>，然后<code class="inline">f =&gt; (lambda (z) x)</code>，<br>在<code class="inline">f</code>被调用<code class="inline">(f 3)</code>之前，我们覆盖了<code class="inline">x</code>的值，<code class="inline">x =&gt; 2</code>，<br>这时候，求值<code class="inline">f</code>的函数体，得到了<code class="inline">(f 3) =&gt; 2</code>。</p><p>我们看到<code class="inline">f =&gt; (lambda (z) x)</code>函数体中的<code class="inline">x</code>，<br>是在<code class="inline">f</code>被调用的环境中求值的，<code class="inline">f</code>在不同的位置被调用，<code class="inline">x</code>可能不同。</p><code lang="racket">((lambda (x)
   (f 3))
 ?)</code><p>我们可以给<code class="inline">x</code>任意的值。</p><p>这样有利有弊，<br>因为实现起来简单，很多古老的语言都是动态作用域的，<br>可是会带来上面所说的不确定性，类库的设计者无法预测所有被调用的场景，<br>很容易出现问题。</p><p>后来从Scheme开始，人们提出了词法作用域（静态作用域）的概念，<br>让<code class="inline">f =&gt; (lambda (z) x)</code>中的<code class="inline">x</code>始终等于<code class="inline">f</code>被定义处的<code class="inline">x =&gt; 1</code>，<br>这就避免了上述问题，当我们想确定<code class="inline">x</code>的值时，<br>只需要去<code class="inline">f</code>被定义的地方找一下，看看代码就行了。<br>（通过看看代码，分析文本，因此称为“词法的”。。</p><h2><b>下文</b></h2><p>本文我们实现了一个简单的具有动态作用域的Lisp方言，<br>下文，我们将在它的基础上进行修改，让它支持词法作用域，<br>我们会引入词法环境和闭包的概念，请拭目以待吧。。</p><hr><h2><b>参考</b></h2><p><a href="https://docs.racket-lang.org/reference/index.html">The Racket Reference</a> <br><a href="https://book.douban.com/subject/1148282/">SICP : 4.1元循环求值器</a> <br><a href="http://www.cs.rpi.edu/academics/courses/fall00/ai/scheme/reference/schintro-v14/schintro_toc.html">An Introduction to Scheme and its Implementation</a></p>
<script async defer="defer" src="https://www.googletagmanager.com/gtag/js?id=UA-7909075-5"></script>
<script>
window.dataLayer = window.dataLayer || [];
function gtag(){ dataLayer.push(arguments); }
gtag('js', new Date());
gtag('config', 'UA-7909075-5');
</script>
<script>
var _hmt = _hmt || [];
</script>
<script async defer="defer" src="https://hm.baidu.com/hm.js?e3d40295e416616ddc21287da9646d31"></script>
</body>
</html>
