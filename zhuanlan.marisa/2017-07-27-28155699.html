<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<title>Erlang入门教程 - 6. Maps</title>
</head>
<body>
<p><a href="https://zhuanlan.zhihu.com/p/28155699">原文</a></p>
<p>Maps是一堆键值关联对的集合。键值对使用 “#{” 和”}”进行封装。要创建一个key关联value 42可以这么做：</p><code lang="erlang">&gt; #{ "key" =&gt; 42 }.
#{"key" =&gt; 42}</code><p>让我们通过下面这个使用有趣特性的例子直接跳到一个深度。</p><p>下面的例子显示了如何通过maps引用颜色和α通道进而计算α混合(alpha blending)。在color.erl文件中输入下面代码：</p><code lang="erlang">-module(color).

-export([new/4, blend/2]).

-define(is_channel(V), (is_float(V) andalso V &gt;= 0.0 andalso V =&lt; 1.0)).

new(R,G,B,A) when ?is_channel(R), ?is_channel(G),
                  ?is_channel(B), ?is_channel(A) -&gt;
    #{red =&gt; R, green =&gt; G, blue =&gt; B, alpha =&gt; A}.

blend(Src,Dst) -&gt;
    blend(Src,Dst,alpha(Src,Dst)).

blend(Src,Dst,Alpha) when Alpha &gt; 0.0 -&gt;
    Dst#{
        red   := red(Src,Dst) / Alpha,
        green := green(Src,Dst) / Alpha,
        blue  := blue(Src,Dst) / Alpha,
        alpha := Alpha
    };
blend(_,Dst,_) -&gt;
    Dst#{
        red   := 0.0,
        green := 0.0,
        blue  := 0.0,
        alpha := 0.0
    }.

alpha(#{alpha := SA}, #{alpha := DA}) -&gt;
    SA + DA*(1.0 - SA).

red(#{red := SV, alpha := SA}, #{red := DV, alpha := DA}) -&gt;
    SV*SA + DV*DA*(1.0 - SA).
green(#{green := SV, alpha := SA}, #{green := DV, alpha := DA}) -&gt;
    SV*SA + DV*DA*(1.0 - SA).
blue(#{blue := SV, alpha := SA}, #{blue := DV, alpha := DA}) -&gt;
    SV*SA + DV*DA*(1.0 - SA).</code><p>编译并测试：</p><code lang="erlang">&gt; c(color).
{ok,color}
&gt; C1 = color:new(0.3,0.4,0.5,1.0).
#{alpha =&gt; 1.0,blue =&gt; 0.5,green =&gt; 0.4,red =&gt; 0.3}
&gt; C2 = color:new(1.0,0.8,0.1,0.3).
#{alpha =&gt; 0.3,blue =&gt; 0.1,green =&gt; 0.8,red =&gt; 1.0}
&gt; color:blend(C1,C2).
#{alpha =&gt; 1.0,blue =&gt; 0.5,green =&gt; 0.4,red =&gt; 0.3}
&gt; color:blend(C2,C1).
#{alpha =&gt; 1.0,blue =&gt; 0.38,green =&gt; 0.52,red =&gt; 0.51}</code><p><br></p><p>This example warrants some explanation:</p><code lang="erlang">-define(is_channel(V), (is_float(V) andalso V &gt;= 0.0 andalso V =&lt; 1.0)).</code><p>首先定义一个宏is_channel用于guard测试。为了方便起见这里使用它减少语法的杂糅。关于宏的更多信息,参见Preprocessor[8]。</p><code lang="erlang">new(R,G,B,A) when ?is_channel(R), ?is_channel(G),
                  ?is_channel(B), ?is_channel(A) -&gt;
    #{red =&gt; R, green =&gt; G, blue =&gt; B, alpha =&gt; A}.</code><p>函数new/4创建一个map并且让键red，green，blue，alpha关联一个初始值。 在这里, 将宏<b>?is_channel</b>作用于每个参数，确保只有0.0到1.0之间的浮点值才被允许，只有在创建新map的时候才允许使用<b>=&gt;</b>运算符。</p><p>使用new/4创建一个color，然后将colors传递给blend/2，它就能计算出混合后的颜色。</p><p>首先blend/2做的事是计算混合后的α通道</p><code lang="erlang">alpha(#{alpha := SA}, #{alpha := DA}) -&gt;
    SA + DA*(1.0 - SA).</code><p>使用<b>:=</b>运算符获取参数中键alpha所关联的value。参数中的其他键会被忽略掉，只要求键alpha。 <br>函数red/2, blue/2,green/2也是如此：</p><code lang="erlang">red(#{red := SV, alpha := SA}, #{red := DV, alpha := DA}) -&gt;
    SV*SA + DV*DA*(1.0 - SA).</code><p>这里不同点是这几个函数获取参数中的两个键，其它的被忽略。</p><p>最后，让我们在blend/3中返回混合后的颜色：</p><code lang="erlang">blend(Src,Dst,Alpha) when Alpha &gt; 0.0 -&gt;
    Dst#{
        red   := red(Src,Dst) / Alpha,
        green := green(Src,Dst) / Alpha,
        blue  := blue(Src,Dst) / Alpha,
        alpha := Alpha
    };</code><p>Dst使用新通道值进行更新。更新一个已有的键关联的值的语法是使用<b>:=</b>运算符。</p>
<script async defer="defer" src="https://www.googletagmanager.com/gtag/js?id=UA-7909075-5"></script>
<script>
window.dataLayer = window.dataLayer || [];
function gtag(){ dataLayer.push(arguments); }
gtag('js', new Date());
gtag('config', 'UA-7909075-5');
</script>
<script>
var _hmt = _hmt || [];
</script>
<script async defer="defer" src="https://hm.baidu.com/hm.js?e3d40295e416616ddc21287da9646d31"></script>
</body>
</html>
