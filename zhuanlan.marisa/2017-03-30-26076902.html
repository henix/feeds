<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<title>(译) Algorithm W Step By Step</title>
</head>
<body>
<p><a href="https://zhuanlan.zhihu.com/p/26076902">原文</a></p>
<p>原作者 Martin Grabmüller</p><p>未来：代码注释/其他语言转写；HM Type System 的前置知识。</p><h2>导读</h2><p>在这篇文章里，我们会用 Haskell 完整地实现用于 Hindley-Milner 多态类型推断(Hindley-Milner polymorphic type inference)的经典算法： W。</p><h2>1. 简介</h2><p>类型推断是一件很棘手的事情，也很难入门。因为大多数相关资料都是关于更加高深的话题的，例如 rank-N polymorphism，predicative/impredicative type systems，universal and existential types 等。我已经通过实际解决问题学到了一点东西，因此我决定写一篇关于类型推导的基础教程——实现一个最基础的类型推断算法，用于基本的类型检查（例如 ML 或者 Haskell）。</p><p>这里研究的类型推导算法是由 Milner 提出的 Algorithm W。[1]</p><p>有关此算法的变种、拓展或者更多信息，请参阅[2]。</p><p>本教程也受到[3]的启发。</p><h2>2. Algorithm W</h2><p>让我们从必须的 import 开始。为了表示环境（Environment，又名上下文 Context）以及替换（Substitution），我们需要 import Data.Map。类型变量的集合等将由 Data.Set 表示。</p><p>我们也需要一些 monad transformer，因此我们需要 import 关于 monad 的库。</p><p>Text.PrettyPrint 提供了一些让输出内容格式化并且缩进良好的方法，PrettyPrint 的代码在附录 A。（译注：可以直接使用Pretty Print，以专注于算法本身。）</p><code lang="haskell">import qualified Data.Map as Map
import qualified Data.Set as Set


import Control.Monad.Error
import Control.Monad.Reader
import Control.Monad.State


import qualified Text.PrettyPrint as PP</code><h2>2.1 准备</h2><p>我们先定义一些抽象语法，包括 expressions (Exp)，types (Type) 以及 type schemes (Scheme)。</p><p>Scheme ∀a1, ..., an.t 表示的是一系列全称量词（Universal Quantifier）的多态类型变量。</p><code lang="haskell">data Exp = EVar String
        | ELit Lit
        | EApp Exp Exp
        | EAbs String Exp
        | ELet String Exp Exp
        deriving (Eq, Ord)


data Lit = LInt Integer
        | LBool Bool
        deriving (Eq, Ord)


data Type = TVar String
        | TInt
        | TBool
        | TFun Type Type
        deriving (Eq, Ord)


data Scheme = Scheme [String] Type</code><p>为了提供可读的输出以及报错信息，我们定义了一些 pretty-printing 有关的方法（附录 A）。</p><p>我们需要确定类型的自由类型变量。ftv (free type variables) 方法实现了这一操作。我们会在 Types 这个类型类里实现这个方法，因为 type environments 也需要它（下文会提到）。另一个对 types, type schemes 等的有用操作是 apply 一个替换。一个替换仅仅替换自由类型变量，所以 type scheme 里的量化类型变量不受影响。</p><code lang="haskell">class Types a where
    ftv    ::  a -&gt; Set.Set String
    apply  ::  Subst -&gt; a -&gt; a


instance Types Type where
    ftv (TVar n)      =  Set.singleton n
    ftv TInt          =  Set.empty
    ftv TBool         =  Set.empty
    ftv (TFun t1 t2)  =  ftv t1 `Set.union` ftv t2
    apply s (TVar n)      =  case Map.lookup n s of
                               Nothing  -&gt; TVar n
                               Just t   -&gt; t
    apply s (TFun t1 t2)  = TFun (apply s t1) (apply s t2)
    apply s t             =  t


instance Types Scheme where
    ftv (Scheme vars t)      =  (ftv t) `Set.difference` (Set.fromList vars)
    apply s (Scheme vars t)  =  Scheme vars (apply (foldr Map.delete s vars) t)</code><p>如果把 types 方法扩展到列表有时是有用的。</p><code lang="haskell">instance Types a =&gt; Types [a] where
    apply s  =  map (apply s)
    ftv l    =  foldr Set.union Set.empty (map ftv l)</code><p>现在我们定义替换，它被定义为一个有限长的类型变量到类型的映射。</p><code lang="haskell">type Subst = Map.Map String Type
nullSubst  ::  Subst
nullSubst  =   Map.empty
composeSubst         :: Subst -&gt; Subst -&gt; Subst
composeSubst s1 s2   = (Map.map (apply s1) s2) `Map.union` s1</code><p>Type environments，也就是 Γ，是一个从 term variables 到相应 type schemes 的映射。</p><code lang="haskell">newtype TypeEnv = TypeEnv (Map.Map String Scheme)</code><p>我们定义一些有关 type environments 的方法。被称为 remove 的操作 Γ\x 移除 Γ 中有关 x 的绑定。</p><code lang="haskell">remove                    ::  TypeEnv -&gt; String -&gt; TypeEnv
remove (TypeEnv env) var  =  TypeEnv (Map.delete var env)
instance Types TypeEnv where
    ftv (TypeEnv env)      =  ftv (Map.elems env)
    apply s (TypeEnv env)  =  TypeEnv (Map.map (apply s) env)</code><p>The function |generalize| abstracts a type over all type variables</p><p>which are free in the type but not free in the given type environment.</p><p>（这句话读原文吧……）</p><code lang="haskell">generalize        ::  TypeEnv -&gt; Type -&gt; Scheme
generalize env t  =   Scheme vars t
  where vars = Set.toList ((ftv t) `Set.difference` (ftv env))</code><p>Type scheme 实例化之类的操作，需要给新引入的类型变量一些新名称。通过合适的生成新名称的 monad 来实现这一想法。它同时还能传递 dynamically scoped environment，处理错误以及执行 I/O 操作。但这里暂时不介绍细节。</p><code lang="haskell">data TIEnv = TIEnv  {}
data TIState = TIState { tiSupply :: Int }
type TI a = ErrorT String (ReaderT TIEnv (StateT TIState IO)) a
runTI :: TI a -&gt; IO (Either String a, TIState)
runTI t = 
    do (res, st) &lt;- runStateT (runReaderT (runErrorT t) initTIEnv) initTIState
       return (res, st)
  where initTIEnv = TIEnv
        initTIState = TIState{tiSupply = 0}
newTyVar :: String -&gt; TI Type
newTyVar prefix =
    do  s &lt;- get
        put s{tiSupply = tiSupply s + 1}
        return (TVar  (prefix ++ show (tiSupply s)))</code><p>Instantiate 方法替换一个 type scheme 里所有的绑定类型变量为新的类型变量。</p><code lang="haskell">instantiate :: Scheme -&gt; TI Type
instantiate (Scheme vars t) = do  nvars &lt;- mapM (\ _ -&gt; newTyVar "a") vars
                                  let s = Map.fromList (zip vars nvars)
                                  return $ apply s t</code><p>下面的是类型的 unification 方法。对于 t1 t2 两个类型，这个方法给出最通用（general）的合一（unifier）。</p><p>合一是一个替换S，它使得 S(t1) ≡ S(t2)。 方法 varBind 将一个类型变量绑定为一个类型，并且返回这个绑定的替换，并且不会将一个变量绑定到自身。</p><code lang="haskell">mgu :: Type -&gt; Type -&gt; TI Subst
mgu (TFun l r) (TFun l' r')  =  do  s1 &lt;- mgu l l'
                                    s2 &lt;- mgu (apply s1 r) (apply s1 r')
                                    return (s1 `composeSubst` s2)
mgu (TVar u) t               =  varBind u t
mgu t (TVar u)               =  varBind u t
mgu TInt TInt                =  return nullSubst
mgu TBool TBool              =  return nullSubst
mgu t1 t2                    =  throwError $ "types do not unify: " ++ show t1 ++ 
                                " vs. " ++ show t2
varBind :: String -&gt; Type -&gt; TI Subst
varBind u t  | t == TVar u           =  return nullSubst
             | u `Set.member` ftv t  =  throwError $ "occurs check fails: " ++ u ++
                                         " vs. " ++ show t
             | otherwise             =  return (Map.singleton u t)</code><h2>2.2 类型推断</h2><p>方法 tiLit 用于推断字面量。</p><code lang="haskell">tiLit :: Lit -&gt; TI (Subst, Type)
tiLit (LInt _)   =  return (nullSubst, TInt)
tiLit (LBool _)  =  return (nullSubst, TBool)</code><p>方法 ti 用于推断表达式的类型。 Type environment 必须包含表达式里所有自由变量的绑定。返回值里记录了表达式里的类型约束的替换以及表达式的类型。</p><code lang="haskell">ti        ::  TypeEnv -&gt; Exp -&gt; TI (Subst, Type)
ti (TypeEnv env) (EVar n) = 
    case Map.lookup n env of
       Nothing     -&gt;  throwError $ "unbound variable: " ++ n
       Just sigma  -&gt;  do  t &lt;- instantiate sigma
                           return (nullSubst, t)
ti _ (ELit l) = tiLit l
ti env (EAbs n e) =
    do  tv &lt;- newTyVar "a"
        let TypeEnv env' = remove env n
            env'' = TypeEnv (env' `Map.union` (Map.singleton n (Scheme [] tv)))
        (s1, t1) &lt;- ti env'' e
        return (s1, TFun (apply s1 tv) t1)
ti env exp@(EApp e1 e2) =
    do  tv &lt;- newTyVar "a"
        (s1, t1) &lt;- ti env e1
        (s2, t2) &lt;- ti (apply s1 env) e2
        s3 &lt;- mgu (apply s2 t1) (TFun t2 tv)
        return (s3 `composeSubst` s2 `composeSubst` s1, apply s3 tv)
    `catchError`
    \e -&gt; throwError $ e ++ "\n in " ++ show exp
ti env (ELet x e1 e2) =
    do  (s1, t1) &lt;- ti env e1
        let TypeEnv env' = remove env x
            t' = generalize (apply s1 env) t1
            env'' = TypeEnv (Map.insert x t' env')
        (s2, t2) &lt;- ti (apply s1 env'') e2
        return (s1 `composeSubst` s2, t2)</code><p>这是类型推断器的入口，只是简单地调用了 ti 并且将返回的替换应用到返回的类型上。</p><code lang="haskell">typeInference :: Map.Map String Scheme -&gt; Exp -&gt; TI Type
typeInference env e =
    do  (s, t) &lt;- ti (TypeEnv env) e
        return (apply s t)</code><h2>2.3 测试</h2><p>下面列出一些简单的表达式（部分取自[2]）用于测试这些方法。</p><code lang="haskell">e0  =  ELet "id" (EAbs "x" (EVar "x"))
        (EVar "id")
e1  =  ELet "id" (EAbs "x" (EVar "x"))
        (EApp (EVar "id") (EVar "id"))
e2  =  ELet "id" (EAbs "x" (ELet "y" (EVar "x") (EVar "y")))
        (EApp (EVar "id") (EVar "id"))
e3  =  ELet "id" (EAbs "x" (ELet "y" (EVar "x") (EVar "y")))
        (EApp (EApp (EVar "id") (EVar "id")) (ELit (LInt 2)))
e4  =  ELet "id" (EAbs "x" (EApp (EVar "x") (EVar "x")))
        (EVar "id")
e5  =  EAbs "m" (ELet "y" (EVar "m")
                 (ELet "x" (EApp (EVar "y") (ELit (LBool True)))
                       (EVar "x")))

e6  =  EApp (ELit (LInt 2)) (ELit (LInt 2))</code><p>下面这个简单的测试函数试图推断给定表达式的类型。如果成功了，它将打印出这个表达式和它的类型，否则，打印错误信息。</p><code lang="haskell">test :: Exp -&gt; IO ()
test e =
    do  (res, _) &lt;- runTI (typeInference Map.empty e)
        case res of
          Left err  -&gt;  putStrLn $ show e ++ "\n " ++ err ++ "\n"
          Right t   -&gt;  putStrLn $ show e ++ " :: " ++ show t ++ "\n"</code><h2>2.4 Main</h2><p>Main 只是简单地推断了所有示例表达式并且将它们和所推导出来的类型一同打印出来，如果失败，将打印错误信息。</p><code lang="haskell">main :: IO ()
main = mapM_ test [e0, e1, e2, e3, e4, e5, e6]
-- |Collecting Constraints|
-- |main = mapM_ test' [e0, e1, e2, e3, e4, e5]|</code><p>这就是这个类型推导算法的全部实现了。</p><h2>3. 结论</h2><p>这些 Haskell 代码是一个自包含的 Algorithm W 实现。你可以随意使用、扩展这些代码，让它能够支持更好的错误信息、类型类、类型注解等等。最终你也许会得到一个完整地 Haskell 的类型检查器。</p><h2>4. 参考</h2><p>[1] Copied from <a href="http://www.grabmueller.de/martin/www/pub/AlgorithmW.en.html">http://www.grabmueller.de/martin/www/pub/AlgorithmW.en.html</a> and edited by Wei Hu. Unfortunately the bibliography is missing. </p><p>[2] The most helpful references are <a href="http://www.cs.uu.nl/research/techreps/repo/CS-2002/2002-031.pdf">http://www.cs.uu.nl/research/techreps/repo/CS-2002/2002-031.pdf</a> Generalizing Hindley-Milner Type Inference Algorithms, and Chapter 22 of TAPL.</p><p>[3] A Cornell course touchs on this topic and gives an OCaml implementation. <a href="http://www.cs.cornell.edu/Courses/cs3110/2009fa/lectures/lec26a.htm">Lecture 26: Type Inference and Unification</a></p><h2>附录 A</h2><p>本附录提供了 pretty-printing 方法和 type 的 Show 类型类实例。</p><code lang="haskell">instance Show Type where
    showsPrec _ x = shows (prType x)
prType             ::  Type -&gt; PP.Doc
prType (TVar n)    =   PP.text n
prType TInt        =   PP.text "Int"
prType TBool       =   PP.text "Bool"
prType (TFun t s)  =   prParenType t PP.&lt;+&gt; PP.text "-&gt;" PP.&lt;+&gt; prType s
prParenType     ::  Type -&gt; PP.Doc
prParenType  t  =   case t of
                      TFun _ _  -&gt; PP.parens (prType t)
                      _         -&gt; prType t
instance Show Exp where
    showsPrec _ x = shows (prExp x)
prExp                  ::  Exp -&gt; PP.Doc
prExp (EVar name)      =   PP.text name
prExp (ELit lit)       =   prLit lit
prExp (ELet x b body)  =   PP.text "let" PP.&lt;+&gt; 
                           PP.text x PP.&lt;+&gt; PP.text "=" PP.&lt;+&gt;
                           prExp b PP.&lt;+&gt; PP.text "in" PP.$$
                           PP.nest 2 (prExp body)
prExp (EApp e1 e2)     =   prExp e1 PP.&lt;+&gt; prParenExp e2
prExp (EAbs n e)       =   PP.char '\\' PP.&lt;&gt; PP.text n PP.&lt;+&gt;
                           PP.text "-&gt;" PP.&lt;+&gt;
                           prExp e

prParenExp    ::  Exp -&gt; PP.Doc
prParenExp t  =   case t of
                    ELet _ _ _  -&gt; PP.parens (prExp t)
                    EApp _ _    -&gt; PP.parens (prExp t)
                    EAbs _ _    -&gt; PP.parens (prExp t)
                    _           -&gt; prExp t
instance Show Lit where
    showsPrec _ x = shows (prLit x)
prLit            ::  Lit -&gt; PP.Doc
prLit (LInt i)   =   PP.integer i
prLit (LBool b)  =   if b then PP.text "True" else PP.text "False"
instance Show Scheme where
    showsPrec _ x = shows (prScheme x)
prScheme                  ::  Scheme -&gt; PP.Doc
prScheme (Scheme vars t)  =   PP.text "All" PP.&lt;+&gt;
                              PP.hcat 
                                (PP.punctuate PP.comma (map PP.text vars))
                              PP.&lt;&gt; PP.text "." PP.&lt;+&gt; prType t</code>
<script async defer="defer" src="https://www.googletagmanager.com/gtag/js?id=UA-7909075-5"></script>
<script>
window.dataLayer = window.dataLayer || [];
function gtag(){ dataLayer.push(arguments); }
gtag('js', new Date());
gtag('config', 'UA-7909075-5');
</script>
<script>
var _hmt = _hmt || [];
</script>
<script async defer="defer" src="https://hm.baidu.com/hm.js?e3d40295e416616ddc21287da9646d31"></script>
</body>
</html>
