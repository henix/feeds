<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<title>Haskell中的范畴之函子和自然变换</title>
</head>
<body>
<p><a href="https://zhuanlan.zhihu.com/p/25565309">原文</a></p>
<div class="title-image"><img src="https://pic2.zhimg.com/v2-924d4313803502a678947a3a87a51d29_r.jpg" alt=""></div><p>在上一篇<a href="https://zhuanlan.zhihu.com/p/25407184" data-editable="true" data-title="Haskell中的范畴 - 知乎专栏" class="">Haskell中的范畴 - 知乎专栏</a>文章中，介绍了范畴的一些基本概念。在这一篇中，我们将从单个范畴中走出来，建立范畴之间的态射，进而得到范畴的范畴。另外，由范畴之间的态射，得到函子，由函子之间的态射，得到自然变换，从而得到了函子范畴。这样我们就一步步的得到了更多的范畴，有了更多好用的性质和代数结构，在函数式编程中有了更多的用处。</p><p>下面，我们先从函子开始吧。</p><ul><li><b>函子</b><br></li></ul><br><p>若我们将范畴看成是一个更高层的范畴的对象，则两个范畴之间的态射就是函子。函子<equation>F: \mathcal{C} \to \mathcal{D}</equation>将范畴<equation>\mathcal{C}</equation>中的对象a变换为范畴<equation>\mathcal{D}</equation>中的对象F a，将范畴<equation>\mathcal{C}</equation>中的态射f变换为范畴<equation>\mathcal{D}</equation>中的态射F f，并保持了范畴的结构不变，即这两个范畴都必须满足范畴需要的定律。所以函子<equation>F: \mathcal{C} \to \mathcal{D}</equation>必须满足如下的定律：</p><equation>F\ id_\mathcal{C} &amp;=&amp; id_\mathcal{D} \\
F\ (f\ \cdot_\mathcal{C}\ g) &amp;=&amp; F\ f\ \cdot_\mathcal{D}\ F\ g</equation><br>在Haskell中，函子<equation>F</equation>用类型类Functor 以如下方式来约束。<br><code lang="haskell">class Functor f where
  fmap :: (a -&gt; b) -&gt; f a -&gt; f b

fmap id      = id                   --保持单位元不变
fmap (f . g) = fmap f . fmap g      --保持组合操作不变
</code>在Haskell的类型系统中，函子就是类型构造子，函子 F 将简单类型a 构造为复杂类型 F a，将简单类型的函数 f 变换为复杂类型的函数 F f，并满足上面的函子定律。很常见的一个简单的函子是Maybe，其将一个类型 a 变换为Maybe a，表示恰好有个类型 a 的值，或者什么都没有。函子Maybe的定义如下：<br><code lang="haskell">data Maybe a = Nothing | Just a

instance Functor Maybe where
  fmap :: (a -&gt; b) -&gt; Maybe a -&gt; Maybe b
  fmap f Nothing  = Nothing
  fmap f (Just a) = Just (f a)

-- 推导 fmap id = id
  fmap id Nothing
= Nothing
= id Nothing

  fmap id (Just a)
= Just a
= id (Just a)

-- 推导 fmap (f . g) = fmap f . fmap g
  fmap (f . g) Nothing
= Nothing
= fmap f (fmap g Nothing)
= (fmap f . fmap g) Nothing

  fmap (f . g) (Just a)
= Just (f (g a))
= fmap f (Just (g a))
= fmap f (fmap g (Just a))
= (fmap f . fmap g) (Just a)

</code><p>由此可见，Maybe的定义满足函子定律，因此是一个函子。</p><p><b>注意：类型构造子只有同时变换类型和函数，且满足函子定律时，才是一个函子。</b></p><p>函子的用处是将简单类型的函数通过类型构造子提升为复杂类型的函数，且不需要写额外的代码，使得一些基本函数可以应用到多种复杂数据类型的成员中，提高了这些基本函数的重用性。例如列表函子 [ ]，其将普通函数 (*2) 提升为将列表中的所有数乘以2得到一个新列表的函数。</p><p>当函子连接的两个范畴都是同一个时，即从一个函子到其自身的函子，称为自函子。Haskell中的函子都是Hask范畴的自函子。</p><p>范畴有对偶性，因此对于任意一个函子<equation>F: \mathcal{C} \to \mathcal{D}</equation>，都存在一个函子<equation>F^{op}: \mathcal{C}^{op} \to \mathcal{D}</equation>，是一个对偶范畴到范畴的函子，称为反变函子。我们前面介绍的函子也叫协变函子，反变函子与协变函子的不同是其改变了态射的方向，目标范畴与源范畴中的态射方向是相反的。</p><p>反变函子<equation>F^{op}</equation>用类型类ContraFunctor 以如下方式来约束。</p><br><code lang="haskell">class ContraFunctor f where
  contramap :: (b -&gt; a) -&gt; f a -&gt; f b    --态射的方向是反的

contramap id      = id                   --保持单位元不变
contramap (g . f) = fmap f . fmap g      --改变了组合次序，从对偶范畴来看则      
                                         --是保持组合操作不变
</code><p>态射是可以组合的，函子作为范畴之间的态射，那自然也是可以组合的。函子 F 和函子 G 组合后得到一个新函子 H，这三个函子的关系表示为<equation>H = F \circ G</equation>。在Haskell中有如下的形式：</p><br><code lang="haskell">newtype Compose f g a = Compose {getCompose :: f (g a)} deriving (Show)

instance (Functor f, Functor g) =&gt; Functor (Compose f g) where
  fmap k (Compose fga) = Compose $ fmap (fmap k) fga
</code><p>有时为了简化书写，我们也可以用符号:.: 来表示函子的组合操作。</p><code lang="haskell">type f :.: g = Compose f g</code>常用的数据类型State s a的类型构造子State s就是由((-&gt;) s)函子和(, s)函子组合得到的函子。<code lang="haskell">newtype State s a = State { runState :: s -&gt; (a, s) }

instance Functor (State s) where
  fmap :: (a -&gt; b) -&gt; State s a -&gt; State s b
  --fmap f st = State $ fmap (fmap f) (runState st)
  fmap f st = State $ \s -&gt; let (a, s') = runState st s in (f a, s')</code><br><ul><li><b>范畴的范畴（Cat范畴）</b></li></ul><p>将范畴看成对象，函子就是范畴之间的态射。与单位态射相对应的，是单位函子<equation>Id</equation>，就是一个没有任何改变的自函子，即有<equation>Id\ a = a</equation>。任何函子与单位函子的组合都是其自身，即有<equation>F \circ Id = F</equation>和<equation>Id \circ F = F</equation>。</p><br><code lang="haskell">newtype Identity a = Identity { runIdentity :: a }

instance Functor Identity where
  fmap f (Identity a) = Identity (f a)

-- Identity a ≅ a
idphi :: (Identity a) -&gt; a
idphi = runIdentity

idpsi :: a -&gt; (Identity a)
idpsi = Identity

-- Compose f Id a ≅ f a
fidphi :: Functor f =&gt; (Compose f Identity) a -&gt; f a
fidphi (Compose x) = fmap runIdentity x

fidpsi :: Functor f =&gt; f a -&gt; (Compose f Identity) a
fidpsi fa = Compose (fmap Identity fa)
</code><p>函子的组合运算满足结合律，多个函子组合时其结果和组合次序无关，即有<equation>(F \circ G) \circ H = F \circ (G \circ H)</equation></p><code lang="haskell">fphi :: (Functor f, Functor g, Functor h) =&gt;
        (Compose (Compose f g) h) a -&gt; (Compose f (Compose g h)) a
fphi (Compose (Compose fg_ha)) = Compose (fmap Compose fg_ha)

fpsi :: (Functor f, Functor g, Functor h) =&gt;
        (Compose f (Compose g h)) a -&gt; (Compose (Compose f g) h)) a
fpsi (Compose (Compose f_gha)) = Compose (Compose (fmap getCompose f_gha))</code><p>这样我们就有了范畴的范畴，记为Cat范畴，这也是范畴论被称为猫论的由来。在Cat范畴中，对象是范畴，态射是函子，函子的组合满足结合律，单位元是单位函子。</p><br><ul><li><b>Hom函子</b></li></ul><p>任意范畴<equation>C</equation>中两个对象 a 和 b 之间的所有态射组成的集合叫Hom-Set，记为<equation>Hom_\mathcal{C}(a, b)</equation>。范畴<equation>C</equation>中的所有Hom-Set在一起组成了一个Set范畴，我们可以看到范畴<equation>C</equation>和这个Set范畴之间存在态射，就是协变Hom函子。</p><p>在<equation>Hom_\mathcal{C}(a, b)</equation>中，如果把对象 a 固定，对象 b 是任意可变的，那我们就得到了<equation>Hom_\mathcal{C}(a, -)</equation>，这是一个从范畴<equation>C</equation>到Set范畴的函子，我们称之为协变Hom函子。</p><code lang="haskell">instance Functor ((-&gt;) a) where
  fmap f g = f . g</code><p>在<equation>Hom_\mathcal{C}(a, b)</equation>中，如果把对象 b 固定，对象 a 是任意可变的，那我们就得到了<equation>Hom_\mathcal{C}(-, b)</equation>，这是一个从范畴<equation>C^{op}</equation>到Set范畴的函子，我们称之为反变Hom函子。</p><code lang="haskell">instance ContraFunctor (Op a) where
  contramap f (Op g) = Op (g . f)</code><p>协变Hom函子和反变Hom函子使得范畴<equation>C</equation>中的对象可以在Set范畴中表示出来，这是很有意义的事情，后续我们将会具体介绍这些意义和用处。</p>在<equation>Hom_\mathcal{C}(a, b)</equation>中，如果对象 a 和对象 b 都是任意可变的，那我们就得到了<equation>Hom_\mathcal{C}(-, -)</equation>，这是一个从范畴<equation>C^{op} \times C</equation>到Set范畴的二函子，我们称之为Hom函子。这个我们后续会再次介绍。<br><ul><li><b>自然变换</b></li></ul><p>若我们有函子 F 和函子 G， 都是范畴<equation>C</equation>到范畴<equation>D</equation>之间的函子。那么对于范畴<equation>C</equation>中的任意对象 a，则在范畴<equation>D</equation>中分别有对应的对象F a 和G a，F a和G a之间存在函数<equation>t_a: F\ a \to G\ a</equation>。对于范畴<equation>C</equation>中的任意对象 b，则在范畴<equation>D</equation>中分别有对应的对象F b和G b，F b和G b之间存在函数<equation>t_b: F\ b \to G\ b</equation>。如果函数<equation>t_a</equation>和<equation>t_b</equation>满足一致性关系，即对于范畴<equation>C</equation>上的态射<equation>f: a \to b</equation>，满足交换关系<equation>G\ f \circ t_a = t_b \circ F\ f</equation>，则称变换<equation>t: F \Rightarrow G</equation>是函子 F和 G的自然变换。</p><br><p>在Haskell中，我们用如下的方式来定义自然变换。</p><code lang="haskell">newtype Nat f g = Nat {runNat :: forall a. f a -&gt; g a }
</code><p>这个定义没有一致性关系的约束，需要程序员在定义具体的自然变换时确保其满足如下所示的一致性关系。这是Haskell相比Idris弱的地方。</p><code lang="haskell">phi :: Nat f g
phi . fmap k = fmap k . phi     --一致性关系</code><p>自然变换实际上就是由函子 F 和 G 确定的范畴<equation>D</equation>上的一组态射族，这组态射族由范畴<equation>C</equation>中所有的 a 得到的范畴<equation>D</equation>中的对象对<equation>(F\ a, G\ a)</equation>之间的态射组成，而且这些态射都必须满足自然变换的一致性关系。</p><br>如果自然变换<equation>t: F \Rightarrow G</equation>的函数<equation>t_a: F\ a \to G\ a</equation>是一个同构态射，也就是对象<equation>F\ a</equation>和<equation>G\ a</equation>是同构的，且对范畴<equation>C</equation>中所有的 a 都成立，那自然变换<equation>t: F \Rightarrow G</equation>就是一个<b>自然同构</b>。且自然变换t 存在一个相反的自然变换<equation>r: G \Rightarrow F</equation>。<br><p>在Haskell中有很多常见的自然变换的例子，下面列出了几个。</p><p>safeHead 函数是列表函子到Maybe函子到自然变换</p><code lang="haskell">safeHead :: [a] -&gt; Maybe a
safeHead []     = Nothing
safeHead (x:xs) = Just x

---- 一致性关系 ----
safeHead . fmap k = fmap k . safeHead

---- 一致性关系的证明 ----
  safeHead (fmap k [])
= safeHead []
= Nothing

  fmap k (safeHead [])
= fmap k Nothing
= Nothing

  safeHead (fmap k [a, b])
= safeHead [(k a), (k b)]
= Just (k a)

  fmap k (safeHead [a, b])
= fmap k (Just a)
= Just (k a)</code><p>maybeToList函数是Maybe函子到列表函子的自然变换</p><code lang="haskell">maybeToList :: Maybe a -&gt; [a]
maybeToList Nothing = []
maybeToList Just a  = [a]

---- 一致性关系 ----
maybeToList . fmap k = fmap k . maybeToList

---- 一致性关系的证明 ----
  maybeToList (fmap k Nothing)
= maybeToList Nothing
= []

  fmap k (maybeToList Nothing)
= fmap k []
= []

  maybeToList (fmap k (Just a))
= maybeToList (Just (k a))
= [k a]

  fmap k (maybeToList (Just a))
= fmap k [a]
= [k a]</code><p>reverse函数是列表函子到其自身的自然同构，reverse函数没有改变列表的形状（即列表的长度），是一个同构函数。</p><code lang="haskell">reverse :: [a] -&gt; [a]
reverse []     = []
reverse (x:xs) = reverse xs ++ [x]

---- 一致性关系 ----
reverse . fmap k = fmap k . reverse

---- 一致性关系的证明 ----
  reverse (fmap k [a, b, c])
= reverse [k a, k b, k c]
= [k c, k b, k a]

  fmap k (reverse [a, b, c])
= fmap k [c, b, a]
= [k c, k b, k a]</code><ul><li><b>自然变换的组合</b><br></li></ul><br><p>自然变换可以看成是函子之间的态射，因此也是可以组合的。</p><p>若函子F、函子G 和函子H 都是范畴<equation>C</equation>到范畴<equation>D</equation>的函子，<equation>\alpha: F \Rightarrow G</equation>是函子F 到函子G 的自然变换，<equation>\beta: G \Rightarrow H</equation>是函子G 到函子H 的自然变换。则我们可以将<equation>\alpha</equation>和<equation>\beta</equation>组合起来，得到一个新的自然变换<equation>\beta \circ \alpha: F \Rightarrow H</equation>，这个组合又称为自然变换的垂直组合。</p><p>自然变换的垂直组合的Haskell表示如下。</p><code lang="haskell">vertComp :: (Functor f , Functor g, Functor (h :: k -&gt; *))
         =&gt; Nat g h -&gt; Nat f g -&gt; Nat f h
vertComp beta@(Nat gh) alpha@(Nat fg) = Nat $ gh . fg</code><p>自然变换的垂直组合本质上就是态射的组合，因此是满足结合律的。</p><code lang="haskell">  gamma  `vertComp` (beta   `vertComp` alpha)
= Nat hk `vertComp` (Nat gh `vertComp` Nat fg)
= Nat ( hk . (gh  . fg))
= Nat ((hk .  gh) . fg )
= (Nat hk `vertComp` Nat gh) `vertComp` Nat fg
= (gamma  `vertComp` beta)   `vertComp` alpha
</code><p>若函子F、函子F' 是范畴<equation>C</equation>到范畴<equation>D</equation>的函子，函子G、函子G‘是范畴<equation>D</equation>到范畴<equation>E</equation>的函子，<equation>\alpha: F \Rightarrow F'</equation>是函子F 到函子F' 的自然变换，<equation>\beta: G \Rightarrow G'</equation>是函子G 到函子G' 的自然变换。我们也可以将<equation>\alpha</equation>和<equation>\beta</equation>组合起来，得到一个新的自然变换<equation>\beta \diamond \alpha: G \circ F \Rightarrow G' \circ F'</equation>，这个组合又称为自然变换的水平组合，这里我们用和垂直组合不同的符号<equation>\diamond</equation>来表示水平组合。</p><p>自然变换的水平组合的Haskell表示如下。<br></p><code lang="haskell">horzComp :: (Functor f, Functor f', Functor g, Functor g')
         =&gt; Nat g g' -&gt; Nat f f' -&gt; Nat (g :.: f) (g' :.: f')
         -- gg' :: g a -&gt; g' a
         -- ff' :: f a -&gt; f' a
         -- gfa :: g (f a)
         -- f'g' x :: f' (g' a)
         -- fmap ff' :: g' (f a) -&gt; g' (f' a)
horzComp beta@(Nat gg') alpha@(Nat ff')
  = Nat $ \(Compose gfa) -&gt; Compose (fmap ff' (gg' gfa))
</code><p>自然变换的水平组合本质上是函子的组合，因此也满足结合律。</p><br><code lang="haskell">  gamma  `vertComp` (beta   `vertComp` alpha)
= Nat hk `vertComp` (Nat gh `vertComp` Nat fg)
= Nat (Compose .  fmap (fmap fg  .  gh)      . hk  . getCompose)
= Nat (Compose . (fmap (fmap fg) .  fmap gh) . hk  . getCompose)
= Nat (Compose .  fmap (fmap fg) . (fmap gh  . hk) . getCompose)
= (Nat hk `vertComp` Nat gh) `vertComp` Nat fg
= (gamma  `vertComp` beta)   `vertComp` alpha
</code><p>垂直组合和水平组合是可以混合在一起的，即垂直组合后得到的自然变换也可以再次与其他自然变换水平组合，同样的，水平组合后得到的自然变换也可以再次与其他自然变换垂直组合。</p><p>垂直组合和水平组合是可以互换的，先垂直复合再水平复合与先水平复合再垂直复合所得到的结果是相等的 ，即有<equation>(\beta' \circ \alpha') \diamond (\beta \circ \alpha) = (\beta' \diamond \beta) \circ (\alpha' \diamond \alpha)</equation>，我们称之为自然变换的垂直水平互换定律。</p><br>我们再来看一个特殊的自然变换的水平组合，一个函子F和自然变换<equation>\alpha: G \Rightarrow H</equation>的水平组合， 可以将其看成是函子F 上的恒等自然同构<equation>id_F: F \Rightarrow F</equation>和自然变换<equation>\alpha: G \Rightarrow H</equation>的水平组合。<p>当自然变换在前面时，称这个组合是前置水平组合，其形式为<equation>\alpha \circ F</equation>，定义为<equation>(\alpha \circ F)\ a = \alpha\ (F\ a)</equation>，有时为了更简明些，前置水平组合简记为<equation>\alpha F</equation>。Haskell中表示如下。</p><code lang="haskell">preFComp :: (Functor f, Functor g, Functor h)
         =&gt; Nat g h -&gt; g (f a) -&gt; h (f a)
preFComp (Nat gh) fa = gh fa</code><p>当自然变换在后面时，称这个组合是后置组合，其形式为<equation>F \circ \alpha</equation>，定义为<equation>(F \circ \alpha)\ a = F\ (\alpha\ a)</equation>，有时为了书写更简明，后置水平组合简记为<equation>F \alpha</equation>。Haskell中表示如下。<br></p><code lang="haskell">postFComp :: (Functor f, Functor g, Functor h)
         =&gt; Nat g h -&gt; f (g a) -&gt; f (h a)
postFComp (Nat gh) fa = fmap gh fa
</code><p>自然变换的水平组合可以直观的认为其扩展了自然变换的宽度，由单个函子之间的自然变换扩展为多个函子组合后的函子之间的自然变换。我们前面提到的Cat范畴就是由函子的组合得来的，自然变换的水平组合使得组合后的函子也是存在自然变换的。</p><p>自然变换的垂直组合则可以直观的认为其扩展了自然变换的深度，由两个函子之间的自然变换扩展为跨越多个函子的自然变换，多个函子都可以通过自然变换联系起来。若我们将函子看成对象，那就通过自然变换的垂直组合得到了函子范畴。<br></p><ul><li><b>函子范畴</b></li></ul><br><p>范畴<equation>C</equation>到范畴<equation>D</equation>之间的所有函子组成了一个函子范畴，对象就是范畴<equation>C</equation>到范畴<equation>D</equation>之间的函子，态射就是这些函子之间的自然变换，态射的组合操作就是自然变换的垂直组合。单位元就是函子上的恒等自然同构，结合律可以由自然变换的垂直组合的结合律得到。</p><p>在Haskell中，函子范畴的定义如下：</p><code lang="haskell">instance Category Nat where
  id = Nat id
  Nat f . Nat g = Nat (f . g)</code><p>函子范畴是一个非常基础的概念，在后续的文章中将会多处用到这一概念。将这里介绍的函子和自然变换的概念理解透彻后，会对后续的学习非常有利。</p><p>学习了这一章的内容后，特别是自然变换的垂直组合、水平组合的概念，已经有了理解单子这个概念的基础了。不过先别急，在介绍单子之前，我们先看点其他东西吧。</p><p>本章到此结束，意犹未尽的朋友请等待下一篇：范畴上的代数结构</p>
<script async defer="defer" src="https://www.googletagmanager.com/gtag/js?id=UA-7909075-5"></script>
<script>
window.dataLayer = window.dataLayer || [];
function gtag(){ dataLayer.push(arguments); }
gtag('js', new Date());
gtag('config', 'UA-7909075-5');
</script>
<script>
var _hmt = _hmt || [];
</script>
<script async defer="defer" src="https://hm.baidu.com/hm.js?e3d40295e416616ddc21287da9646d31"></script>
</body>
</html>
