<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<title>[Parsing] 2. Naive Evaluation</title>
</head>
<body>
<p><a href="https://zhuanlan.zhihu.com/p/20033711">原文</a></p>
<p>Datalog相比Prolog，少了复合类型，但是要求结果是和求值顺序无关。这点就比Prolog好多了，Prolog完全没有逻辑可言，而Datalog至少是和一阶逻辑类似的。</p><p>我们用 {var, 'X'} 的形式来表示Datalog中的变量。那么，类型定义可以写成</p><code lang="text">-type datalog_term() :: atom() | {'var', atom()}.
-type datalog_goal() :: {atom(), [datalog_term()]}.
-type datalog_clause() :: {datalog_goal(), [datalog_goal()]}.</code><p> 一个简单的例子</p><code lang="text">%% start(a).
{{start, [a]},         []},
%% arc(d,a).
{{arc,   [d,a]},       []},
%% arc(e,a).
{{arc,   [e,a]},       []},
%% arc(a,b).
{{arc,   [a,b]},       []},
%% arc(a,c).
{{arc,   [a,c]},       []},
%% arc(b,f).
{{arc,   [b,f]},       []},
%% arc(c,f).
{{arc,   [c,f]},       []},
%% black(X):- start(X).
{{black, [{var,'X'}]}, [{start,[{var,'X'}]}]},
%% black(X):- white(Y), arc(Y,X).
{{black, [{var,'X'}]}, [{white,[{var,'Y'}]}, {arc,[{var,'Y'},{var,'X'}]}]},
%% white(X):- black(Y), arc(Y,X).
{{white, [{var,'X'}]}, [{black,[{var,'Y'}]}, {arc,[{var,'Y'},{var,'X'}]}]},
%% black(X):- white(Y), arc(X,Y).
{{black, [{var,'X'}]}, [{white,[{var,'Y'}]}, {arc,[{var,'X'},{var,'Y'}]}]},
%% white(X):- black(Y), arc(X,Y).
{{white, [{var,'X'}]}, [{black,[{var,'Y'}]}, {arc,[{var,'X'},{var,'Y'}]}]}
</code><p>而所谓的naive evaluation，既然都叫naive了，那肯定是非常简单的。</p><p>首先，把所有clause分成两类，EDB (extensional) 和 IDB (intentional)</p><p>EDB</p><code lang="text">start(a).
arc(d,a).
arc(e,a).
arc(a,b).
arc(a,c).
arc(b,f).
arc(c,f).
</code><p>IDB<br></p><code lang="text">black(X): start(X).
black(X): white(Y), arc(Y,X).
white(X): black(Y), arc(Y,X).
black(X): white(Y), arc(X,Y).
white(X): black(Y), arc(X,Y).
</code><p>直接提供Fact的就是EDB，提供规则的就是IDB</p><br><code lang="text">build_db(Clauses) -&gt;
    EDB =
        lists:foldl(
          fun add_fact/2,
          dict:new(),
          [Head || {Head, []} &lt;- Clauses]),

    IDB =
        [Clause || {_, Body} = Clause &lt;- Clauses, Body =/= []],

    EDB1 = build_db(IDB, EDB),
    [ {P, sets:to_list(Tuples)} || {P,Tuples} &lt;- dict:to_list(EDB1) ].
</code><p>IDB里每条规则的正文部分，可以看作是查询。每条规则都出EDB里查一遍，<br>并把结果加到EDB里。</p><code lang="text">naive_iteration([], EDB) -&gt;
    EDB;
naive_iteration([{{P,A}, Goals}|IDB], EDB) -&gt;
    EDB1 =
        lists:foldl(
          fun add_fact/2,
          EDB,
          [ {P, [subst(T, VarMap) || T &lt;- A]}
            || VarMap &lt;- query_db(Goals, EDB) ]),
    naive_iteration(IDB, EDB1).
</code><p> 可是，这样并不能得到所有结果。那么就不断重复这个过程，直到EDB不变为止。(为了让人觉得高深莫测，那就叫不动点吧)。 </p><code lang="text">build_db(IDB, EDB) -&gt;
    case naive_iteration(IDB, EDB) of
        EDB -&gt;
            EDB;
        EDB1 -&gt;
            build_db(IDB, EDB1)
    end.
</code><p>剩下的函数定义，主要就是简单的unification。</p><code lang="text">subst({var, V}, VarMap) -&gt;
    case dict:find(V, VarMap) of
        {ok, V1} -&gt;
            subst(V1, VarMap);
        error -&gt;
            {var, V}
    end;
subst(V, _) -&gt;
    V.

bind(V, T, VarMap) -&gt;
    case dict:is_key(V, VarMap) of
        true -&gt;
            false;
        false -&gt;
            dict:store(V, T, VarMap)
    end.


unify(V1, V2, VarMap) -&gt;
    case {subst(V1, VarMap), subst(V2, VarMap)} of
        {{var, X}, {var, X}} -&gt;
            VarMap;
        {{var, X1}, X2} -&gt;
            bind(X1, X2, VarMap);
        {X1, {var, X2}} -&gt;
            bind(X2, X1, VarMap);
        {X, X} -&gt;
            VarMap;
        _ -&gt;
            false
    end.

unify_list([], [], VarMap) -&gt;
    VarMap;
unify_list([H1|T1], [H2|T2], VarMap) -&gt;
    case unify(H1, H2, VarMap) of
        false -&gt;
            false;
        VarMap1 -&gt;
            unify_list(T1, T2, VarMap1)
    end.

add_fact({P, A}, EDB) -&gt;
    dict:update(P, fun (Old) -&gt; sets:add_element(A, Old) end, sets:from_list([A]), EDB).

find_facts(P, EDB) -&gt;
    case dict:find(P, EDB) of
        {ok, S} -&gt;
            sets:to_list(S);
        error -&gt;
            []
    end.

query_db([], VarMaps, _EDB) -&gt;
    VarMaps;
query_db([{P, A}|Goals], VarMaps, EDB) -&gt;
    VarMaps1 =
        [ M || M &lt;- [ unify_list(A, Fact, VarMap)
                      || Fact &lt;- find_facts(P, EDB),
                         VarMap &lt;- VarMaps ],
               M =/= false],

    query_db(Goals, VarMaps1, EDB).

query_db(Goals, EDB) -&gt;
    query_db(Goals, [dict:new()], EDB).
</code><p>参考 <a href="http://www.inf.unibz.it/%7Enutt/Teaching/CL0910/CLSlides/5-datalog-eval.pdf">http://www.inf.unibz.it/%7Enutt/Teaching/CL0910/CLSlides/5-datalog-eval.pdf</a></p><p><a href="http://webdam.inria.fr/Alice/">http://webdam.inria.fr/Alice/</a></p>
<script async defer="defer" src="https://www.googletagmanager.com/gtag/js?id=UA-7909075-5"></script>
<script>
window.dataLayer = window.dataLayer || [];
function gtag(){ dataLayer.push(arguments); }
gtag('js', new Date());
gtag('config', 'UA-7909075-5');
</script>
<script>
var _hmt = _hmt || [];
</script>
<script async defer="defer" src="https://hm.baidu.com/hm.js?e3d40295e416616ddc21287da9646d31"></script>
</body>
</html>
