<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<title>PLAI前6章回顾</title>
</head>
<body>
<p><a href="https://zhuanlan.zhihu.com/p/25219591">原文</a></p>
<div class="title-image"><img src="https://pic2.zhimg.com/v2-f94555d70b9a6bb704dfd919a5e5c419_r.jpg" alt=""></div><p>离上一章节翻译已经过去三周，跨过了一个年。这篇文章大致回顾一下前几章的内容并将相关代码整理至此。</p><p>第一章（<a href="https://zhuanlan.zhihu.com/p/24578121" data-editable="true" data-title="译文">译文</a>，<a href="http://cs.brown.edu/courses/cs173/2012/book/Introduction.html" data-editable="true" data-title="原文">原文</a>）简单介绍了本书的结构以及书中使用的语言 plai-typed。可以参考 <a href="https://zhuanlan.zhihu.com/p/24634895" data-editable="true" data-title="PLAI编程环境简单介绍">PLAI编程环境简单介绍</a>配置好编程环境，然后简单过一下该章代码熟悉一下 plai-typed 中 define-type 、 type-case 和 test 的使用。</p><br><code lang="racket">#lang plai-typed

(define-type MisspelledAnimal
  [caml (humps : number)]
  [yacc (height : number)])

(define ma1 (caml 2))
(define ma2 (yacc 1.9))

(define (good? [ma : MisspelledAnimal]) : boolean
  (type-case MisspelledAnimal ma
    [caml (humps) (&gt;= humps 2)]
    [yacc (height) (&gt; height 2.1)]))

(test (good? ma1) #t)
(test (good? ma2) #f)</code><p>要实现一个编程语言的解释器，第一步当然需要实现对输入的解析，而本书的重点不在于此， 于是第二章（<a href="https://zhuanlan.zhihu.com/p/24578121" data-editable="true" data-title="译文" class="">译文</a>，<a href="http://cs.brown.edu/courses/cs173/2012/book/Everything__We_Will_Say__About_Parsing.html" data-editable="true" data-title="原文">原文</a>） 介绍了使用 plai-typed 语言内建函数 read 将输入解析成 s-expression ， 后再解析成定义的语言的内部表示。</p><br><code lang="racket">#lang plai-typed

(define-type ArithC
  [numC (n : number)]
  [plusC (l : ArithC) (r : ArithC)]
  [multC (l : ArithC) (r : ArithC)])

(define (parse [s : s-expression]) : ArithC
  (cond
    [(s-exp-number? s) (numC (s-exp-&gt;number s))]
    [(s-exp-list? s)
     (let ([sl (s-exp-&gt;list s)])
       (case (s-exp-&gt;symbol (first sl))
         [(+) (plusC (parse (second sl)) (parse (third sl)))]
         [(*) (multC (parse (second sl)) (parse (third sl)))]
         [else (error 'parse "invalid list input")]))]
    [else (error 'parse "invalid input")]))

(parse (read))
;; (+ 2 (* 3 20))
;; -&gt;
;; (plusC (numC 2) (multC (numC 3) (numC 20)))</code><p>第二章（<a href="https://zhuanlan.zhihu.com/p/24578121" data-editable="true" data-title="译文">译文</a>，<a href="http://cs.brown.edu/courses/cs173/2012/book/Everything__We_Will_Say__About_Parsing.html" data-editable="true" data-title="原文">原文</a>） 解析了一个简单的算术语言（支持整数的加法和乘法），第三章（<a href="https://zhuanlan.zhihu.com/p/24578121" data-editable="true" data-title="译文">译文</a>，<a href="http://cs.brown.edu/courses/cs173/2012/book/first-interp.html" data-editable="true" data-title="原文">原文</a>） 针对该语言的内部表示实现了一个解释器。</p><br><code lang="racket">#lang plai-typed

;; ArithC 代码不变
;; parse 代码不变

(define (interp [a : ArithC]) : number
  (type-case ArithC a
    [numC (n) n]
    [plusC (l r) (+ (interp l) (interp r))]
    [multC (l r) (* (interp l) (interp r))]))</code><p>第三章（<a href="https://zhuanlan.zhihu.com/p/24578121" data-editable="true" data-title="译文">译文</a>，<a href="http://cs.brown.edu/courses/cs173/2012/book/first-interp.html" data-editable="true" data-title="原文">原文</a>） 实现的算术语言只支持加法和乘法，第四章（<a href="https://zhuanlan.zhihu.com/p/24578121" data-editable="true" data-title="译文">译文</a>，<a href="http://cs.brown.edu/courses/cs173/2012/book/first-desugar.html" data-editable="true" data-title="原文">原文</a>） 考虑在不改变语言核心（语言的内部表示）的情况下引入减法操作，于是引入了一个语法糖， 并且简单讨论了语法糖设计中的一些问题。最后通过让读者引入单目操作符“取负”引导读者思考了更多有关语法糖的问题。</p><br><code lang="racket">#lang plai-typed

;; ArithC 代码不变
;; interp 代码不变

(define-type ArithS
  [numS (n : number)]
  [plusS (l : ArithS) (r : ArithS)]
  [bminusS (l : ArithS) (r : ArithS)]
  [multS (l : ArithS) (r : ArithS)])

;; ArithS -&gt; ArithC
(define (desugar [as : ArithS]) : ArithC
  (type-case ArithS as
    [numS (n) (numC n)]
    [plusS (l r) (plusC (desugar l)
                        (desugar r))]
    [multS (l r) (multC (desugar l)
                        (desugar r))]
    [bminusS (l r) (plusC (desugar l)
                      (multC (numC -1) (desugar r)))]))

;; 解析器现在输出 ArithS
(define (parse [s : s-expression]) : ArithS
  (cond
    [(s-exp-number? s) (numS (s-exp-&gt;number s))]
    [(s-exp-list? s)
     (let ([sl (s-exp-&gt;list s)])
       (case (s-exp-&gt;symbol (first sl))
         [(+) (plusS (parse (second sl)) (parse (third sl)))]
         [(*) (multS (parse (second sl)) (parse (third sl)))]
         [(-) (bminusS (parse (second sl)) (parse (third sl)))]
         [else (error 'parse "invalid list input")]))]
    [else (error 'parse "invalid input")]))

;; 解释代码之前需要使用 desugar 剔除语法糖
(interp (desugar (parse (read))))</code><p>在第五章（<a href="https://zhuanlan.zhihu.com/p/24720187" data-editable="true" data-title="译文">译文</a>，<a href="http://cs.brown.edu/courses/cs173/2012/book/adding-functions.html" data-editable="true" data-title="原文">原文</a>） 中首次引入了函数，注意这里在语言中没有引入函数值类型，函数都是预定义，函数列表作为参数传递给解释器使用， 所有表达式的值还只是数。重点在于函数的求值过程的讨论。</p><br><code lang="racket">#lang plai-typed

;; 函数类型
(define-type FunDefC
  [fdC (name : symbol) (arg : symbol) (body : ExprC)])

;; 语言的核心数据结构 ExprC 添加了标识符 (idC) 和函数调用 (appC) 函数类型
(define-type ExprC
  [numC (n : number)]
  [idC (s : symbol)]
  [appC (fun : symbol) (arg : ExprC)]
  [plusC (l : ExprC) (r : ExprC)]
  [multC (l : ExprC) (r : ExprC)])

;; 辅助函数:从函数列表 fds 取出名为 n 的函数定义
(define (get-fundef [n : symbol] [fds : (listof FunDefC)]) : FunDefC
  (cond
    [(empty? fds) (error 'get-fundef "reference to undefined function")]
    [(cons? fds) (cond
                   [(equal? n (fdC-name (first fds))) (first fds)]
                   [else (get-fundef n (rest fds))])]))

;; 辅助函数：将表达式 in 中所有标识符 for 替换为 what
(define (subst [what : ExprC] [for : symbol] [in : ExprC]) : ExprC
  (type-case ExprC in
    [numC (n) in]
    [idC (s) (cond
              [(symbol=? s for) what]
              [else in])]
    [appC (f a) (appC f (subst what for a))]
    [plusC (l r) (plusC (subst what for l)
                        (subst what for r))]
    [multC (l r) (multC (subst what for l)
                        (subst what for r))]))

;; parser
(define (parse [s : s-expression]) : ExprC
  (cond
    [(s-exp-number? s) (numC (s-exp-&gt;number s))]
    [(s-exp-symbol? s) (idC (s-exp-&gt;symbol s))]
    [(s-exp-list? s)
     (let ([sl (s-exp-&gt;list s)])
       (case (s-exp-&gt;symbol (first sl))
         [(+) (plusC (parse (second sl)) (parse (third sl)))]
         [(*) (multC (parse (second sl)) (parse (third sl)))]
         [else (appC (s-exp-&gt;symbol (first sl))
                     (parse (second sl)))]))]))

;; 解释器多了一个参数，传入预定义的函数列表
(define (interp [e : ExprC] [fds : (listof FunDefC)]) : number
  (type-case ExprC e
    [numC (n) n]
    [idC (_) (error 'interp "shouldn't get here")]
    [appC (f a) (let ([fd (get-fundef  f fds)])
                  (interp (subst a (fdC-arg fd) (fdC-body fd))
                          fds))]
    [plusC (l r) (+ (interp l fds) (interp r fds))]
    [multC (l r) (* (interp l fds) (interp r fds))]))

(interp (parse (read)) (list
                        (fdC 'plusOne 'x (plusC (idC 'x) (numC 1)))
                        (fdC 'square 'x (multC (idC 'x) (idC 'x)))))
;; (square (plusOne 10))
;; -&gt;
;; 121</code><p>第六章（<a href="https://zhuanlan.zhihu.com/p/24991964" data-editable="true" data-title="译文">译文</a>，<a href="http://cs.brown.edu/courses/cs173/2012/book/From_Substitution_to_Environments.html" data-editable="true" data-title="原文">原文</a>）首先说明了一些上一章实现的函数替换模型的问题，以该章解释器为参考实现，将替换模型更换成环境模型重新实现了一个行为一致的解释器。 并在实现过程讨论了关于动态作用域的问题。</p><br><code lang="racket">#lang plai-typed

;; 函数类型
(define-type FunDefC
  [fdC (name : symbol) (arg : symbol) (body : ExprC)])

;; 语言的核心数据结构 ExprC 添加了标识符 (idC) 和函数调用 (appC) 函数类型
(define-type ExprC
  [numC (n : number)]
  [idC (s : symbol)]
  [appC (fun : symbol) (arg : ExprC)]
  [plusC (l : ExprC) (r : ExprC)]
  [multC (l : ExprC) (r : ExprC)])

;; 辅助函数:从函数列表 fds 取出名为 n 的函数定义
(define (get-fundef [n : symbol] [fds : (listof FunDefC)]) : FunDefC
  (cond
    [(empty? fds) (error 'get-fundef "reference to undefined function")]
    [(cons? fds) (cond
                   [(equal? n (fdC-name (first fds))) (first fds)]
                   [else (get-fundef n (rest fds))])]))

;; 定义环境相关数据结构与操作
(define-type Binding
  [bind (name : symbol) (val : number)])
(define-type-alias Env (listof Binding))
(define mt-env empty)
(define extend-env cons)

;; 在环境 env 中查找标识符 n 绑定的值
(define (lookup [n : symbol] [env : Env]) : number
  (cond
    [(= 0 (length env)) (error 'lookup "Can't find binding")]
    [else (let [(b (first env))]
            (if (symbol=? n (bind-name b))
                (bind-val b)
                (lookup n (rest env))))]))

;; parser
(define (parse [s : s-expression]) : ExprC
  (cond
    [(s-exp-number? s) (numC (s-exp-&gt;number s))]
    [(s-exp-symbol? s) (idC (s-exp-&gt;symbol s))]
    [(s-exp-list? s)
     (let ([sl (s-exp-&gt;list s)])
       (case (s-exp-&gt;symbol (first sl))
         [(+) (plusC (parse (second sl)) (parse (third sl)))]
         [(*) (multC (parse (second sl)) (parse (third sl)))]
         [else (appC (s-exp-&gt;symbol (first sl))
                     (parse (second sl)))]))]))

;; 解释器多了一个参数,传入预定义的函数列表
(define (interp [a : ExprC] [env : Env] [fds : (listof FunDefC)]) : number
  (type-case ExprC a
    [numC (n) n]
    [idC (n) (lookup n env)]
    [appC (f arg) (let ([fd (get-fundef f fds)])
                     (interp (fdC-body fd)
                             (extend-env (bind (fdC-arg fd)
                                               (interp arg env fds))
                                         mt-env) ;; 这个地方 mt-env 改成 env 此解释器将变成动态绑定的
                             fds))]
    [plusC (l r) (+ (interp l env fds) (interp r env fds))]
    [multC (l r) (* (interp l env fds) (interp r env fds))]))

(interp (parse (read))
        mt-env
        (list
         (fdC 'plusOne 'x (plusC (idC 'x) (numC 1)))
         (fdC 'square 'x (multC (idC 'x) (idC 'x)))))
;; (square (plusOne 10))
;; -&gt;
;; 121

;; 可以通过计算
;; (interp (appC 'f1 (numC 3))
;;                  mt-env
;;                  (list (fdC 'f1 'x (appC 'f2 (numC 4)))
;;                        (fdC 'f2 'y (plusC (idC 'x) (idC 'y)))))
;; 来了解动态绑定的求解过程</code><p>后面一章将把函数作为值引入解释器，我们将能够进行高阶函数的解释，敬请期待， 或直接去往<a href="http://cs.brown.edu/courses/cs173/2012/book/higher-order-functions.html" data-editable="true" data-title="原文">原文</a>。</p>
<script async defer="defer" src="https://www.googletagmanager.com/gtag/js?id=UA-7909075-5"></script>
<script>
window.dataLayer = window.dataLayer || [];
function gtag(){ dataLayer.push(arguments); }
gtag('js', new Date());
gtag('config', 'UA-7909075-5');
</script>
<script>
var _hmt = _hmt || [];
</script>
<script async defer="defer" src="https://hm.baidu.com/hm.js?e3d40295e416616ddc21287da9646d31"></script>
</body>
</html>
