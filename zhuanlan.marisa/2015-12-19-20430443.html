<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<title>七树归一（上）</title>
</head>
<body>
<p><a href="https://zhuanlan.zhihu.com/p/20430443">原文</a></p>
<p>众所周之，一颗二叉树的定义是：</p><p>空的，或</p><p>一个元素，加上两颗二叉树树（一个结点）</p><code lang="coq">Inductive Tree T :=
| Empty
| Node : Tree T -&gt; T -&gt; Tree T -&gt; Tree T.
</code><p>T可以是任何东西，比如说自然数，链表，字符串，另一个Tree等等．．．</p><p>那T可不可以是unit，不带有任何信息？换句话说，可不可能T的所有元素皆相等于一个常数？在此之下，Tree有没有意义？</p><code lang="text">Inductive Tree :=
| Empty
| Node : Tree -&gt; Tree -&gt; Tree.
</code><p>当然可以！那没，既然树没有保留任何关于元素的信息，有什么意义？</p><p>尽管树不保存元素，就如同list unit并不是毫无意义，而是跟自然数同构一样，树自身的结构已经保留了一定的信息．</p><p>那这棵树有什么特别的地方吗？他跟一个结构有着很简单的同构关系（一一映射）．</p><p>跟他自己．不，跟7个他自己 ．换句话说，Tree~=Tree^7．</p><p>具体证明在<a class="" data-title="Seven Trees in One at Steven Landsburg" data-editable="true" href="http://www.thebigquestions.com/2013/05/09/seven-trees-in-one/">Seven Trees in One at Steven Landsburg</a> ，我们今天要试着把这个用Coq证明一遍．</p><p>我们先给出一个简写：</p><code lang="coq">Notation "[ a , b ]" := (Node a b).
Notation "0" := Empty.
</code><p> 这里是把七棵树合并的定义：</p><code lang="coq">Definition Combine_helper (T1 T2 T3 T4 T5 T6 T7 : Tree) : Tree :=
  match (T1, T2, T3, T4) with
  | (Empty, Empty, Empty, Empty) =&gt; 
      match T5 with
      | Node T5a T5b =&gt; [[[[Empty, T7], T6], T5a], T5b]
      | Empty =&gt; 
          match T6 with
          | Node _ _ =&gt; [[[[[T6, T7], Empty], Empty], Empty], Empty]
          | Empty =&gt; 
              match T7 with
              | [[[[T7a, T7b], T7c], T7d], T7e] =&gt;
                  [[[[[Empty, T7a], T7b], T7c], T7d], T7e]
              | _ =&gt; T7
              end
          end
      end
  | _ =&gt; [[[[[[T7, T6], T5], T4], T3], T2], T1]
  end.
</code><p>这个把七棵树分开：</p><code lang="coq">Definition Split (T : Tree) :=
  match T with
  | [[[[[0, T7a], T7b], T7c], T7d], T7e] =&gt; (0, 0, 0, 0, 0, 0, [[[[T7a, T7b], T7c], T7d], T7e])
  | [[[[[T6, T7], 0], 0], 0], 0] =&gt; (0, 0, 0, 0, 0, T6, T7)
  | [[[[0, T7], T6], T5a], T5b] =&gt; (0, 0, 0, 0, [T5a, T5b], T6, T7)
  | [[[[[[T7, T6], T5], T4], T3], T2], T1] =&gt; (T1, T2, T3, T4, T5, T6, T7)
  | _ =&gt; (0, 0, 0, 0, 0, 0, T)
  end.</code><p>这个是Combine_helper的wrapper：</p><code lang="coq">Definition Combine X := 
  match X with (t1, t2, t3, t4, t5, t6, t7) =&gt; Combine_helper t1 t2 t3 t4 t5 t6 t7 end.
</code><p>接下来我们开始证明：</p><code lang="coq">Goal forall T, Combine (Split T) = T.
  intros; compute.
</code><p>先做一点基本的简化 ．<br></p><p>这是Goal：</p><code lang="coq">1 subgoal
T : Tree
______________________________________(1/1)
(let
 (y, t7) :=
 match T with
 | 0 =&gt; (0, 0, 0, 0, 0, 0, T)
 | [0, _] =&gt; (0, 0, 0, 0, 0, 0, T)
 | [[0, _], _] =&gt; (0, 0, 0, 0, 0, 0, T)
 | [[[0, _], _], _] =&gt; (0, 0, 0, 0, 0, 0, T)
 | [[[[0, T4], T3], T2], T1] =&gt; (0, 0, 0, 0, [T2, T1], T3, T4)
 | [[[[[0, T5], T4], T3], T2], T1] =&gt; (0, 0, 0, 0, 0, 0, [[[[T5, T4], T3], T2], T1])
 | [[[[[[T7, T8] as T6, T5], 0 as T4], 0 as T3], 0 as T2], 0] =&gt; (0, 0, 0, 0, 0, T6, T5)
 | [[[[[[T7, T8] as T6, T5], 0 as T4], 0 as T3], 0 as T2], [_, _] as T1] =&gt;
     (T1, T2, T3, T4, T5, T8, T7)
 | [[[[[[T7, T8] as T6, T5], 0 as T4], 0 as T3], [_, _] as T2], T1] =&gt;
     (T1, T2, T3, T4, T5, T8, T7)
 | [[[[[[T7, T8] as T6, T5], 0 as T4], [_, _] as T3], T2], T1] =&gt; (T1, T2, T3, T4, T5, T8, T7)
 | [[[[[[T7, T8] as T6, T5], [_, _] as T4], T3], T2], T1] =&gt; (T1, T2, T3, T4, T5, T8, T7)
 end in
 let
 (y0, t6) := y in
 let
 (y1, t5) := y0 in
 let
 (y2, t4) := y1 in
 let
 (y3, t3) := y2 in
 let
 (t1, t2) := y3 in
 match t1 with
 | 0 =&gt;
     match t2 with
     | 0 =&gt;
         match t3 with
         | 0 =&gt;
             match t4 with
             | 0 =&gt;
                 match t5 with
                 | 0 =&gt;
                     match t6 with
                     | 0 =&gt;
                         match t7 with
                         | 0 =&gt; t7
                         | [0, _] =&gt; t7
                         | [[0, _], _] =&gt; t7
                         | [[[0, _], _], _] =&gt; t7
                         | [[[[T7a, T7b], T7c], T7d], T7e] =&gt;
                             [[[[[0, T7a], T7b], T7c], T7d], T7e]
                         end
                     | [_, _] =&gt; [[[[[t6, t7], 0], 0], 0], 0]
                     end
                 | [T5a, T5b] =&gt; [[[[0, t7], t6], T5a], T5b]
                 end
             | [_, _] =&gt; [[[[[[t7, t6], t5], t4], t3], t2], t1]
             end
         | [_, _] =&gt; [[[[[[t7, t6], t5], t4], t3], t2], t1]
         end
     | [_, _] =&gt; [[[[[[t7, t6], t5], t4], t3], t2], t1]
     end
 | [_, _] =&gt; [[[[[[t7, t6], t5], t4], t3], t2], t1]
 end) = T
</code><p>当然，这道题可以用多次case analysis直接做出，不过内样太丑，我们试试看写一个tactic帮助我们做这件事：</p><code lang="coq">Ltac match_context_destruct H :=
  match H with 
  | context[match ?x with _ =&gt; _ end] =&gt; 
      destruct x eqn:?
  end.

Ltac match_destruct :=
  match goal with
  | H : _ |- _ =&gt; let HT := type of H in match_context_destruct HT
  | |- ?H =&gt; match_context_destruct H
  end.</code><p>现在可以试着证明：</p><code lang="coq">Goal forall T, Combine (Split T) = T.
  intros; compute.
  repeat(match_destruct; trivial).
</code><code lang="coq">120 subgoals
T : Tree
p : Tree * Tree * Tree * Tree * Tree * Tree
t : Tree
Heqt0 : T = 0
p0 : Tree * Tree * Tree * Tree * Tree
t0 : Tree
p1 : Tree * Tree * Tree * Tree
t1 : Tree
p2 : Tree * Tree * Tree
t2 : Tree
p3 : Tree * Tree
t3, t4, t5 : Tree
Heqt6 : t4 = 0
Heqt1 : t5 = 0
Heqp4 : p3 = (0, 0)
Heqt2 : t3 = 0
Heqp3 : p2 = (0, 0, 0)
Heqt3 : t2 = 0
Heqp2 : p1 = (0, 0, 0, 0)
Heqt4 : t1 = 0
Heqp1 : p0 = (0, 0, 0, 0, 0)
Heqt5 : t0 = 0
Heqp0 : p = (0, 0, 0, 0, 0, 0)
t6_1, t6_2 : Tree
Heqt8 : t6_1 = 0
Heqt7 : t = [0, t6_2]
Heqp : (0, 0, 0, 0, 0, 0, 0) = (0, 0, 0, 0, 0, 0, [0, t6_2])
______________________________________(1/120)
[0, t6_2] = 0
</code><p> match_destruct生成了很多等式，我们可以用subst进行替换，消除一大部分：<br></p><code lang="coq">Goal forall T, Combine (Split T) = T.
  intros; compute.
  repeat(match_destruct; subst; trivial).
</code><code lang="coq">120 subgoals
t0_2 : Tree
Heqp : (0, 0, 0, 0, 0, 0, 0) = (0, 0, 0, 0, 0, 0, [0, t0_2])
______________________________________(1/120)
[0, t0_2] = 0
</code><p>这个等式很明显不成立，用discriminate可以消除．</p><code lang="text">Goal forall T, Combine (Split T) = T.
  intros; compute.
  repeat(discriminate||match_destruct; subst; trivial).
</code><code lang="coq">16 subgoals
t0_2, t0_3 : Tree
Heqp : (0, 0, 0, 0, 0, 0, [0, t0_2]) = (0, 0, 0, 0, 0, 0, [0, t0_3])
______________________________________(1/16)
[0, t0_3] = [0, t0_2]
</code><p>discriminate是廉价操作，所以智能度不高，有一定的等式不能否证，可以用congruence．<br></p><code lang="text">Goal forall T, Combine (Split T) = T.
  intros; compute.
  repeat(congruence||match_destruct; subst; trivial).
Qed.
</code><p>这下过了，然而很慢，我们可以通过ltac match避免使用congruence来提速．</p><code lang="text">Goal forall T, Combine (Split T) = T.
  intros; compute.
  repeat(
      match goal with H : _ = _ |- _ =&gt; 
        inversion H; subst end ||
      match_destruct; 
    subst;
    trivial).
Qed.
</code><p> 这下ltac再次进入死循环，我们如果单步执行，我们会发现同一等式被inversion多次．因为inversion不会丢失信息，我们可以在inversion后clear：</p><code lang="coq">Goal forall T, Combine (Split T) = T.
  intros; compute.
  repeat(
      match goal with H : _ = _ |- _ =&gt; 
        inversion H; subst; clear H end ||
      match_destruct; 
    subst;
    trivial).
Qed.
</code><p> 这下ltac再次进入死循环，单步执行后发现有的时候inversion会产生一个一模一样的新等式，所以我们要减小match的成功率：</p><code lang="coq">Goal forall T, Combine (Split T) = T.
  intros; compute.
  repeat(
      match goal with H : (_,_) = (_,_) |- _ =&gt; 
        inversion H; subst; clear H end ||
      match_destruct; 
    subst;
    trivial).
Qed.
</code><p>很好，过了．</p><code lang="coq">Goal forall T, Split (Combine T) = T.
  intros; compute.
  repeat(
      match goal with H : (_,_) = (_,_) |- _ =&gt; inversion H; subst; clear H end ||
      match goal with H : [_,_] = [_,_] |- _ =&gt; inversion H; subst; clear H end ||
      discriminate ||
      match_destruct; 
    subst;
    trivial).
Qed.
</code><p>如发炮制，除了要多加一个match．</p><p>inversion, subst, clear三连击经常出现，所以我们可以重构成单一tactic．</p><p>最终代码：</p><code lang="coq">Set Implicit Arguments.

Inductive Tree :=
| Empty
| Node : Tree -&gt; Tree -&gt; Tree.

Notation "[ a , b ]" := (Node a b).
Notation "0" := Empty.

Definition Combine_helper (T1 T2 T3 T4 T5 T6 T7 : Tree) : Tree :=
  match (T1, T2, T3, T4) with
  | (0, 0, 0, 0) =&gt; 
      match T5 with
      | Node T5a T5b =&gt; [[[[0, T7], T6], T5a], T5b]
      | 0 =&gt; 
          match T6 with
          | Node _ _ =&gt; [[[[[T6, T7], 0], 0], 0], 0]
          | 0 =&gt; 
              match T7 with
              | [[[[T7a, T7b], T7c], T7d], T7e] =&gt;
                  [[[[[0, T7a], T7b], T7c], T7d], T7e]
              | _ =&gt; T7
              end
          end
      end
  | _ =&gt; [[[[[[T7, T6], T5], T4], T3], T2], T1]
  end.

Definition Split (T : Tree) :=
  match T with
  | [[[[[0, T7a], T7b], T7c], T7d], T7e] =&gt; (0, 0, 0, 0, 0, 0, [[[[T7a, T7b], T7c], T7d], T7e])
  | [[[[[T6, T7], 0], 0], 0], 0] =&gt; (0, 0, 0, 0, 0, T6, T7)
  | [[[[0, T7], T6], T5a], T5b] =&gt; (0, 0, 0, 0, [T5a, T5b], T6, T7)
  | [[[[[[T7, T6], T5], T4], T3], T2], T1] =&gt; (T1, T2, T3, T4, T5, T6, T7)
  | _ =&gt; (0, 0, 0, 0, 0, 0, T)
  end.

Definition Combine X := 
  match X with (t1, t2, t3, t4, t5, t6, t7) =&gt; Combine_helper t1 t2 t3 t4 t5 t6 t7 end.

Ltac match_context_destruct H :=
  match H with 
  | context[match ?x with _ =&gt; _ end] =&gt; 
      destruct x eqn:?
  end.

Ltac match_destruct :=
  match goal with
  | H : _ |- _ =&gt; let HT := type of H in match_context_destruct HT
  | |- ?H =&gt; match_context_destruct H
  end.
Ltac invcs H := inversion H; subst; clear H.

Goal forall T, Combine (Split T) = T.
  intros; compute.
  repeat(
      match goal with H : (_,_) = (_,_) |- _ =&gt; invcs H end ||
      match_destruct; 
    subst;
    trivial).
Qed.

Goal forall T, Split (Combine T) = T.
  intros; compute.
  repeat(
      match goal with H : (_,_) = (_,_) |- _ =&gt; invcs H end ||
      match goal with H : [_,_] = [_,_] |- _ =&gt; invcs H end ||
      discriminate ||
      match_destruct; 
    subst;
    trivial).
Qed.</code><p>这样，就没什么亮点地完成了任务． 在续集，我们会在此代码上玩很好玩的东西，敬请期待（雾 <br></p><p>本系列上，完！</p>
<script async defer="defer" src="https://www.googletagmanager.com/gtag/js?id=UA-7909075-5"></script>
<script>
window.dataLayer = window.dataLayer || [];
function gtag(){ dataLayer.push(arguments); }
gtag('js', new Date());
gtag('config', 'UA-7909075-5');
</script>
<script>
var _hmt = _hmt || [];
</script>
<script async defer="defer" src="https://hm.baidu.com/hm.js?e3d40295e416616ddc21287da9646d31"></script>
</body>
</html>
