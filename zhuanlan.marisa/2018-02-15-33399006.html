<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<title>delimited continuations完全攻略</title>
</head>
<body>
<p><a href="https://zhuanlan.zhihu.com/p/33399006">原文</a></p>
<div class="title-image"><img src="https://pic2.zhimg.com/v2-eb471c83c20ac7b33a45d0f2bf17a90c_r.jpg" alt=""></div><p>宣传一下我的群：Expert Scheme：523791077</p><p><b>Part I ： shift and reset</b></p><p><b>警告：在racket中使用shift/reset，请加上这句：</b></p><code lang="racket">(require racket/control)</code><p><b>A. shift和reset的基本规则</b></p><p>要了解shift和reset如何使用，就必须先了解shift和reset的求值规则：</p><p>当reset的内部是简单表达式的时候，整个表达式直接返回其值。</p><code lang="racket">(reset (+ 1 2)) =&gt; 3</code><p>当reset的内部的shift被求值时，需要注意，返回shift的body。</p><code lang="racket">(reset (+ 1 (shift k 2)))  =&gt; 2</code><p>把reset想象成一个整块，shift就是在其中打洞。</p><code lang="racket">(+ 1 hole)</code><p>你现在需要一个值把洞填上去，我们可以写一个函数：</p><code lang="racket">(lambda (hole) (+ 1 hole))</code><p>而shift的第一个标识符k所绑定的值就是这个函数(一般称为continuation，用字母k表示，或者用ctx表示）。</p><p>这个表达式：</p><code lang="racket">(reset (+ (shift k (k (k 1))) 1))</code><p>我们的k函数应当是（注意，这个例子中的hole与上一个例子的hole位置是不同的）</p><code lang="racket">(lambda (hole) (+ hole 1))</code><p>整个表达式应当返回shift的body部分，即：</p><code lang="racket">(k (k 1))</code><p>而我们已经知道k的值了，直接带入表达式计算得出3。</p><p><br></p><p><b>B. 与Arclisp中方括号的关系</b></p><blockquote>Since functions of one argument are so often used in Lisp programs,<br>Arc has a special notation for them.  [... _ ...]  is an abbreviation<br>for (fn (_) (... _ ...)).  So our first map example could have been<br>written<br><br>arc&gt; (map [+ _ 10] '(1 2 3))<br>(11 12 13)</blockquote><p>Arclisp允许你在表达式中开洞，这样表达式就成了一个函数。</p><p>而reset/shift中也允许你这么做。</p><code lang="racket">(reset (shift k k))  = 
(lambda (x) x) = 
[ _ ] (in Arclisp)</code><p>第二个例子：</p><code lang="racket">(reset (+ 1 (* 2 (shift k k))))  =
(lambda (x) (+ 1 (* 2 x)))  =
[+ 1 (* 2 _)] (in Arclisp)</code><p>我们发现Arclisp中的_与(shift k k)某种意义上有很大的相似性。</p><p>下面是一个更酷的例子：</p><code lang="racket">(reset (+ (shift k1 k1) (shift k2 k2)))</code><p>为了区分前后两个k，我将它们分别标记为k1和k2。</p><p>很明显k1是：</p><code lang="racket">(lambda (x1)
  (+ x1 (shift k2 k2)))</code><p>我们的reset表达式返回了k1。</p><p>尝试向k1这个函数中喂一个值2：</p><code lang="racket">(+ 2 (shift k2 k2))</code><p>现在你应该感到犯难了，还有一个shift表达式，但是外层已经没有reset了。</p><p>于是shift并不知道把k2返回到哪里，也不知道<b>打洞的范围是多大</b>。</p><p>k2是:</p><code lang="racket">(lambda (x) x)</code><p>还是：</p><code lang="racket">(lambda (x) (+ 2 x))</code><p>幸运的是，shift和reset已经帮我们考虑到这个难题了，会帮我们自动地在k1体内加一个reset。</p><code lang="racket">k1 = (lambda (x1) (reset (+ x1 (shift k2 k2))))</code><p>很明显，我们可以直接写出k2 （别弄丢了reset）：</p><code lang="racket">(lambda (x2) (reset (+ x1 x2)))</code><p>综合起来结果就是</p><code lang="racket">(lambda (x1)
  (lambda (x2)
    (+ x1 x2)))</code><p>因为当reset体内没有shift时，直接返回其值，所以我们可以将两个reset消去。</p><p><br></p><p>请验证 (shift k (k <i>val</i>))和<i>val</i>在reset体内是等价的。</p><p><br></p><p><b>C. 重新认识shift/reset</b></p><p>我们从shift里面获得一个k，上一节讨论过这个k应当是：</p><code lang="racket">(lambda (x) (reset expr ...))</code><p>考虑下面的表达式：</p><code lang="racket">(reset (shift k (shift k 1)))</code><p>结果是1，按照我们原来的规则进行求值：</p><code lang="racket">=&gt; (shift k 1)</code><p>遇到了同样的难题！外层没有reset，肯定是自动帮我们加的:</p><code lang="racket">=&gt; (reset (shift k 1))</code><p>所以，是时候拓展我们的求值规则了。</p><p>(reset val) =&gt; val<br>(reset E[(shift k expr)]) =&gt; (<b>reset</b> ((lambda (k) expr) (lambda (v) (<b>reset</b> E[v]))))<br> ; where E has no reset</p><p>我将其中需要添加reset的部分用粗体标出。</p><p><br></p><p><b>D. 与不确定性计算，list monad的关系</b></p><p>这是一个计算a+b是否等于c的程序，如果等于c，那么输出整个式子：</p><code lang="racket">(let ([a 1])
  (let ([b 1])
    (let ([c 2])
      (when (= (+ a b) c) (printf "~a+~a=~a\n" a b c)))))</code><p>可以把他们写成reset/shift的形式：</p><code lang="racket">(reset (let ([a (shift k (k 1))])
         (let ([b (shift k (k 1))])
           (let ([c (shift k (k 2))])
             (when (= (+ a b) c) (printf "~a+~a=~a\n" a b c))))))</code><p>两种写法是等价的，现在我们的a，b，c可能是多个不确定的值，也就是要进行多次计算。</p><p>我们可以把k想象成填一个值，进行一次计算，于是我们将函数k进行细胞分裂，多次计算。</p><code lang="racket">(reset (let ([a (shift k (k 1) (k 2) (k 3))])
         (let ([b (shift k (k 1) (k 2) (k 3))])
           (let ([c (shift k (k 1)(k 2)(k 3))])
             (when (= (+ a b) c) (printf "~a+~a=~a\n" a b c))))))</code><p>返回结果：</p><code lang="text">1+1=2
1+2=3
2+1=3</code><p>如果你觉得这个例子难以理解，那么下面的呢（输出 123）：</p><code lang="racket">(reset (let ([a (shift k (k 1) (k 2) (k 3))])
         (print a)))</code><p>list monad或是list comprehension是一种很好用的计算列表的方法，但是racket中没有do notation，必须用宏来替代。（实际上racket提供了for comprehension作为替代方案，我们这里为了演示reset和shift的作用不考虑。）</p><p>但是，我们也可以用reset和shift来直接实现：</p><code lang="racket">(define return list)
(define (in-list lst)
  (shift k
         (apply append
                (map k lst))))

(reset (let ([a (in-list '(1 2 3))]
             [b (in-list '(1 2 3))])
         (return (list a b))))</code><p>返回结果：</p><code lang="racket">'((1 1) (1 2) (1 3) (2 1) (2 2) (2 3) (3 1) (3 2) (3 3))</code><p><br></p><p><b>E. 与状态，state monad的关系</b></p><p>在一些语言中不支持赋值语句，无法保存状态，可以用state monad来模拟，同样地，state monad也可以用reset和shift模拟。</p><code lang="racket">(define (get)
  (shift k (lambda (s) ((k s) s))))
(define (put s)
  (shift k (lambda (-s)
             ((k (void)) s))))
(define ((return x) s) x)</code><p>下面展现了一个很有意思的例子：</p><code lang="racket">(define (tick)
  (let ([a (get)])
    (put (+ a 1))))
((reset (tick)
       (tick)
       (tick)
       (return (get))) 0)</code><p>试试看用shift和reset是否能实现其他monad。</p><p><br></p><p><b>F. 与stream，generator的关系</b></p><p>当我们需要表示无穷时，我们通常借助于stream与generator，而shift/reset与这二者间有着微妙的关系。</p><p>我们需要使用define-syntax-rule，不了解的请看：<a href="http://docs.racket-lang.org/reference/stx-patterns.html?q=define-syntax-rule#%28form._%28%28lib._racket%2Fprivate%2Fmisc..rkt%29._define-syntax-rule%29%29">12.1 Pattern-Based Syntax Matching</a>。</p><p>看下面的例子：</p><code lang="racket">(define s (reset (shift k (stream-cons 1 (k (void))))
                 (shift k (stream-cons 2 (k (void))))
                 (shift k (stream-cons 3 (k (void))))
                 (shift k '())))</code><p>这个例子生成了一个内容为1 2 3的stream，而内部的语法却接近generator！</p><p>我们可以写宏来让你看的更加清楚：</p><code lang="racket">(define-syntax-rule (stream/generate expr ...)
  (reset expr ...))
(define-syntax-rule (stream/yield expr ...)
  (shift k (stream-cons (begin expr ...) (k (void)))))

(define s (stream/generate
           (stream/yield 1)
           (stream/yield 2)
           (stream/yield 3)))

(define (integer-sequences x)
  (stream/generate (stream/yield x)
                   (integer-sequences (+ x 1))))</code><p><br></p><p>用shift和reset实现generator的任务更为艰巨，我们曾经在<a href="https://zhuanlan.zhihu.com/p/25749077">愉悦的scheme之旅（2）--用callcc合成控制流</a>介绍过用call/cc实现的generator，但是因为有了shift和reset一切都变简单了。</p><p>我们需要使用syntax-parameter，不了解的请看<a href="http://docs.racket-lang.org/reference/stxparam.html?q=syntax-parameter">12.5 Syntax Parameters</a>。</p><p>完整程序如下：</p><code lang="racket">#lang racket
(require racket/control racket/stxparam)
(define-syntax-parameter yield 
  (syntax-rules ()
    [(_ expr ...)
     (raise-syntax-error 'generator-err
                         "You must use yield in the body of generator")]))

(define-syntax-rule (generate expr ...)
  (letrec ([state (lambda ()
                 (syntax-parameterize
                     ([yield (syntax-rules ()
                               [(_ expr2 (... ...)) (shift k
                                                           (set! state
                                                                 (lambda () (k (void))))
                                                           (values expr2 (... ...)))])])
                   (reset (begin expr ... (void)))))])
    (lambda ()
      (state)
      )))
(define g (generate (yield 1)
                    (yield 6)
                    (yield 7)
                    (yield 10)))</code><p>宏会干扰我们的思考，我们看看展开后是什么样子吧。</p><p>racket的macro stepper是宏调试的利器，通过它，我们可以直接看到展开的结果：</p><code lang="racket">(syntax-parameterize
  ((yield
    (syntax-rules ()
      ((_ expr2 ...)
       (shift k (set! state (lambda () (k (void)))) (values expr2 ...))))))
  (reset (begin (yield 1) (yield 6) (yield 7) (yield 10) (void))))</code><p>这并不是最终的结果，因为syntax-parameterize没有展开,由于一些限制，我们手动展开一下：</p><code lang="racket">(reset (begin (shift k (set! state (lambda () (k (void)))) (values 1))
                (shift k (set! state (lambda () (k (void)))) (values 6))
                (shift k (set! state (lambda () (k (void)))) (values 7))
                (shift k (set! state (lambda () (k (void)))) (values 10))
                (void)))</code><p>现在应该看的很清楚了，第一次调用时返回1，state则变成了：</p><code lang="racket">(lambda () (reset (begin (void)
                         (shift k (set! state (lambda () (k (void)))) (values 6))
                         (shift k (set! state (lambda () (k (void)))) (values 7))
                         (shift k (set! state (lambda () (k (void)))) (values 10))
                         (void))))</code><p>剩下的应该不难了。</p><p><br></p><p><b>G.  算法</b></p><p>考虑函数genlist：</p><code lang="racket">(genlist '(1 2)) =&gt; '((1) (1 2))
(genlist '(1 2 3 4)) =&gt; '((1) (1 2) (1 2 3) (1 2 3 4))</code><p>有人会这么做：</p><code lang="racket">(define (genlist lst)
  (define (genlist lst last)
    (if (null? lst) '()
        (let ([x (append last (list (car lst)))])
          (cons x (genlist (cdr lst) x)))))
  (genlist lst '()))</code><p>可惜效率并不高，因为多次向队尾插入数据。</p><p>借助CPS，我们可以这么写（其实也可看成difference list）：</p><code lang="racket">(define (genlist lst)
  (define (genlist lst ctx)
    (if (null? lst)
        (list (ctx '()))
        (cons (ctx (list (car lst)))
              (genlist (cdr lst)
                       (lambda (x) (ctx (cons (car lst) x)))))))
  (if (null? lst)
      '()
      (genlist lst (lambda (x) x))))</code><p>考虑lst='(1 2 3 4)的情况，ctx分别是：</p><code lang="racket">ctx1 = [list* _]
ctx2 = [list* 1 _]
ctx3 = [list* 1 2 _]
ctx4 = [list* 1 2 3 _]</code><p>我们可以用shift和reset重写：</p><code lang="text">(define (genlist lst)
  (define (genlist lst)
    (if (null? lst)
        (shift k '())
        (shift k
               (cons (k (list (car lst)))
                     (reset (k (cons (car lst)
                                     (genlist (cdr lst)))))))))
  (reset (genlist lst)))</code><p>可以自己尝试推倒一下。</p><p>shift和reset还能做其他一些算法例如cps变换，anf变换，closure conversion等，这里不再详细介绍。</p><p>新手的课后作业：</p><p>尝试用shift和reset实现cps变换和anf变换。</p><p>老手的课后作业(难度非常大，做好心理准备）：</p><p>阅读</p><p><a href="http://www.schemeworkshop.org/2016/scheme16-paper6.pdf">Deriving Pure, Naturally-Recursive Operations for Processing Tail-Aligned Lists</a>(SCHEME AND FUNCTIONAL PROGRAMMING WORKSHOP 2016)</p><p>并尝试用shift和reset改写其中的算法。</p><p><br></p><h2><b>Part II : shift0 and reset0</b></h2><p><b>A. 求值规则</b></p><p>直接给出shift0和reset0的求值规则：</p><p>(reset0 <i>val</i>) <a href="file:///C:/Program%20Files/Racket/doc/reference/if.html?q=shift#%28form._%28%28lib._racket%2Fprivate%2Fletstx-scheme..rkt%29._~3d~3e%29%29">=&gt;</a> <i>val</i></p><p>(reset0 <i>E</i>[(shift0 <i>k</i> <i>expr</i>)]) <a href="file:///C:/Program%20Files/Racket/doc/reference/if.html?q=shift#%28form._%28%28lib._racket%2Fprivate%2Fletstx-scheme..rkt%29._~3d~3e%29%29">=&gt;</a> ((lambda (<i>k</i>) <i>expr</i>) (lambda (<i>v</i>) (<b>reset0</b> <i>E</i>[<i>v</i>])))</p><p>仔细比较与shift和reset的不同，你会发现是不是少了一个reset0，也就是说</p><code lang="racket">(reset0 (shift0 k expr)) =&gt; expr</code><p>而不是</p><code lang="racket">(reset (shift k expr)) =&gt; (reset expr)</code><p>那么思考下面这段程序，k1和k2分别是什么？</p><code lang="racket">(reset0 (+ 1
           (reset0 (+ 2 (shift0 k1 (shift0 k2 3))))))</code><p>k1是显而易见的：</p><code lang="racket">(lambda (x1)
  (reset0 (+ 2 x1)))</code><p>根据我们刚才讨论的内容继续求值：</p><code lang="racket">=&gt; (reset0 (+ 1 (shift0 k2 1)))</code><p>然后就能得到k2了</p><code lang="racket">(lambda (x2) (reset0 (+ 1 x2)))</code><p>这里，我们最先得到的k1，是内层的continuation，而k2，是外层的continuation。</p><p>我们可以将他们组合一下，得到整个continuation：</p><code lang="racket">(reset0 (+ 1 (reset0
              (+ 2 (shift0 k1
                           (shift0 k2 (k2 (k1 3))))))))</code><p>这与 (+ 1 (+ 2 3))是等价的。</p><p>我们捕获的continuation具有了层次这一概念，接下来我们该利用这一特点搞事了。</p><p><br></p><p><b>B. 括号解析</b></p><p>下面思考这个问题，给与一个含有左括号的标记'left 和右括号标记的'right的列表，将其转换成有层次的列表。</p><p>(parse-parens '(a b left c right)) =&gt; '(a b (c))</p><p>(parse-parens '(a left b left c right right d)) =&gt; '(a (b (c)) d)</p><p>现在我们考虑解析'(a b left c right)，以continuation的思维来看的话：</p><p>符号a ，ctx = (lambda (x) x)</p><p>符号b，ctx = (lambda (x) (cons a x))</p><p>符号left，ctx = (lambda (x) (list* a b x))</p><p>到这里我们就觉得有些困难了，因为left c right是一个整体，我们必须要解析到right才能知道这个整体的内容是'(c)。</p><p>我们可以考虑使用双层的continuation：</p><p>符号a，ctx-inner = (lambda (x) x) ctx-outer = (lambda (x) x)</p><p>符号b，ctx-inner = (lambda (x) x) ctx-outer = (lambda (x) (cons a x))</p><p>符号left,ctx-inner = (lambda (x) x) ctx-outer = (lambda (x) (list* a b x))</p><p>符号c ,  ctx-inner = (lambda (x) (cons c x)) ctx-outer = (lambda (x) (list* a b x))</p><p>符号right , 清空ctx-inner， 结果送入ctx-outer</p><p>ctx-inner = (lambda (x) x) ctx-outer = (lambda (x) (list* a b c x))</p><p>解析完毕，清空ctx-outer。</p><p>解析时ctx-inner负责处理内部，ctx-outer负责解析外部。</p><p>当我们的括号的层次不止两层时，我们的ctx也不止两个，无穷无尽的ctx组成了一个ctx栈。</p><p>现在，考虑如何用shift0和reset0实现：</p><code lang="racket">(define (parse-parens lst)
  (define (parse-parens lst)
    (if (null? lst) '()
        (match (car lst)
          ['left (reset0 (parse-parens (cdr lst)))]
          ['right (shift0 k1 (shift0 k2 (reset0 (k2 (cons (k1 '()) (parse-parens (cdr lst)))))))]
          [_ (shift0 k (reset0 (k (cons (car lst) (parse-parens (cdr lst))))))])))
  (reset0 (parse-parens lst)))</code><p>每次遇到left时，我们开辟一个新的reset0层，每次遇到right时，我们将该层次闭合。</p><p>这是利用同样办法写出来的表达式解析器：</p><code lang="racket">(define (parse-exp lst)
  (define (parse-exp lst)
    (if (null? lst) (error "No exp")
        (match lst
          [(list (? number? x)) x]
          [(list (? number? x) (and (or '+ '-) op) rst ...) (shift0 k1
                                                                    (shift0 k2 (shift0 k3
                                                                                       (reset0 
                                                                                        (list op  (k3 (k2 (k1 x))) (reset0
                                                                                                                    (reset0
                                                                                                                     (parse-exp rst))))))))]
          [(list (? number? x) (and (or '* '/) op) rst ...) (shift0
                                                             k1 (shift0 k2
                                                                        (reset0  (list op (k1 (k2 x)) (reset0
                                                                                                       (parse-exp rst))))))]
          [(list (? number? x) (and '^ op) rst ...) (shift0
                                                     k (reset0
                                                        (list op (k x) (parse-exp rst))))]
          )))
  (reset0 (reset0 (reset0 (parse-exp lst)))))</code><p>比如1+2*3^6-7*8的层次是：</p><p>1 + [ 2 * (3^6) ] - [ 7*8 ]</p><p>这就是为什么有三个优先级就需要三个reset0的缘故。</p><p>课后作业：</p><p>1.可以用shift0和reset0实现shift和reset吗，试试看。</p><p>2.这个表达式解析器存在一个缺陷，所有操作符都会视为左结合的，然而：</p><code lang="racket">(parse-exp '(1 ^ 2 ^ 3))  =&gt; '(^ (^ 1 2) 3)</code><p>指数运算应当是右结合的，请尝修复这个bug。</p><p></p><p></p><p></p><p></p><p></p><p></p><p></p><p></p><p></p><p></p>
<script async defer="defer" src="https://www.googletagmanager.com/gtag/js?id=UA-7909075-5"></script>
<script>
window.dataLayer = window.dataLayer || [];
function gtag(){ dataLayer.push(arguments); }
gtag('js', new Date());
gtag('config', 'UA-7909075-5');
</script>
<script>
var _hmt = _hmt || [];
</script>
<script async defer="defer" src="https://hm.baidu.com/hm.js?e3d40295e416616ddc21287da9646d31"></script>
</body>
</html>
