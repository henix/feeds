<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<title>某愉悦的scheme之旅（5）--从零开始实现模式匹配宏</title>
</head>
<body>
<p><a href="https://zhuanlan.zhihu.com/p/26000780">原文</a></p>
<div class="title-image"><img src="https://pic3.zhimg.com/v2-a39aff637256b6abfff064782fd4c0de_r.jpg" alt=""></div>鉴于前面几篇文章被人吐槽太难了，所以从这一篇开始降低难度，我们从写一个模式匹配开始学习宏。<p><b>为什么需要模式匹配</b></p><p>模式匹配可以大幅度减少你的代码，将你从大量嵌套if的地狱中拯救出来。</p><p>实际体验：写一个简单的lisp解释器，不使用模式匹配。</p><p>尝试学习racket里面提供的match函数改写。</p><p>模式匹配可以轻易的解构和绑定数据，常用的模式有：</p><code lang="text">(match something-to-match
      [(list a b ...) Exp]
      [var Exp]
      [`(a ,b ...) Exp]
[(struct-constructor-name a ...) Exp]
    [else Exp])</code><p><b>注：在racket中中括号和小括号是等效的，但是配对时必须一致。</b></p><p><b>第一步</b></p><code lang="text">(define-syntax pmatch
  (syntax-rules ()
    [(_ val (var res))
        (let ([t val])
          (let ([var t])
            res))]))
(pmatch '(1 . 2) (a (car a)))
(pmatch 2 (x (+ x 1)))</code><p>我们先写了一个简单的pmatch宏，仅仅支持直接绑定到一个变量，虽然看似简陋，不过，千里之行，始于足下。</p><p>将val计算出来并绑定到var</p><p><b>第二步</b></p><p>下面我们来支持Pair的配对：</p><code lang="text">(define-syntax pmatch
  (syntax-rules (cons)
    [(_ val ((cons a b) res))
        (let ([t val])
          (let ([a (car t)]
                [b (cdr t)])
            res))]
    [(_ val (var res))
        (let ([t val])
          (let ([var t])
            res))]
             ))
(pmatch '(1 . 2) [(cons a b) b])
(pmatch '(1 2) [(cons a b) b])
(pmatch '(3 4 5) [a a])</code><p>这段代码增加了对pair（cons）的处理，越来越实用了。</p><p>这里还有一个注意点，处理pair的代码要放在处理var的代码前面，否则（cons a b）会被直接匹配到 var上面，永远也不会用到处理pair的代码。</p><p><b>第三步</b></p><p>现在仅仅有一种匹配的模式一定不能满足要求，我们需要改成支持多种匹配模式的。如果所有模式都不能满足要求，提示错误。else和var 能满足所有的要求。<br></p><code lang="text">(define-syntax pmatch
  (syntax-rules (cons else)
    [(_ val ((cons a b) res) rest ...)
        (let ([t val])
          (if (pair? val)
          (let ([a (car t)]
                [b (cdr t)])
            res) (pmatch t rest ...)))]
    [(_ val (else res) rest ...)
        res]
    [(_ val (var res) rest ...)
        (let ([t val])
          (let ([var t])
            res))]
    [(_ val) (error "Pattern Matching Error" val)]
             ))

(pmatch '(1 2 3) ((cons a b) b) (else (void)))
(pmatch 23 ((cons a b) a) (a (+ 1 a)))
(pmatch 23 ((cons a b) a))</code><p>我们使用rest ...表示余下的模式，当不存在余下模式时，直接报错。</p><p><b>第四步：更多的cons</b></p><p>虽然我们已经能匹配cons这一模式了，但是还不够，我们现在还不支持(cons a (cons b c))这样的复杂模式.。</p><p>怎么办呢,我们先把pmatch改成Continuation-Passing-Style Macro，什么，宏也能cps?</p><p>没错，尽管这算是奇淫技巧，宏里面的奇淫技巧太多了，甚至有人拿syntax-rules来实现非卫生宏，还有人实现宏层面的apply和lambda，不过一股蛋疼的感觉。。</p><p>我们先写一个ppat宏，算是pmatch的简化+cps版，参数为fctx（失败时的cont），val（值），pattern （模式），res（结果表达式）。</p><code lang="text">(define-syntax ppat
  (syntax-rules (cons)
    [(_ fctx val (cons a b) res) (if (pair? val)
                                 (ppat fctx (car val) a
                                       (ppat fctx (cdr val) b res))
                                 fctx)]
    [(_ fctx val var res) (let ((var val))
                            res)]))

(ppat (display "failed") '((1 . 2) 3) (cons (cons a b) (cons c d)) (list c b a))
(ppat (display "failed") '(2 3) (cons (cons a b) (cons c d)) (list c b a))</code><p>ppat宏完美解决了复杂嵌套模式的问题，现在我们要开始构建我们新的pmatch宏了。</p><code lang="text">(define-syntax ppat
  (syntax-rules (cons)
    [(_ fctx val (cons a b) res) (if (pair? val)
                                 (ppat fctx (car val) a
                                       (ppat fctx (cdr val) b res))
                                 fctx)]
    [(_ fctx val var res) (let ((var val))
                            res)]))

(define-syntax pmatch
  (syntax-rules ()
    [(_ val) (error "Pattern Matching Error")]
    [(_ val (pattern res) rest ...)
         (let ((t val))
           (ppat (pmatch t rest ...)
                 t pattern res))]))

(pmatch '(1 2 3) ((cons (cons x y) z) z) ((cons x y) x))
(pmatch '(1 2 3) ((cons x (cons y (cons z t))) (list z y x)))</code><p><b>第四步：更多的模式，更多的模式</b></p><p>现在我们还没有一些对常量的匹配，我们需要扩展ppat宏。</p><p>不过我们没有办法在宏里面判断输入的参数是否是一个常量，不过也有变通的办法，比如用const表示常量模式，虽然可能麻烦一些。</p><code lang="text">(define-syntax ppat
  (syntax-rules (cons else const)
    [(_ fctx val (const cs) res) (if (equal? cs val)
                                     res
                                     fctx)]
    [(_ fctx val else res) res]
    [(_ fctx val (cons a b) res) (if (pair? val)
                                 (ppat fctx (car val) a
                                       (ppat fctx (cdr val) b res))
                                 fctx)]
    [(_ fctx val var res) (let ((var val))
                            res)]))</code><p>这是最终版的ppat，你可以尝试改进他。</p><p><b>其实我们现在离真正好用的pattern matching还远着呢。。。但是演示其是如何实现的对提高水平是大有裨益的。</b></p><p>结尾：用pmatch写一个解释器（typed racket）</p><p>先定义数据结构：</p><code lang="text">(struct Add ([x : Exp][y : Exp]))
(struct Sub ([x : Exp][y : Exp]))
(struct Mul ([x : Exp](y : Exp)))
(struct Div ([x : Exp][y : Exp]))
(define-type Exp (U Number Add Sub Mul Div))
(: interp (-&gt; Exp Number))</code><p><br>然后给我们的ppat增加对add，sub，mul，div的匹配。</p><code lang="text">(define-syntax ppat
  (syntax-rules (cons else const Add Sub Mul Div)
    [(_ fctx val (Div a b) res) (if (Div val)
                                    (ppat fctx (Div-x val) a
                                          (ppat fctx (Div-y val) b res))
                                    fctx)]
    [(_ fctx val (Mul a b) res) (if (Mul? val)
                                    (ppat fctx (Mul-x val) a
                                          (ppat fctx (Mul-y val) b res))
                                    fctx)]
    [(_ fctx val (Sub a b) res) (if (Sub? val)
                                    (ppat fctx (Sub-x val) a
                                          (ppat fctx (Sub-y val) b res))
                                    fctx)]
    [(_ fctx val (Add a b) res) (if (Add? val)
                                    (ppat fctx (Add-x val) a
                                          (ppat fctx (Add-y val) b res))
                                    fctx)]
    [(_ fctx val (const cs) res) (if (equal? cs val)
                                     res
                                     fctx)]
    [(_ fctx val else res) res]
    [(_ fctx val (cons a b) res) (if (pair? val)
                                 (ppat fctx (car val) a
                                       (ppat fctx (cdr val) b res))
                                 fctx)]
    [(_ fctx val var res) (let ((var val))
                            res)]))</code><p>然后开始写interp函数：</p><code lang="text">(define interp
  (lambda (exp)
    (pmatch exp
            [(Add x y) (+ (interp x) (interp y))]
            [(Sub x y) (- (interp x) (interp y))]
            [(Mul x y) (* (interp x) (interp y))]
            [(Div x (const 0)) (error "DIVIDE ERROR")]
            [(Div x y) (/ (interp x) (interp y))]
            [num (if (number? num)
                     num
                     (error "INTERP ERROR"))])))
(interp (Add (Sub 1 2) (Mul 1 2)))
(interp (Add (Div 3 0) 3))</code><p>呼，第五篇文章完结，有任何问题尽管提出。</p><p>补充 由于时间仓促，这样实现的模式匹配存在一个微小的问题，但仅仅需要做很少的改动，我们在下一篇解决，聪明的你不妨先尝试解决这个问题。</p>
<script async defer="defer" src="https://www.googletagmanager.com/gtag/js?id=UA-7909075-5"></script>
<script>
window.dataLayer = window.dataLayer || [];
function gtag(){ dataLayer.push(arguments); }
gtag('js', new Date());
gtag('config', 'UA-7909075-5');
</script>
<script>
var _hmt = _hmt || [];
</script>
<script async defer="defer" src="https://hm.baidu.com/hm.js?e3d40295e416616ddc21287da9646d31"></script>
</body>
</html>
