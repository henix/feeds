<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<title>使用宏给racket加类型</title>
</head>
<body>
<p><a href="https://zhuanlan.zhihu.com/p/26971686">原文</a></p>
<div class="title-image"><img src="https://pic4.zhimg.com/v2-b03d68c12ceb332acecb9b516dc9b207_r.jpg" alt=""></div><p><b> 前言：</b></p><p>首先，感谢<a class="member_mention" href="http://www.zhihu.com/people/e60f1b12f4f63270fe9c7886e9541be7" data-editable="true" data-title="@qww6" data-hovercard="p$b$e60f1b12f4f63270fe9c7886e9541be7" data-hash="e60f1b12f4f63270fe9c7886e9541be7">@qww6</a> 解答了我许多问题，如果有任何问题，可以在下面的评论区提出。</p><p>一直以来，racket语言因为没有静态类型系统而为人诟病，不少人认为这门语言辣鸡。但是，racket中远比scheme强大的宏系统带给我们了一线希望，我们可以通过宏给racket加类型，而且不需要等待官方更新typed racket，就可以给我们的type system加上各种各样的功能。</p><p>目前已知的给racket加类型的方法至少有两种：1.Typed Racket所采用的将程序展开为core form后进行类型检查的方法。</p><p>2.Type System as Macros 中所提到的一边展开一边检查的方法（type tailoring）。 </p><p>我们这篇文章的采用的方法是第二种。</p><p><b> 前置知识：</b></p><p>我们的读者至少应该已经了解并会使用scheme中的syntax-case宏（不会的点这里：<a href="http://scheme.com/tspl4/syntax.html#./syntax:h0" data-editable="true" data-title="Syntactic Extension">Syntactic Extension</a>）</p><p>由于我们要对类型进行检查，所以我们必须拦截所有的app，racket中提供了一个#%app的形式，所有的函数应用都会被转化为(#%app func . args)的形式。</p><p>类似的，我们可以用重写lambda宏，#%datum的形式来进行类型检查。</p><code lang="racket">(provide (rename-out [app #%app]))
(define-syntax (app stx)
  (syntax-case stx ()
    [(_ func . args) #'(#%app func . args)]))</code><p> 相关链接：<a href="https://docs.racket-lang.org/guide/module-languages.html?q=creating%20languages" data-editable="true" data-title="17.1 Module Languages">17.1 Module Languages</a></p><p><b> 我们的简易类型系统:</b></p><p>我们将会在下面实现一个简单的类型系统，它将支持以下类型：</p><p> type=int|string|bool|(-&gt; type type ...)</p><p>看起来确实很简陋啊， 但是这是一个从没有静态类型系统到有静态类型系统的质变。</p><p>另外，我们的类型系统不支持自动的currying（scheme传统）。</p><p>除此之外，我们还将支持对我们程序的一些优化。 </p><p><b>常量的类型：</b> </p><p>给程序中出现的数字，字符串和布尔值加上类型是极为简单的，我们只需要插进#%datum就行了，最后，我们用syntax-property(<a href="https://docs.racket-lang.org/reference/stxprops.html?q=syntax-property#%28def._%28%28quote._~23~25kernel%29._syntax-property%29%29" data-editable="true" data-title="12.7 Syntax Object Properties">12.7 Syntax Object Properties</a>)给typing terms加上类型信息。</p><code lang="racket">(require (for-syntax syntax/parse))
(provide (rename-out [app #%app][datum #%datum]))
 (define-syntax (datum stx)
  (syntax-parse stx
    [(d . c:integer) (syntax-property #'(#%datum . c)
                                  'type
                                  #'int)]
    [(d . c:boolean) (syntax-property #'(#%datum . c)
                                   'type
                                   #'bool)]
    [(d . c:str) (syntax-property #'(#%datum . c)
                                  'type
                                  #'string)]
    [(d . any) (raise-syntax-error 'type-system-error "Don't Support this type." #'any)]
     ))</code><p> 相关链接（syntax-parse）：<a href="https://docs.racket-lang.org/syntax/Parsing_Syntax.html?q=syntax-parse#%28form._%28%28lib._syntax%2Fparse..rkt%29._syntax-parse%29%29" data-editable="true" data-title="1.3 Parsing Syntax">1.3 Parsing Syntax</a></p><p><b>一些primitive operator的类型：</b></p><p>我们这个类型系统还支持一些primop，它们是+,-,*,/,string-append,and,or,not(注意了，这里and和or是宏，不是函数，我们必须要对它们做额外的检查）。</p><p>我们将会用到identifier macro的知识：<a href="https://docs.racket-lang.org/guide/pattern-macros.html?q=identifier%20macro#%28tech._identifier._macro%29" data-editable="true" data-title="16.1 Pattern-Based Macros">16.1 Pattern-Based Macros</a>，为了方便，我们写一个macro-transformer实现相同的功能。</p><code lang="racket">(define-for-syntax (make-val-transformer val)
  (lambda (stx)
    (syntax-case stx ()
      [_ (identifier? stx) val]
      [(_ . args) #`(app #,val . args)])))</code><p>对于简单的函数+，-，*,/,string-append,not，我们可以直接写：</p><br><code lang="racket"> (provide (rename-out [app #%app][datum #%datum]
                     [add +][sub -][div /][mul *]
                     [strapd string-append]
                     [tnot not]))
 (define-syntax add (make-val-transformer (syntax-property #'(lambda (x y) (+ x y)) 'type #'(-&gt; int int int))))
(define-syntax sub (make-val-transformer (syntax-property #'(lambda (x y) (- x y)) 'type #'(-&gt; int int int))))
(define-syntax mul (make-val-transformer (syntax-property #'(lambda (x y) (* x y)) 'type #'(-&gt; int int int))))
(define-syntax div (make-val-transformer (syntax-property #'(lambda (x y) (/ x y)) 'type #'(-&gt; int int int))))
(define-syntax strapd (make-val-transformer (syntax-property #'(lambda (x y) (string-append x y)) 'type #'(-&gt; string string string))))
(define-syntax tnot (make-val-transformer (syntax-property #'not 'type #'(-&gt; bool bool))))</code><br><p> 对于上面这段冗余的代码，你也可以写一个宏来简化它们，不过，这不是本篇文章的目的。</p><p> 接下来我们将会对app做一个检查。</p><p><b>Type Check for app：</b> </p><p>我们首先为app加上type check，检查参数类型，并返回正确的类型。</p><code lang="racket"> (define-syntax (app stx)
  (syntax-case stx ()
    [(_ func . args) (let ([types (map
                                   (lambda (x)
                                     (let ([t (syntax-property x 'type)])
                                       (if t t (raise-syntax-error 'type-system-error
                                                                   "missing type:"
                                                                   x))))
                                      (syntax-&gt;list #'(func . args)))])
                       (syntax-property #'(#%app func . args) 'type
                                        (type-app types)))]))</code>接下来定义type-app，它的行为遵循以下的规则。<p>type-app (-&gt; a b) a=b</p><p>type-app (-&gt; a b c ...) a rest ...=type-app (-&gt; b c ...) rest ...</p><p>type-app other=error </p><p>补充：实际上这个type-app还写的不够好，但是我懒，你们饶了我把。 </p><code lang="racket">(define-for-syntax (type-app stx errorinf)
  (syntax-case stx ()
    [((-&gt; a b) c) (free-identifier=? a c) #'b]
    [((-&gt; a b c ...) d e ...) (free-identifier=? a d)
                              (type-app #'((-&gt; b c ...) e ...) errorinf)]
    [others (raise-syntax-error 'type-system-error
                                "error when apply the arguements to function"
                                errofinf)]))</code><br><p>测试我们的程序， 发现一个问题，它会提示我们已经支持的int，string，bool没有类型，所以我们必须要对app进行修改,让app的参数先展开（<a href="https://docs.racket-lang.org/reference/stxtrans.html?q=local-expand#%28def._%28%28quote._~23~25kernel%29._local-expand%29%29" data-editable="true" data-title="12.4 Syntax Transformers">12.4 Syntax Transformers</a>），才能获取到参数的type。</p><code lang="racket">(define-syntax (app stx)
  (syntax-case stx ()
    [(_ func . args) (let ([types #`(#,@(map
                                   (lambda (x)
                                     (let ([t (syntax-property (local-expand x
                                                                (syntax-local-context)
                                                                (list #'#%datum))
 'type)])
                                       (if t t (raise-syntax-error 'type-system-error
                                                                   "missing type:"
                                                                   x))))
                                      (syntax-&gt;list #'(func . args))))])
                       (syntax-property #'(#%app func . args) 'type
                                        (type-app types #'(#%app func . args))))]))</code><br><p>这时，我们测试一下，发现各种运算都表现的很好，报错也是正常的。</p><p><b>Type check for lambda：</b> </p><p>下面我们要正式支持lambda了，先把lambda的form写一下：</p><p>(lambda ([x : type] ...) expr) </p><p>大致思路是，将x的type代入expr中（make-val-transformer） ，然后根据获得的type生成整个lambda term的type，好累。</p><code lang="racket"> (define-for-syntax (gen-type pa f)
  (syntax-case pa ()
    [(a ...) #`(-&gt; a ... #,f)]
    ))
 (define-syntax (lam stx)
  (syntax-case stx ()
    [(_ ([var : types] ...) body)
      (let* ([body (local-expand #'(lambda (var ...)
                        (let-syntax ([var (make-val-transformer (syntax-property #'var 'type #'types))] ...)
                          body))
                    (syntax-local-context) '())]
            [bodyt (syntax-case body ()
                     [(_ args (letvalues () (letvalues2 () e)))
                      (syntax-property #'e 'type)]
                     [(_ (lam args (letvalues () (letvalues2 () e))))
                      (syntax-property #'e 'type)]
                     )]
            )
        (if bodyt
            (syntax-property body 'type (gen-type #'(types ...) bodyt))
            (raise-syntax-error 'type-system-error
                          "the body of lambda doesn;t have a type"
                          #'(lambda ([var : types] ...) body)))
        )]))</code><p> 我们可以添加一个方便的语法形式print-type:来打印表达式的type：</p><code lang="racket">(define-syntax (print-type: stx)
  (syntax-case stx ()
    [(_ e) #`(begin
               (display '#,(syntax-property (local-expand #'e (syntax-local-context) '()) 'type))
               (newline))]))</code><p>一些测试：</p><br><code lang="racket"> (require "type.rkt")
((lambda ([x : int][y : int]) (* 1 (+ 4 x))) 2 3)
((lambda ([x : string][y : string]) (string-append x y)) "dfd" "dfdf")
(+ 1 2)
(print-type: 3)
(print-type: (not #t))
(print-type: (lambda ([x : int] [y : string]) y))
(print-type: (lambda ([x : int]) (lambda ([y : int]) x)))
(print-type: (lambda ([x : int][y : int]) x))
(print-type: +)

result：
6
"dfddfdf"
3
int
bool
(-&gt; int string string)
(-&gt; int (-&gt; int int))
(-&gt; int int int)
(-&gt; int int int)</code><p><b>一点改进：</b></p><p>上面我们在判断两个类型是否相等时直接使用了非常naive的free-identifier=?，我们必须要改进它，采用把type全部展开后再逐个比较的方法。</p><code lang="racket">(define-for-syntax (type=? x y)
  (or (and (identifier? x) (identifier? y) (free-identifier=? x y))
      (and (stx-null? x) (stx-null? y))
      (and (type=? (stx-car x) (stx-car y))
           (type=? (stx-cdr x) (stx-cdr y)))))</code><br><p>最后，我们需要type-alias，比如</p><p>(define-type  another-int int)</p><p>(define-type op (-&gt; int int int)) </p><p>甚至</p><p>(define-type (f3 t) (-&gt; t t t))</p><p>看起来很复杂，其实我们根本不需要自己动手，只要借刀（宏）杀人 就可以了。</p><p>为了防止type展开的时候出错，我们需要先定义：</p><code lang="racket">(define int 'type-int)
(define string 'type-string)
(define bool 'type-bool)
(define -&gt; (lambda l (error "ooh，you can't call me at runtime")))</code>然后把type=?中的参数用local-expand展开。<p>展开时我们会发现type-check无法通过，原因是-&gt;没有type，我们可以把-&gt;单独弄出来，而不是让app进行check。</p><code lang="racket">(define -&gt;internal (lambda l (error "ooh")))
(define-syntax -&gt;
  (syntax-rules ()
    [(_ a ...) (#%app -&gt;internal a ...)])) </code><p> 我们写出不带参数的define-type:</p><code lang="racket"> (define-syntax (define-type stx)
  (syntax-case stx ()
    [(_ tname tvar) #'(define-syntax tname (make-val-transformer #'tvar))])</code><br><p>(print-type: (lambda ([x : another-int] [y : another-int]) (+ x y)))的结果是(-&gt; another-int another-int int)，但是其实和(-&gt; int int int)是一样的。</p><p>如何实现(define-type (tc xx yy) e)这种形式，留给读者自己动手实现。 </p><p>由于我们的类型都是已经经过检查的，所以根本不需要运行时检查，可以把+的内部实现替换成unsafe-fx+，以此类推。</p><p>最后我们的type system还是不完善的，最好给speical form比如define（一般是core form）加上支持，不过这就留作读者朋友的作业了。 </p><p>另外，对于这种方法感兴趣的同学，可以学习racket平台上的turnstile语言，只要写出对应的typing rule，就可以轻易实现自己的类型系统啦。</p><p>附录：全部源码</p><br><code lang="racket"> #lang racket
(require (for-syntax syntax/parse syntax/stx))
 (require racket/unsafe/ops)
(provide (rename-out [app #%app][datum #%datum]
                     [add +][sub -][div /][mul *]
                     [strapd string-append]
                     [tnot not][lam lambda]))
(provide print-type: int string bool -&gt; define-type)
(define int 'type-int)
(define string 'type-string)
(define bool 'type-bool)
(define -&gt;internal (lambda l (error "ooh,you can't call me at runtime")))
(define-syntax -&gt;
  (syntax-rules ()
    [(_ a ...) (#%app -&gt;internal a ...)]))
(define-syntax (define-type stx)
  (syntax-case stx ()
    [(_ tname tvar) #'(define-syntax tname (make-val-transformer #'tvar))]
    ))
(define-for-syntax (type-app stx errorinf)
  (syntax-case stx ()
    [((-&gt; a b) c) (type=e? #'a #'c) #'b]
    [((-&gt; a b c ...) d e ...) (type=e? #'a #'d)
                              (type-app #'((-&gt; b c ...) e ...) errorinf)]
    [others (raise-syntax-error 'type-system-error
                                "error when apply the arguements to function"
                                errorinf)]))
(define-for-syntax (type=e? x y) (type=? (local-expand x (syntax-local-context) '())
                                         (local-expand y (syntax-local-context) '())))
(define-for-syntax (type=? x y)
  (or (and (identifier? x) (identifier? y) (free-identifier=? x y))
      (and (stx-null? x) (stx-null? y))
      (and (type=? (stx-car x) (stx-car y))
           (type=? (stx-cdr x) (stx-cdr y)))))
(define-syntax (app stx)
  (syntax-case stx ()
    [(_ func . args) (let ([types #`(#,@(map
                                   (lambda (x)
                                     (let ([t (syntax-property (local-expand x
                                                                (syntax-local-context)
                                                                '())
 'type)])
                                       (if t t (raise-syntax-error 'type-system-error
                                                                   "missing type:"
                                                                   x))))
                                      (syntax-&gt;list #'(func . args))))])
                       
                       (syntax-property #'(#%app func . args) 'type
                                        (type-app types #'(#%app func . args))))]))
(define-syntax (datum stx)
  (syntax-parse stx
    [(d . c:integer) (syntax-property #'(#%datum . c)
                                  'type
                                  #'int)]
    [(d . c:boolean) (syntax-property #'(#%datum . c)
                                   'type
                                   #'bool)]
    [(d . c:str) (syntax-property #'(#%datum . c)
                                  'type
                                  #'string)]
    [(d . any) (raise-syntax-error 'type-system-error "Don't Support this type." #'any)]
     ))
(define-syntax (lam stx)
  (syntax-case stx ()
    [(_ ([var : types] ...) body)
      (let* ([body (local-expand #'(lambda (var ...)
                        (let-syntax ([var (make-val-transformer (syntax-property #'var 'type #'types))] ...)
                          body))
                    (syntax-local-context) '())]
            [bodyt (syntax-case body ()
                     [(_ args (letvalues () (letvalues2 () e)))
                      (syntax-property #'e 'type)]
                     [(_ (lam args (letvalues () (letvalues2 () e))))
                      (syntax-property #'e 'type)]
                     )]
            )
        (if bodyt
            (syntax-property body 'type (gen-type #'(types ...) bodyt))
            (raise-syntax-error 'type-system-error
                          "the body of lambda doesn;t have a type"
                          #'(lambda ([var : types] ...) body)))
        )]))
(define-syntax (print-type: stx)
  (syntax-case stx ()
    [(_ e) #`(begin
               (display '#,(syntax-property (local-expand #'e (syntax-local-context) '()) 'type))
               (newline))]))
(define-for-syntax (gen-type pa f)
  (syntax-case pa ()
    [(a ...) #`(-&gt; a ... #,f)]
    ))
              
(define-for-syntax (make-val-transformer val)
  (lambda (stx)
    (syntax-case stx ()
      [_ (identifier? stx) val]
      [(_ . args) #`(app #,val . args)])))
(define-syntax add (make-val-transformer (syntax-property #'(lambda (x y) (unsafe-fx+ x y)) 'type #'(-&gt; int int int))))
(define-syntax sub (make-val-transformer (syntax-property #'(lambda (x y) (unsafe-fx- x y)) 'type #'(-&gt; int int int))))
(define-syntax mul (make-val-transformer (syntax-property #'(lambda (x y) (unsafe-fx* x y)) 'type #'(-&gt; int int int))))
(define-syntax div (make-val-transformer (syntax-property #'(lambda (x y) (unsafe-fx/ x y)) 'type #'(-&gt; int int int))))
(define-syntax strapd (make-val-transformer (syntax-property #'(lambda (x y) (string-append x y)) 'type #'(-&gt; string string string))))
(define-syntax tnot (make-val-transformer (syntax-property #'not 'type #'(-&gt; bool bool))))
</code>
<script async defer="defer" src="https://www.googletagmanager.com/gtag/js?id=UA-7909075-5"></script>
<script>
window.dataLayer = window.dataLayer || [];
function gtag(){ dataLayer.push(arguments); }
gtag('js', new Date());
gtag('config', 'UA-7909075-5');
</script>
<script>
var _hmt = _hmt || [];
</script>
<script async defer="defer" src="https://hm.baidu.com/hm.js?e3d40295e416616ddc21287da9646d31"></script>
</body>
</html>
