<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<title>Haskell 中的“定理证明”</title>
</head>
<body>
<p><a href="https://zhuanlan.zhihu.com/p/31567423">原文</a></p>
<div class="title-image"><img src="https://pic4.zhimg.com/v2-24cc9824870cdcc4194b2c0ec0097d23_r.jpg" alt=""></div><p>题图：鲸 | 花生( •̀ ω •́ ) [pixiv] <a href="https://www.pixiv.net/member_illust.php?mode=medium&amp;illust_id=66023124">https://www.pixiv.net/member_illust.php?mode=medium&amp;illust_id=66023124</a></p><h2><b>这篇文章讲了什么</b></h2><p><i>这是一篇休闲向文章</i></p><p>总结了 Codewars 上面的几道证明题的思想并加以延拓，希望读者能在看完这篇文章之前/之后解决练习中的题目。</p><p><br></p><p>下面是正文</p><hr><p>众所周知，Haskell 并没有真正意义上的 dependent type，但通过一些技巧 （利用GADT）可以“模拟”dependent type，并可以通过其证明一些定理。</p><h2><b>利用 GADT 模拟 Dependent Type</b></h2><p>首先我们需要作为<b>值</b>的类型</p><code lang="haskell"> data Z
 data S n</code><p>注意到，这里的 <code class="inline">Z</code> 和 <code class="inline">S</code>n 都只是单纯的类型，你甚至不能构造出相应类型的值出来。</p><p>所以我们需要把它们与实际存在的值联系起来</p><code lang="haskell"> data Nat a where
      Zero :: Nat Z
      Succ :: Nat a -&gt; Nat (S a)</code><p>于是我们有</p><code lang="haskell">  (Succ Zero) :: Nat (S Z)
  (Succ (Succ Zero)) :: Nat (S (S Z))</code><p>这样我们获得了在类型层次上的自然数，并保持了它与实际的值的同构关系。</p><p>光有自然数是没有意义的，我们需要赋予它们运算的能力。</p><p>那么 <code class="inline">Nat n</code> 与 <code class="inline">Nat m</code> 相加的结果应该是什么呢？</p><p>首先它应该是一个 <code class="inline">Nat</code> ，其次它的类型应该对应于两数相加的结果。</p><p>借助于 GHC 的 Type Family 与 Type Operator 扩展，我们可以定义出 合理的 类型层面上的自然数的加法与乘法</p><code lang="haskell">  type family (:+:) (n :: *) (m :: *) :: *
  type instance Z   :+: m = m
  type instance S n :+: m = S (n :+: m)
  
  type family (:*:) (n :: *) (m :: *) :: *
  type instance Z   :*: m = Z
  type instance S n :*: m = m :+: (n :*: m)</code><p>然后是数据层面的自然数</p><code lang="haskell">  infixl 4 +
  (+) :: Nat n -&gt; Nat m -&gt; Nat (n :+: m)
  Zero + a = a
  (Succ x) + a = Succ (x + a)
  
  infixl 5 *
  (*) :: Nat n -&gt; Nat m -&gt; Nat (n :*: m)
  Zero * _ = Zero
  (Succ x) * m = m + x * m</code><p>有</p><code lang="haskell">  (Succ (Succ Zero)) * (Succ (Succ Zero)) :: Nat (S (S (S (S Z))))</code><p>基于类型上的自然数，我们可以定义 <code class="inline">Vec</code> (在类型中包含了长度信息的 <code class="inline">List</code>)了</p><code lang="haskell">  data Vec a n where
      VNil :: Vec a Z
      VCons :: a -&gt; Vec a n -&gt; Vec a (S n)</code><p>长度为 <code class="inline">n</code> 的 <code class="inline">Vec</code> 与长度为 <code class="inline">m</code> 的 <code class="inline">Vec</code> 连接起来之后的长度为 <code class="inline">n + m</code></p><code lang="haskell">  (++) :: Vec a n -&gt; Vec a m -&gt; Vec a (n :+: m)
  VNil ++ ys = ys
  VCons x xs ++ y = VCons x (xs ++ y)</code><p>把长度为 <code class="inline">m</code> 的 <code class="inline">Vec</code> 重复 <code class="inline">n</code> 次之后得到的 <code class="inline">Vec</code> 的长度为 <code class="inline">n * m</code></p><code lang="haskell">  repeat :: Nat n -&gt; Vec a m -&gt; Vec a (n :*: m)
  repeat Zero _ = VNil
  repeat (Succ x) xs = xs ++ repeat x xs</code><p>有了长度信息，就有类型安全的 <code class="inline">head</code> 和 <code class="inline">tail</code> 了</p><code lang="haskell">  headV :: Vec a (S n) -&gt; a
  headV (VCons x _) = x
  
  tailV :: Vec a (S n) -&gt; Vec a n
  tailV (VCons _ xs) = xs</code><p>因为 <code class="inline">Vec</code> 作为 GADT 的类型在这里受到了第二个类型参数为 <code class="inline">S n</code> 的束缚，所以可以保证这里的 <code class="inline">Vec</code> 不会是 <code class="inline">VNil</code> ，这两个函数都是 <b>完全</b> 的。</p><p>借鉴之前定义类型层面上自然数的加法与乘法的思想，我们可以定义相应的减法、求最大值、最小值等。</p><code lang="haskell">  type family (:-:) (n :: *) (m :: *) :: *
  type instance n :-: Z = n
  type instance Z :-: m = Z
  type instance S n :-: S m = n :-: m
  
  type family (:^:) (n :: *) (m :: *) :: *
  type instance Z :^: n = n
  type instance n :^: Z = n
  type instance S n :^: S m = S (n :^: m)
  
  type family (:~:) (n :: *) (m :: *) :: *
  type instance Z :~: m = Z
  type instance n :~: Z = Z
  type instance S n :~: S m = S (n :~: m)</code><p>(这里使用了 <code class="inline">:~:</code> 和 <code class="inline">:^:</code> 分别代表求最大值和最小值)</p><p>于是有</p><code lang="haskell">  zip :: Vec a n -&gt; Vec b m -&gt; Vec (a, b) (n :~: m)
  zip VNil _ = VNil
  zip _ VNil = VNil
  zip (VCons x xs) (VCons y ys) = VCons (x, y) (zip xs ys)</code><h2><b>进行简单的定理证明</b></h2><p><code class="inline">Haskell</code> 中没有 Prop ，所以我们得从头开始描述“相等”这个概念。</p><p>首先定义代表两个类型层面的自然数相等的类型</p><code lang="haskell"> data Equal a b where
      EqZ :: Equal Z Z
      EqS :: Equal a b -&gt; Equal (S a) (S b)</code><p>借助于 Type Operator 我们可以写成</p><code lang="haskell"> type a === b = Equal a b</code><p>相等关系具有自反性</p><code lang="haskell">  refl :: Nat n -&gt; n === n
  refl Zero = EqZ
  refl (Succ x) = EqS $ refl x</code><p>相等关系具有对称性</p><code lang="haskell">  symm :: a === b -&gt; b === a
  symm EqZ = EqZ
  symm (EqS x) = EqS $ symm x</code><p>相等关系具有传递性</p><code lang="haskell">  (&lt;=&gt;) :: a === b -&gt; b === c -&gt; a === c
  EqZ &lt;=&gt; EqZ = EqZ
  EqS x &lt;=&gt; EqS y = EqS $ x &lt;=&gt; y</code><p>因为 Type Operator 也能定义优先级，所以下面给出：</p><code lang="haskell">  infixl 2 ===
  infixl 4 :+:
  infixr 4 :-:
  infixl 5 :*:</code><p> 来减少括号的个数</p><h2>加法结合律</h2><p>那么我们来试着证明一下加法的结合律</p><code lang="haskell">  plusComb :: Nat n -&gt; Nat m -&gt; Nat p -&gt; n :+: (m :+: p) === n :+: m :+: p</code><p>我们对 <code class="inline">n</code> 进行归纳：</p><p> 当 <code class="inline">n</code> 为 0 的时，在加法中的 0 会直接消除，所以使用 <code class="inline">refl (m + p)</code> 就好了；</p><p> 当 <code class="inline">n</code> 为 <code class="inline">S n1</code> 时，把等式两边的 S 用 <code class="inline">EqS</code> 提取出来，再使用归纳假设就可以了：</p><code lang="haskell">  plusComb :: Nat n -&gt; Nat m -&gt; Nat p -&gt; n :+: (m :+: p) === n :+: m :+: p
  plusComb Zero p q = refl (p + q)
  plusComb (Succ n) m p = EqS $ plusComb n m p</code><h2>加法交换律</h2><p>那么再尝试一下稍微困难一点的加法交换律吧</p><code lang="haskell">  plusCommutes :: Nat n -&gt; Nat m -&gt; n :+: m === m :+: n</code><p>当 <code class="inline">m</code> 为 0 的时，证明很平凡。我们需要的是这么一个类型：</p><code lang="haskell">  Nat n -&gt; n :+: Z === n</code><p>只需要对 n 进行归纳就好了。</p><code lang="haskell">  nPlusZero :: n === n -&gt; n === n :+: Z
  nPlusZero EqZ        = EqZ
  nPlusZero (EqS eq) = EqS $ nPlusZero eq</code><p>当 <code class="inline">m</code> 为 <code class="inline">S m1</code> 时，我们需要的东西的类型为</p><code lang="haskell">  (n :+: S m1) === S (m1 :+: n)</code><p>根据归纳法，我们可以得到</p><code lang="haskell">EqS (plusCommutes n m1) :: S (m1 :+: n) === S (n :+: m1)</code><p>借助于相等关系的传递性，目标变成了</p><code lang="haskell"> (n :+: S m1) === S (n :+: m1)</code><p>而这个命题的证明也只需要对 <code class="inline">n</code> 进行归纳</p><code lang="haskell">  nPlusSm :: Nat n -&gt; Nat m -&gt; n :+: S m === S (n :+: m)
  nPlusSm Zero      m = EqS $ refl m
  nPlusSm (Succ n) m = EqS $ nPlusSm n m</code><p>把这些步骤拼起来，就得到了我们最终的证明</p><code lang="haskell">  plus2 :: Nat n -&gt; Nat m -&gt; n :+: m === m :+: n
  plus2 n Zero = symm $ nPlusZero $ refl n
  plus2 n (Succ m) = nPlusSm n m &lt;=&gt; symm (EqS $ plus2 m n)</code><h2>乘法交换律</h2><p>看了加法我们再来看乘法</p><code lang="haskell"> multCommute :: Nat n -&gt; Nat m -&gt; n :*: m === m :*: n</code><p>证明的方法和加法几乎一样，仍然从归纳法入手，对 n 进行归纳。中间需要用到一些引理，比如</p><code lang="haskell">  multCommuteS :: Nat n -&gt; Nat m -&gt; m :*: S n === m :+: m :*: n
  plusSwap :: Nat a -&gt; Nat b -&gt; Nat c -&gt; a :+: (b :+: c) === b :+: (a :+: c)</code><p>等等，这里不再详细说明具体的推导，直接给出实现，有兴趣的同学可以试着自己写一遍</p><code lang="haskell"> equalPlus :: Nat a -&gt; Nat b -&gt; a === b -&gt; Nat c -&gt; a :+: c === b :+: c
 equalPlus a b eq Zero = symm (nPlusZero a) &lt;=&gt; eq &lt;=&gt; nPlusZero b
 equalPlus a b eq (Succ c) = nPlusSm a c &lt;=&gt; Fuck (equalPlus a b eq c) &lt;=&gt; symm (nPlusSm b c)
  
 equalPlus' :: Nat a -&gt; Nat b -&gt; a === b -&gt; Nat c -&gt; c :+: a === c :+: b
 equalPlus' a b eq c = plusCommutes c a &lt;=&gt; equalPlus a b eq c &lt;=&gt; plusCommutes b c
  
 plusSwap' :: Nat a -&gt; Nat b -&gt; Nat c -&gt; a :+: b :+: c === b :+: a :+: c
 plusSwap' a b = equalPlus (a + b) (b + a) (plusCommutes a b)
  
 plusSwap :: Nat a -&gt; Nat b -&gt; Nat c -&gt; a :+: (b :+: c) === b :+: (a :+: c)
 plusSwap a b c = plusCommute a b c &lt;=&gt; plusSwap' a b c &lt;=&gt; symm (plusCommute b a c)
  
 multCommuteS_1 :: Nat n -&gt; Nat m -&gt; n :+: (m :*: S n) === m :+: (n :+: (m :*: n))
 multCommuteS_1 n m = equalPlus' (m * Succ n) (m + m * n) (multCommuteS n m) n &lt;=&gt; plusSwap n m (m * n)
  
 multCommuteS :: Nat n -&gt; Nat m -&gt; m :*: S n === m :+: m :*: n
 multCommuteS _ Zero = Refl
 multCommuteS n (Succ m) = Fuck $ multCommuteS_1 n m
  
 multCommutes :: Nat n -&gt; Nat m -&gt; n :*: m === m :*: n
 multCommutes Zero m = symm $ multNZero m
 multCommutes (Succ n) m = symm
      (multCommuteS n m &lt;=&gt; equalPlus' (m * n) (n * m) (multCommutes m n) m)</code><h2><b>从自然数说开去</b></h2><p>我们在上面定义的“相等”中，只对自然数进行了规定，那么我们怎么表示任何一种东西相等的概念呢？我们需要对 <code class="inline">Equal</code> 进行修改。</p><code lang="haskell">data Equal a b where
      Refl :: Equal a a
      Derive :: Equal a b -&gt; Equal (p a) (p b)</code><p><code class="inline">Refl</code> 对应之前的 <code class="inline">EqZ</code> , <code class="inline">Derive</code> 对应 <code class="inline">EqS</code> 。从定义上我们就能知道相等关系的自反性。</p><p>重新定义之后，我们就再也不需要 <code class="inline">refl</code> 这个函数了，因为直接使用 <code class="inline">Refl</code> 就可以了。</p><p><code class="inline">symm</code> 的定义和原来类似，<code class="inline">trans</code> 则需要一点小小的修改：</p><code lang="haskell"> (&lt;=&gt;) :: a === b -&gt; b === c -&gt; a === c
 Refl        &lt;=&gt; Refl        = Refl
 Derive x &lt;=&gt; Refl        = Derive $ x      &lt;=&gt; Refl
 Refl        &lt;=&gt; Derive y = Derive $ Refl &lt;=&gt; y
 Derive x &lt;=&gt; Derive y = Derive $ x      &lt;=&gt; y</code><p>为了证明我们定义的 <code class="inline">Equal</code> 不仅仅对自然数有用，我们引入了类型层面的 <code class="inline">Bool</code> 来增加多样性。</p><p>首先是类型定义</p><code lang="haskell">data T
data F</code><p>然后是对应的数据类型</p><code lang="haskell">data Boolean :: * -&gt; * where
      Tr :: Boolean T
      Fa :: Boolean F</code><p>对于每一个需要参与证明的函数，我们需要在类型上实现一遍。</p><p>对于 <code class="inline">Boolean</code> 而言，先实现与或非 </p><code lang="haskell"> type family Inv (n :: *) :: *
 type instance Inv T = F
 type instance Inv F = T
  
 type family (||) (n :: *) (m :: *) :: *
 type instance T || T = T
 type instance F || T = T
 type instance T || F = T
 type instance F || F = F
  
 type family (&amp;&amp;) (n :: *) (m :: *) :: *
 type instance T &amp;&amp; T = T
 type instance T &amp;&amp; F = F
 type instance F &amp;&amp; T = F
 type instance F &amp;&amp; F = F</code><p>首先证明双重否定律</p><code lang="haskell">  doubleNeg :: Boolean b -&gt; Inv (Inv b) === b
  doubleNeg Tr = Refl
  doubleNeg Fa = Refl</code><p>要注意的是，这里并不能把两个分支合在一起写，因为它并不能直接把 <code class="inline">Inv (Inv b)</code> 推导到 <code class="inline">b</code></p><p>然后是德摩根律</p><code lang="haskell"> demorgan :: Boolean a -&gt; Boolean b -&gt; Inv (a &amp;&amp; b) === Inv a || Inv b
 demorgan Tr Tr = Refl
 demorgan Tr Fa = Refl
 demorgan Fa Tr = Refl
 demorgan Fa Fa = Refl</code><p>仍然要进行详细的分类讨论，理由同上</p><p><br></p><p>同样的，我们还能利用 Type Family 描述一些性质 ，比如自然数的奇偶性</p><code lang="haskell"> type family IsOdd (n :: *) :: *
 type instance IsOdd Z = F
 type instance IsOdd (S Z) = T
 type instance IsOdd (S (S n)) = IsOdd n
  
 type family IsEven (n :: *) :: *
 type instance IsEven Z = T
 type instance IsEven (S Z) = F
 type instance IsEven (S (S n)) = IsEven n</code><p>试着证明一下：</p><code lang="haskell"> oddPlus :: Nat n -&gt; Nat m -&gt; IsOdd (n :+: m) === IsOdd n ^ IsOdd m</code><p>因为 <code class="inline">IsEven</code> 和 <code class="inline">IsOdd</code> 的递归定义中都有两层的预设，所以我们在用归纳法证明时也需要考虑相应的 3 种情况</p><ol><li>当 <code class="inline">n</code> 为 <code class="inline">Z</code> 时</li></ol><code lang="haskell"> oddPlus Zero m :: IsOdd m === (F ^ IsOdd m)</code><p>只需要实现这两个函数（它们的实现都是平凡的）</p><code lang="haskell"> falseXor :: Boolean b -&gt; b === F ^ b
 isodd :: Nat a -&gt; Boolean (IsOdd a)</code><p>就有</p><code lang="haskell"> oddPlus Zero x = falseXor $ isodd x</code><ol><li>当 <code class="inline">n</code> 为 <code class="inline">S Z</code> 时</li></ol><p>对 <code class="inline">m</code> 进行归纳，当 <code class="inline">m</code> 为 <code class="inline">Z</code> 和 <code class="inline">S Z</code> 时，结果就是 <code class="inline">Refl</code> ，而当 <code class="inline">m</code> 为 <code class="inline">S (S m')</code> 时，我们需要的类型为</p><code lang="haskell"> oddPlus (Succ Zero) (Succ (Succ x)) :: IsOdd (S n3) === (T ^ IsOdd n3)</code><p>同样的，只需要把它拎出去，用归纳法再证一遍就好了</p><ol><li>当 <code class="inline">n</code> 为 <code class="inline">S (S n')</code> 时</li></ol><p>我们需要的类型为：</p><code lang="haskell"> oddPlus (Succ (Succ x)) y :: IsOdd (n2 :+: m) === (IsOdd n2 ^ IsOdd m)</code><p>利用归纳假设就能直接证明了</p><code lang="haskell"> oddPlus (Succ (Succ x)) y = oddPlus x y</code><h2><b>还有什么？</b></h2><p>注意到我们的很多证明，实际上都只是在类型层面的变化。比如下面这个</p><code lang="haskell"> oddEvenS :: Nat n -&gt; IsOdd n === IsEven (S n)
 oddEvenS Zero = Refl
 oddEvenS (Succ Zero) = Refl
 oddEvenS (Succ (Succ x)) = oddEvenS x</code><p>把递归展开的话就能发现它所有情况下的 “值” 都是 <code class="inline">Refl</code> ，但我们不能直接对所有的情况赋 <code class="inline">Refl</code> ，因为这个递归是对 <code class="inline">Refl</code> 上的类型进行修改的过程。类型推导不会对 Type Family 中的递归定义进行展开，所以我们还是得自己动手证明一遍。</p><h2>“性质” 的表示方法</h2><p>我们使用了 <code class="inline">Equal</code> 表示两个类型相等的概念，那么同样的，我们还可以用这种方法描述其它的一元/二元/多元关系，比如我们可以换一种方式表示奇偶性：</p><code lang="haskell">data Even :: * -&gt; * where
      ZeroEven :: Even Z
      Add2Even :: Even n -&gt; Even (S (S n))
  
data Odd :: * -&gt; * where
      OneOdd :: Odd (S Z)
      Add2Odd :: Odd n -&gt; Odd (S (S n))</code><p>或者描述两个自然数的大小关系：</p><code lang="haskell">data Greater :: * -&gt; * where
      GreZ :: Greater (S Z) Z
      GreS1 :: Greater x y -&gt; Greater (S x) y
      GreS2 :: Greater x y -&gt; Greater (S x) (S y)</code><h2><code class="inline">Derive</code> 的局限</h2><p>事实上，我们在 <code class="inline">Equal</code> 中定义的 <code class="inline">Derive</code> 并不是万能的，比如我们不能这样</p><code lang="haskell"> deriveOdd :: Nat n -&gt; n === m -&gt; IsOdd n === IsOdd m
 deriveOdd _ eq = Derive eq -- wrong !</code><p>报错信息为</p><code lang="haskell">Couldn't match type ‘IsOdd n’ with ‘p0 n’
      Expected type : IsOdd n === IsOdd m
      Actual   type    : p0 n === p0 m</code><p>它只能对字面量进行推导，比如</p><code lang="haskell"> deriveS :: n === m -&gt; S n === S m</code><h2><b>练习</b></h2><p>你现在应该可以解决下面几道题目</p><p><a href="https://www.codewars.com/kata/singletons">https://www.codewars.com/kata/singletons</a></p><p><a href="https://www.codewars.com/kata/odd-plus-even-equals-odd-prove-it">https://www.codewars.com/kata/odd-plus-even-equals-odd-prove-it</a></p><p><a href="https://www.codewars.com/kata/a-plus-b-equals-b-plus-a-prove-it">https://www.codewars.com/kata/a-plus-b-equals-b-plus-a-prove-it</a></p><p>最后一道题证明的是加法交换律，除了文中给出的方法，还有一种更精妙的写法，读者可以试着找一找</p><h2>更多练习</h2><p>证明乘法交换律</p><code lang="haskell"> multCommute :: Nat n -&gt; Nat m -&gt; Nat p -&gt; n :*: m :*: p === n :*: (m :*: p)</code><p>以及更多你能想到的东西</p><p></p><p></p>
<script async defer="defer" src="https://www.googletagmanager.com/gtag/js?id=UA-7909075-5"></script>
<script>
window.dataLayer = window.dataLayer || [];
function gtag(){ dataLayer.push(arguments); }
gtag('js', new Date());
gtag('config', 'UA-7909075-5');
</script>
<script>
var _hmt = _hmt || [];
</script>
<script async defer="defer" src="https://hm.baidu.com/hm.js?e3d40295e416616ddc21287da9646d31"></script>
</body>
</html>
