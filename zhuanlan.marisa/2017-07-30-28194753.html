<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<title>Erlang入门教程 - 11. List进阶</title>
</head>
<body>
<p><a href="https://zhuanlan.zhihu.com/p/28194753">原文</a></p>
<p>记住”|”运算符可以用于获取列表的头元素：</p><code lang="erlang">47&gt; [M1|T1] = [paris, london, rome].
[paris,london,rome]
48&gt; M1.
paris
49&gt; T1.
[london,rome]</code><p>“|”运算符也可以用于向列表头添加一个元素：</p><code lang="erlang">50&gt; L1 = [madrid | T1].
[madrid,london,rome]
51&gt; L1.
[madrid,london,rome]</code><p>现在举个例子说明它是如何工作的——反转列表：</p><code lang="erlang">-module(tut8).

-export([reverse/1]).

reverse(List) -&gt;
    reverse(List, []).

reverse([Head | Rest], Reversed_List) -&gt;
    reverse(Rest, [Head | Reversed_List]);
reverse([], Reversed_List) -&gt;
    Reversed_List.</code><p><br></p><code lang="erlang">52&gt; c(tut8).
{ok,tut8}
53&gt; tut8:reverse([1,2,3]).
[3,2,1]</code><p>考虑一下<b>Reversed_List</b>是如何构造出来的。它最开始是空列表”[]”，然后列表头元素被取走并添加到<b>Reversed_List</b>里，如下所示：</p><code lang="erlang">reverse([1|2,3], []) =&gt;
    reverse([2,3], [1|[]])

reverse([2|3], [1]) =&gt;
    reverse([3], [2|[1])

reverse([3|[]], [2,1]) =&gt;
    reverse([], [3|[2,1]])

reverse([], [3,2,1]) =&gt;
    [3,2,1]</code><p>lists模块包含了很多操纵列表的函数，比如反转列表。所以在写一个列表操纵的函数之前最好先检查一下是否它早已存在。</p><p>现在让我们回到城市温度的例子，但使用更结构化的方法。首先让我们把列表转化成只包含摄氏度：</p><code lang="erlang">-module(tut7).
-export([format_temps/1]).

format_temps(List_of_cities) -&gt;
    convert_list_to_c(List_of_cities).

convert_list_to_c([{Name, {f, F}} | Rest]) -&gt;
    Converted_City = {Name, {c, (F -32)* 5 / 9}},
    [Converted_City | convert_list_to_c(Rest)];

convert_list_to_c([City | Rest]) -&gt;
    [City | convert_list_to_c(Rest)];

convert_list_to_c([]) -&gt;
    [].</code><p>测试这个函数：</p><code lang="erlang">54&gt; c(tut7).
{ok, tut7}.
55&gt; tut7:format_temps([{moscow, {c, -10}}, {cape_town, {f, 70}},
{stockholm, {c, -4}}, {paris, {f, 28}}, {london, {f, 36}}]).
[{moscow,{c,-10}},
 {cape_town,{c,21.11111111111111}},
 {stockholm,{c,-4}},
 {paris,{c,-2.2222222222222223}},
 {london,{c,2.2222222222222223}}]</code><p>解释：</p><code lang="erlang">format_temps(List_of_cities) -&gt;
    convert_list_to_c(List_of_cities).</code><p>在这里<b>format_temps/1</b>调用<b>convert_list_to_c/1</b>。而<i>convert_list_to_c/1</i>取列表<b>List_of_cities</b>头元素，如果是摄氏度就什么也不做如果是华氏度还需要转化为摄氏度。 “|”运算符用于把转化后的元素添加到转换后的列表中，像这样：</p><code lang="erlang">[Converted_City | convert_list_to_c(Rest)];</code><p>或者：</p><code lang="erlang">[City | convert_list_to_c(Rest)];</code><p>这个操作将会持续到到达列表尾部，即列表为空。</p><code lang="erlang">convert_list_to_c([]) -&gt;
    [].</code><p>当列表完成转换后，添加一个输出的函数：</p><code lang="erlang">-module(tut7).
-export([format_temps/1]).

format_temps(List_of_cities) -&gt;
    Converted_List = convert_list_to_c(List_of_cities),
    print_temp(Converted_List).

convert_list_to_c([{Name, {f, F}} | Rest]) -&gt;
    Converted_City = {Name, {c, (F -32)* 5 / 9}},
    [Converted_City | convert_list_to_c(Rest)];

convert_list_to_c([City | Rest]) -&gt;
    [City | convert_list_to_c(Rest)];

convert_list_to_c([]) -&gt;
    [].

print_temp([{Name, {c, Temp}} | Rest]) -&gt;
    io:format("~-15w ~w c~n", [Name, Temp]),
    print_temp(Rest);
print_temp([]) -&gt;
    ok.</code><p><br></p><code lang="erlang">56&gt; c(tut7).
{ok,tut7}
57&gt; tut7:format_temps([{moscow, {c, -10}}, {cape_town, {f, 70}},
{stockholm, {c, -4}}, {paris, {f, 28}}, {london, {f, 36}}]).
moscow          -10 c
cape_town       21.11111111111111 c
stockholm       -4 c
paris           -2.2222222222222223 c
london          2.2222222222222223 c
ok</code><p>还必须添加一个函数寻找城市最高气温最低气温。下面的程序不是最佳解决方案，因为它遍历了列表四次。但最好是先力求清晰和正确，并且只有在需要时才改善程序效率。</p><code lang="erlang">-module(tut7).
-export([format_temps/1]).

format_temps(List_of_cities) -&gt;
    Converted_List = convert_list_to_c(List_of_cities),
    print_temp(Converted_List),
    {Max_city, Min_city} = find_max_and_min(Converted_List),
    print_max_and_min(Max_city, Min_city).

convert_list_to_c([{Name, {f, Temp}} | Rest]) -&gt;
    Converted_City = {Name, {c, (Temp -32)* 5 / 9}},
    [Converted_City | convert_list_to_c(Rest)];

convert_list_to_c([City | Rest]) -&gt;
    [City | convert_list_to_c(Rest)];

convert_list_to_c([]) -&gt;
    [].

print_temp([{Name, {c, Temp}} | Rest]) -&gt;
    io:format("~-15w ~w c~n", [Name, Temp]),
    print_temp(Rest);
print_temp([]) -&gt;
    ok.

find_max_and_min([City | Rest]) -&gt;
    find_max_and_min(Rest, City, City).

find_max_and_min([{Name, {c, Temp}} | Rest], 
         {Max_Name, {c, Max_Temp}}, 
         {Min_Name, {c, Min_Temp}}) -&gt;
    if 
        Temp &gt; Max_Temp -&gt;
            Max_City = {Name, {c, Temp}};           % Change
        true -&gt; 
            Max_City = {Max_Name, {c, Max_Temp}} % Unchanged
    end,
    if
         Temp &lt; Min_Temp -&gt;
            Min_City = {Name, {c, Temp}};           % Change
        true -&gt; 
            Min_City = {Min_Name, {c, Min_Temp}} % Unchanged
    end,
    find_max_and_min(Rest, Max_City, Min_City);

find_max_and_min([], Max_City, Min_City) -&gt;
    {Max_City, Min_City}.

print_max_and_min({Max_name, {c, Max_temp}}, {Min_name, {c, Min_temp}}) -&gt;
    io:format("Max temperature was ~w c in ~w~n", [Max_temp, Max_name]),
    io:format("Min temperature was ~w c in ~w~n", [Min_temp, Min_name]).</code><p><br></p><code lang="erlang">58&gt; c(tut7).
{ok, tut7}
59&gt; tut7:format_temps([{moscow, {c, -10}}, {cape_town, {f, 70}},
{stockholm, {c, -4}}, {paris, {f, 28}}, {london, {f, 36}}]).
moscow          -10 c
cape_town       21.11111111111111 c
stockholm       -4 c
paris           -2.2222222222222223 c
london          2.2222222222222223 c
Max temperature was 21.11111111111111 c in cape_town
Min temperature was -10 c in moscow
ok</code>
<script async defer="defer" src="https://www.googletagmanager.com/gtag/js?id=UA-7909075-5"></script>
<script>
window.dataLayer = window.dataLayer || [];
function gtag(){ dataLayer.push(arguments); }
gtag('js', new Date());
gtag('config', 'UA-7909075-5');
</script>
<script>
var _hmt = _hmt || [];
</script>
<script async defer="defer" src="https://hm.baidu.com/hm.js?e3d40295e416616ddc21287da9646d31"></script>
</body>
</html>
