<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<title>怎样写出没有 bug 的程序：程序证明的简单例子</title>
</head>
<body>
<p><a href="https://zhuanlan.zhihu.com/p/25568785">原文</a></p>
我写了个翻转二叉树<code lang="text">data Tree : Type -&gt; Type where
    Leaf : a -&gt; Tree a
    Branch : a -&gt; Tree a -&gt; Tree a -&gt; Tree a

flipTree : Tree a -&gt; Tree a
flipTree (Leaf x) = Leaf x
flipTree (Branch x l r) = Branch x (flipTree r) (flipTree l)
</code><p>那么我要怎么证明我的 flipTree 是对的呢？之前 <a href="https://www.zhihu.com/people/2d8f51b6523e01a8529606f466d98198" data-hash="2d8f51b6523e01a8529606f466d98198" class="member_mention" data-editable="true" data-title="@Canto Ostinato" data-hovercard="p$b$2d8f51b6523e01a8529606f466d98198">@Canto Ostinato</a> 在 <a href="https://zhuanlan.zhihu.com/p/21846817" data-editable="true" data-title="红尘里的Haskell（之三）-- 基于属性的单元测试 - 知乎专栏">红尘里的Haskell（之三）-- 基于属性的单元测试</a> 介绍了怎么根据命题去单元测试，然而，咱们现在用的是 idris，可以在直接去把这个属性给证明出来！</p><p>下面的过程是在 VSCode 加上其 idris 插件的基础上介绍的，其他编辑器也有类似的插件。</p><p>1. 写出命题</p><code lang="text">flipTreeSym : (t : Tree a) -&gt; t = flipTree (flipTree t)</code><p>2. 我们看下命题的形式，它是关于 Tree 的，那么可以采用归纳法进行：</p><code lang="text">flipTreeSym : (t : Tree a) -&gt; t = flipTree (flipTree t)
flipTreeSym (Leaf x) = ?g1
flipTreeSym (Branch x l r) = ?g2
</code><p>这儿的 ?g1 和 ?g2 表示还没写好的部分，两个坑。你可以把光标移动上去然后按 Ctrl-Shift-O（Type of），此时在输出面板上面会显示：</p><code lang="text">Idris: Type of g1
  a : Type
  x : a
--------------------------------------
g1 : Leaf x = Leaf x
</code><p>这个地方横线上方的内容表示我们已知的条件，下方是待填的坑应有的类型。</p><p>3. 对于 g1 的部分，它的值要求有类型 Leaf x = Leaf x —— 很容易就能看出填入 Refl 就可以了，于是我们按 Ctrl-Shift-S（Proof Search），此时编辑器中内容变为</p><code lang="text">flipTreeSym : (t : Tree a) -&gt; t = flipTree (flipTree t)
flipTreeSym (Leaf x) = Refl
flipTreeSym (Branch x l r) = ?g2
</code><p>很好坑少了一个……</p><p>4. 同样的方法，把光标移动到 ?g2 上面，此时它的类型是：</p><code lang="text">Idris: Type of g2
  a : Type
  x : a
  l : Tree a
  r : Tree a
--------------------------------------
g2 : Branch x l r = Branch x (flipTree (flipTree l)) (flipTree (flipTree r))
</code><p>由于我们是用归纳法，那么此时可以得知有以下两个命题成立：</p><code lang="text">flipTreeSym l
flipTreeSym r</code><p>那么使用 let 把它们填进去：</p><code lang="text">flipTreeSym (Branch x l r) = do
    let recL = flipTreeSym l
    let recR = flipTreeSym r
    ?g2</code>在 ?g2 上面按 Ctrl-Shift-O 显示的结果是：<code lang="text">Idris: Type of g2
  a : Type
  x : a
  l : Tree a
  r : Tree a
  recL : l = flipTree (flipTree l)
  recR : r = flipTree (flipTree r)
--------------------------------------
g2 : Branch x l r = Branch x (flipTree (flipTree l)) (flipTree (flipTree r))
</code><p>5. 我们已经胜利在望了，现在只要把 recL 和 recR 利用起来就可以填 g2 的坑了——而这个步骤可以使用 rewrite 完成，我们可以进行等量替换：</p><code lang="text">flipTreeSym (Branch x l r) = do
    let recL = flipTreeSym l
    let recR = flipTreeSym r
    rewrite recL in ?g2
</code><p>此时 g2「坑」的类型变为了：</p><code lang="text">Idris: Type of g2
  a : Type
  x : a
  l : Tree a
  r : Tree a
  recL : l = flipTree (flipTree l)
  recR : r = flipTree (flipTree r)
  _rewrite_rule : l = flipTree (flipTree l)
--------------------------------------
g2 : Branch x (flipTree (flipTree l)) r =
     Branch x (flipTree (flipTree l)) (flipTree (flipTree r))
</code><p>其中一处已经被替换了。继续：</p><code lang="text">flipTreeSym (Branch x l r) = do
    let recL = flipTreeSym l
    let recR = flipTreeSym r
    rewrite recL in 
        rewrite recR in ?g2
</code><p>此时：</p><code lang="text">Idris: Type of g2
  a : Type
  x : a
  l : Tree a
  r : Tree a
  recL : l = flipTree (flipTree l)
  recR : r = flipTree (flipTree r)
  _rewrite_rule : l = flipTree (flipTree l)
  _rewrite_rule1 : r = flipTree (flipTree r)
--------------------------------------
g2 : Branch x (flipTree (flipTree l)) (flipTree (flipTree r)) =
     Branch x (flipTree (flipTree l)) (flipTree (flipTree r))
</code><p>太好了，g2 终于变成了 A = A 的形式，可以填上 Refl 了：</p><code lang="text">flipTreeSym (Branch x l r) = do
    let recL = flipTreeSym l
    let recR = flipTreeSym r
    rewrite recL in 
        rewrite recR in Refl
</code><p>freeTypeSym 已经填好，它就是一个定理了，保证了我们反转二叉树的正确性。</p><br><p>ps. 下面这个就是关于 List 真的是个 Functor 的证明：</p><code lang="text">map_id : Functor f =&gt; f a -&gt; f a
map_id = map id

interface Functor f =&gt; VerifiedFunctor (f : Type -&gt; Type) where
    identity : (fa : f a) -&gt; map_id fa = fa 
    dist : (fa : f a) -&gt; (g : b -&gt; c) -&gt; (h : a -&gt; b) -&gt; 
        map (g . h) fa = (map g) . (map h) $ fa

VerifiedFunctor List where
    identity [] = Refl
    identity (x::xs) = let ih = (identity xs) in
                       rewrite ih in Refl

    dist [] _ _ = Refl
    dist (x::xs) g h  = let ih = (dist xs g h) in
                        rewrite ih in Refl
</code>
<script async defer="defer" src="https://www.googletagmanager.com/gtag/js?id=UA-7909075-5"></script>
<script>
window.dataLayer = window.dataLayer || [];
function gtag(){ dataLayer.push(arguments); }
gtag('js', new Date());
gtag('config', 'UA-7909075-5');
</script>
<script>
var _hmt = _hmt || [];
</script>
<script async defer="defer" src="https://hm.baidu.com/hm.js?e3d40295e416616ddc21287da9646d31"></script>
</body>
</html>
