<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<title>0x05: SICP 的魔法 - 实例:数字电路模拟</title>
</head>
<body>
<p><a href="https://zhuanlan.zhihu.com/p/29695168">原文</a></p>
<div class="title-image"><img src="https://pic3.zhimg.com/v2-3076ebc172bbb1203c811053d6b5872a_r.png" alt=""></div><blockquote>知乎的编辑器好烦啊，总是代码块语言总是掉。。。<br>我的github地址：<a href="http://link.zhihu.com/?target=https%3A//github.com/lfkdsk">lfkdsk </a><br>还创建了相关的仓库，可以clone下来看：<a href="http://link.zhihu.com/?target=https%3A//github.com/lfkdsk/SICP-Magical-Book">SICP 的魔法</a> <br>这个仓库还绑定了 gitbook 可以去下载。<br>我的原文地址：<a href="http://lfkdsk.github.io/2017/08/08/learn-sicp-5/">0x05: SICP 的魔法 - 实例:数字电路模拟</a></blockquote><p>在第二章我们学到的和 <b>数据抽象</b> 相关的知识指出，如果想构造数据抽象，我们需要两个部分：</p><ol><li>创建构造函数包含数据</li><li>创建选择函数 <b>分派</b> 数据</li></ol><p>但是在经过了解了第三章相关的 <i>模块化、状态、环境</i> 的知识之后，我们认识到了新的问题，在实际编程之中，需要依赖程序的状态进行编程，那么程序中就要根据我们的环境求值的方式进行计算，那我们在重新设计和模拟系统大的时候就要多考虑几点了：</p><ol><li>在系统中我们带状态的数据抽象</li><li>创建 <b>改变函数(mutator)</b> 去对数据进行重新修改</li></ol><h2><b>基于变动的模拟</b></h2><p>在构建复杂的系统之中，我们最先面对的部分就是关于 <i>同一性</i> 的知识，这部分知识我们已经在上一章的 <b>同一性发生了变化</b> 的那个小节中简单的讨论过一次，我们可以在这个再重新讨论一下 <b>共享和相等</b> 的知识。</p><h2><b>共享和相等</b></h2><p>我们通过引入赋值的方式为系统引入了状态，但是造成了引用透明的危机，我们没办法再通过相同的结构来判断对象西相同，两个相同结构的对象并不能确定两个对象是否相同：</p><code lang="scheme">(define x (list 'a 'b))
(define z1 (cons x x))</code><p>我们定义了这样的一个结构，<i>x</i> 是'a 和 'b 组成的序对，然后 <i>z1</i> 是由两个 <i>x</i> 的组成的序对，我们还要在另外定义一个 <i>z2</i>：</p><code lang="scheme">(define z2 (cons (list 'a 'b) (list 'a 'b)))</code><p>这两个结构的定义起来，看起来的结构是一样的：</p><img src="https://pic3.zhimg.com/v2-7cb8d6a314747e5d2827361f56b7d84d_r.jpg" data-rawwidth="675" data-rawheight="348"><p>这里我们能看到放置 'a 和 'b 两个序对中的节点都指向了同一个节点，这是因为在 Scheme 中符号引用是共享的，因而他们都指向了同一个节点。但是很明显虽然 <b>符号引用</b> 都指向了同一个节点，但是整体的结构是两个结构指向了两个结构，直接使用：</p><code lang="scheme">(eq? z1 z2)</code><p>的结果肯定是不相等的，这意味着我们每次使用 cons 方法结成一个新的序对都是生成了一个新的对象并返回的是可操作该对象的 <i>指针/引用</i> （虽然 Scheme 中没有显示的这种概念）。</p><p>这时候我们需要考虑另一个问题，就是在整个 <i>数据抽象</i> 都是不可变的情况下，我们对于数据结构是否使用了同样的引用是不可知的，但是当我们引入 <i>赋值</i> 我们共享的数据因为使用了同一个贡献的结构而变得复杂起来，修改其中的一个会导致另一个收到影响。</p><p>比如我们上文中提到的 <i>z1</i> 和 <i>z2</i> 就是这样结构，z1 的两个子结构全都是 x ，如果我们修改一个另一个肯定会受到影响，但是 z2 序对中的两个元素是两个不同的序对，并不会有这个的问题。</p><p>引入共享结构会带来一些好处：</p><ul><li>扩充数据抽象能够定义的数据范围</li><li>作为内部状态引入的结构，能够在程序的运行过程中不断地变化，这就可以承担我们需要让他们模拟更为复杂可变的需求</li></ul><p>但是上面的这个例子（关于 z1 和 z2 的）也表明了，我们在构建复杂结构的时候需要自己清楚那些结构是共享的，在对这些共享的进行修改的时候我们也要谨慎。</p><h2><b>以赋值改变结构</b></h2><blockquote>Tips 基于改变函数的新 API，我们的数据依赖 <b>序对</b> 进行构造，那我们的改造函数需要和构造函数类似的结构<br>set-car!  修改序对中的首项<br>set-cdr!  修改序对中的第二项</blockquote><p>在之前我们使用该通过过程来存储数据和模拟序对的表示，当时我们通过  作为 Tag 模拟了序对的分派，这里我们可以对这个程序略作修改：</p><code lang="scheme">(define (cons x y)
  (define (dispatch m)
    (cond ((eq? m 'car) x)
          ((eq? m 'cdr) y)
          (else (error "Undefined operation -- cons" m))))
  dispatch)

(define (car z) (z 'car))
(define (cdr z) (z 'cdr))</code><p>这个程序的变化其实不大，我们这是利用了 Scheme 中的符号引用的唯一性的这点，本质上和用 <b>0 1</b> 没有什么区别。我们再跟着这个改造的理念可以为这个程序进行扩充，添加我们之前提到的 set-car! 和 set-cdr! 函数：</p><code lang="scheme">(define (cons x y)
  (define (set-x! v) (set! x v))
  (define (set-y! v) (set! y v))
  (define (dispatch m)
    (cond ((eq? m 'car) x)
          ((eq? m 'cdr) y)
          ((eq? m 'set-car!) set-x!)
          ((eq? m 'set-cdr!) set-y!)
          (else (error "Undefined operation -- cons" m))))
  dispatch)

(define (car z) (z 'car))
(define (cdr z) (z 'cdr))
(define (set-car! z new-value) ((z 'set-car!) new-value) z)
(define (set-cdr! z new-value) ((z 'set-cdr!) new-value) z)</code><p>这里面我们通过增加了程序的内部状态，并且增加了两个内部方法来搞定了这个问题。</p><p>接下来部分是几个非常有趣的实例学习，包括对表格，队列和数字电路的模拟。这里面前面的表格、队列的知识都比较的简单，这里我们主要关注这个 <i>数字电路模拟</i> 的学习。</p><h2><b>数字电路模拟</b></h2><blockquote>本节中的代码可以到 Github 仓库 <b><a href="https://github.com/lfkdsk/SICP-Magical-Book">SICP-Magical-Book</a></b> 中获取</blockquote><img src="https://pic2.zhimg.com/v2-1f6fa8198befb0f82f9b3c6c6f78e306_r.jpg" data-rawwidth="372" data-rawheight="125"><p>这是一个比较有趣的实例，一个数字电路的模拟器本身是完成一种基于状态的系统模拟，而且本身从设计构建系统的角度中来看数字电路的模拟也是非常好玩，因为本身数字电路是由更多的细小的部件来构成的，本身带有一种自底向上进行程序设计的思路。另外，数字电路模拟还包含了另一种思想，一种基于事件驱动的模拟系统，我们用 <i>事件信号</i> 模拟实际的 <i>数字信号</i> 流过整个模拟的电路程序。</p><h2><b>事件驱动设计</b></h2><blockquote><b>事件驱动程序设计</b>（英语：<b>Event-driven programming</b>）是一种电脑<a href="http://www.wikiwand.com/zh-sg/%E7%A8%8B%E5%BC%8F%E8%A8%AD%E8%A8%88">程序设计</a><a href="http://www.wikiwand.com/zh-sg/%E6%A8%A1%E5%9E%8B">模型</a>。这种模型的程序运行流程是由用户的动作（如<a href="http://www.wikiwand.com/zh-sg/%E6%BB%91%E9%BC%A0">鼠标</a>的按键，键盘的按键动作）或者是由其他程序的<a href="http://www.wikiwand.com/zh-sg/%E8%A8%8A%E6%81%AF">消息</a>来决定的。相对于批处理程序设计（batch programming）而言，程序运行的流程是由<a href="http://www.wikiwand.com/zh-sg/%E7%A8%8B%E5%BC%8F%E8%A8%AD%E8%A8%88%E5%B8%AB">程序员</a>来决定。批量的程序设计在初级程序设计教学课程上是一种方式。然而，事件驱动程序设计这种设计模型是在交互程序（Interactive program）的情况下孕育而生的。<br>—— Wikipedia</blockquote><p>从这段维基百科中摘取出来的关于 <i>事件驱动程序</i> 的介绍，我们对事件驱动可能已经有了一定的理解。其实简单的去理解事件驱动设计，其实就是和传统的基于批处理的程序设计不同，因为那么去写过程式的程序，只能根据程序开发者的编写顺序对程序进行运行，但是事件驱动的程序设计，就像是设定了某些条件，当某些事件触发了这些条件的时候，程序会自动对事件进行处理。</p><p>这里我们的数字电路的模拟也是类似，一个数字电路由更细粒度的一系列的组件构成，在整个数字电路的操作中，各个部件的活动构成了这个整体的活动。</p><ul><li>电路系统的构建的过程中会遇到一些事件，这些事件是由某些部件遇到一些情况而引发的，并且引发可能是有时间顺序的（很正常，数字信号的传播肯定有顺序）。</li><li>还有一个是事件会引起状态的继续改变，状态的改变又会继续带来事件的产生和传播。</li></ul><h2><b>基础部件</b></h2><p>实际的数字电路线路由一些电子元件和它们之间的连线组成，一个电子元件可能有几个输入端口和一个输出端口，功能就是从多个接入信号经过一系列处理然后输出一个信号：</p><img src="https://pic2.zhimg.com/v2-a5f6255b8492d6a228bc76d80fc58bdf_r.jpg" data-rawwidth="261" data-rawheight="432"><p>就向我们上图的这些部件，可以提供一些 <i>与、或、非门</i> 这些基础部件，另外我们还需要各种部件之间的连线，这些连线会需要传递 <i>0、1</i> 这样的信号，除此之外各种功能块都会有不同的输出并且产生删除出信号都会有一些信号延迟。</p><p>在不断地通过基本构造块和连线进行数字链路的搭建的过程中，实际的数字电路程序已经实际成了一种结构设计语言，并且通过模块的不断构造我们语言的中使用的基本模块也在不断的在扩充，我们用这种语言能够构造结构任意复杂的数字电路：</p><ul><li><b>模块化</b> —— 基本构造块是基本元素</li><li><b>组合机制</b> —— 通过基本元素和连线进行组合</li><li><b>抽象机制</b> —— 我们可以进一步将复杂的组合过程抽象成过程</li></ul><h2><b>连线</b></h2><blockquote>Tips 半加器：<br>半加器的功能是将两个一位二进制数相加。它具有两个输入和两个输出（分别是和、进位）。输出的进位信号代表了输入两个数相加溢出的高一位数值。因此，这两2个一位二级制数的和等于2<i>C</i> + <i>S</i>。根据两个一位二进制数相加的结果，可以通过真值表、<a href="https://zh.wikipedia.org/wiki/%E5%8D%A1%E8%AF%BA%E5%9B%BE">卡诺图</a>得到右图所描绘的简易半加器设计。它使用了一个<a href="https://zh.wikipedia.org/wiki/%E5%BC%82%E6%88%96%E9%97%A8">异或门</a>来产生和<i>S</i>，并使用了一个<a href="https://zh.wikipedia.org/wiki/%E4%B8%8E%E9%97%A8">与门</a>来产生进位信号<i>C</i>。如果再添加一个<a href="https://zh.wikipedia.org/wiki/%E6%88%96%E9%97%A8">或门</a>来接收低位的进位输出信号，则两个半加器就构成了一个全加器。</blockquote><p>电路需要由连线来连接我们的基础功能块，由基础的功能块组成更大的功能块，我们这里先使用一个定义生成基础连线的方法 <i>(make-wire)</i> 生成一条连线，我们可以通过这个方法生成连线，然后把各个部件绑定在一起：</p><code lang="scheme">; 生成六根连线
(define a (make-wire))
(define b (make-wire))
(define c (make-wire))
(define d (make-wire))
(define e (make-wire))
(define s (make-wire))

; 使用与或非门构建半加器 参考之前的半加器图片
(or-gate a b d)
(and-gate a b c)
(inverter c e)
(and-gate d e s)</code><p>这里面的 A 和 B 是数据输入，S 和 C 是数据输出，其中S 是和，C 是进位，我们能看到抽象出来连接线、和与、或、非门之后我们对电路的的描述能被抽象到非常简洁形式，我们还可以把这几个过程结合成一个流程生成出一个 半加器的过程：</p><code lang="scheme">(define (half-adder a b s c)
  (let ((d (make-wire))
        (e (make-wire)))
    (or-gate a b d)
    (and-gate a b c)
    (inverter c e)
    (and-gate d e s)
    'ok))</code><p>两个半加器和一个或门能够成一个全加器：</p><img src="https://pic3.zhimg.com/v2-43bd392258485a9d80a28b9c9a3828f5_r.jpg" data-rawwidth="965" data-rawheight="392"><code lang="scheme">(define (full-adder a b c-in sum c-out)
  (let ((s (make-wire))
        (c1 (make-wire))
        (c2 (make-wire)))
    (half-adder b c-in s c1)
    (half-adder a s sum c2)
    (or-gate c1 c2 c-out)
    'ok))</code><p>这就是像是我们之前提到的，使用抽象机制将全加器、半加器这些复杂组件封装成构建过程。</p><p>我们在这里还可以给出连线上的发送信号的基本操作：</p><code lang="scheme">(get-signal &lt;wire&gt;) ; 返回当前线上的信号值
(set-signal! &lt;wire&gt; &lt;new-value&gt;) ; 重设线上的信号值
(add-action! &lt;wire&gt; &lt;procedure of no arguments&gt;) ; 在线上的信号改变的时候运行过程
(after-delay &lt;time&gt; &lt;procedure&gt; ; 设定一个时间延迟和对应的过程，时间过后触发过程</code><p>这就能试着写出 make-wire 的定义，肯定是包含上面三个函数的 dispatch 的 <b>分发器</b>，还要有针对多个属性的 <b>修改器</b>。</p><code lang="scheme">; 创建一条连线
(define (make-wire)
  (let ((signal-value 0)
        (action-procedures '()))
    ; set new value and call procedures
    (define (set-my-signal! new-value)
      (if (not (= signal-value new-value))
          (begin (set! signal-value new-value)
                 (call-each action-procedures))
          'done))
    
    (define (accept-action-procedure! proc)
      (set! action-procedures
            (cons proc action-procedures))
      (proc))
    
    (define (dispatch m)
      (cond ((eq? m 'get-signal) signal-value)
            ((eq? m 'set-signal!) set-my-signal!)
            ((eq? m 'add-action!) accept-action-procedure!)
            (else (error "Unknown operation -- WIRE" m))))
    
    dispatch))</code><p>我们注意到刚才提到的东西我们几乎都有，并且通过过程 accept-action-proceduer 能知道每条线包含的过程不只有一个，是一个列表的形式，而且 set-my-signal! 中我们看到，每当有一个线上的数据修改，我们会重新调用所有监听这条线上的过程。</p><p>其中还有一些其他的方法：</p><code lang="scheme">; 遍历调用
(define (call-each procedures)
  (if (null? procedures)
      'done
      (begin
        ((car procedures))
        (call-each (cdr procedures)))))

(define (get-signal wire)
    (wire 'get-signal))

(define (set-signal! wire new-value)
    ((wire 'set-signal!) new-value))

(define (add-action! wire action-procedure)
    ((wire 'add-action!) action-procedure))</code><p>执行一些 dispatch 方法而已。</p><blockquote>Tips 书中的一到课后题也提到了，例如：<br></blockquote><code lang="scheme">(define (accept-action-proceduer! proc) 
   (set! action-procedures  (cons proc action-procedures))  (proc))</code><blockquote>这个方法，为什么要把 proc 在最后返回，其实这里就是为了让 proc 在那个返回的位置展开，在后面会调用 after-delay ，after-delay 会调用 add-to-agenda! ，将指定的动作添加到模拟器的待处理列表中，当调用 (propagate) 时，这个指定的动作会被执行。</blockquote><h2><b>逻辑电路门</b></h2><code lang="scheme">; 算数逻辑非
(define (logical-not s)
  (cond ((= s 0) 1)
        ((= s 1) 0)
        (else (error " Invalid signal " s))))

; 算数逻辑与
(define (logical-and a b)
  (if (and (= a 1) (b = 1))
      1
      0))
      

; 逻辑或
(define (logical-or a b)
  (if (or (= a 1) (= b 1))
      1
      0))

; 与门 给两个线路都绑上一个监控器
; 当某个值变化的时候 会重新计算 new-value 设置到输出端口
(define (and-gate a1 a2 output)
  (define (and-action-procedure)
    (let ((new-value
           (logical-and (get-signal a1) (get-signal a2))))
      (after-delay and-gate-delay
                   (lambda ()
                     (set-signal! output new-value)))))
  (add-action! a1 and-action-procedure)
  (add-action! a2 and-action-procedure)
  'ok)

; 或门 
(define (or-gate input-1 input-2 output)
  (define (or-action-procedure)
    (let ((new-value
           (logical-or (get-signal input-1) (get-signal input-2))))
      (after-delay or-gate-delay
                   (lambda ()
                     (set-sign! output new-new-value)))))
    (add-action! input-1 or-action-procedure)
    (add-action! input-2 or-action-procedure)
    'ok)

; 反门
(define (inverter-gate input output)
  (define (invert-input)
    (let ((new-value (logical-not (get-signal input))))
      (after-delay inverter-delay
                   (lambda () (set-signal! output new-value)))))
  (add-action! input invert-input)
  'ok)         </code><p>如果我们理清了前面的给连线添加信号的内容，那逻辑电路门部分的代码就很简单了，甚至有点冗余。与或非门的唯一区别在于如何生成 new-value ，每个过程都包含一个内部过程，负责生成新的值，把这个过程绑定到输入线上，这里面我们的输入值一旦改变，就会重新进行计算，然后重设当前线路中的数值。</p><h2><b>待处理表</b></h2><p>待处理表的问题主要是我们之前在程序中的使用 after-delay 操作控制程序在某个时间段之后执行一个过程，能控制一定的操作的时序性，这个功能本质上就是在维护一张 <i>待处理表</i> ，使用队列的结构控制我们执行过程，先给出各个 API 的内容：</p><code lang="scheme">; 返回新建的空待处理表
(make-agenda) 
; 判断待处理表是否为空
(empty-agenda? &lt;agenda&gt;) 
; 返回待处理表中第一个项
(first-agenda-item &lt;agenda&gt;) 
; 删除待处理表里的第一项
(remove-first-agenda-item! &lt;agenda&gt;) 
; 向待处理表中加入一 项，其意义是要求在给定时间运行的过程
(add-to-agenda! &lt;time&gt; &lt;action&gt; &lt;agenda&gt;) 
; 返回当前时间
(current-time &lt;agenda&gt;) </code><p>这些 API 的实现都很容易，我们可以逐个的来讲解一下：</p><code lang="scheme">; segment

(define (make-time-segment time queue)
  (cons time queue))

(define (segment-time s) (car s))

(define (segment-queue s) (cdr s))

; agenda

(define (make-agenda) (list 0))

; 当前时间
(define (current-time agenda) (car agenda))

(define (set-current-time! agenda time)
  (set-car! agenda time))

(define (segments agenda) (cdr agenda))

(define (set-segments! agenda segments)
    (set-cdr! agenda segments))

(define (first-segment agenda) (car (segments agenda)))

(define (rest-segments agenda) (cdr (segments agenda)))

(define (empty-agenda? agenda)
  (null? (segments agenda)))</code><p>关于segment 这个几个过程式构建 <i>时间</i> 和 <i>队列</i> 的绑定关系，待处理表中的每一项是由时间和时间队列的一个绑定，agenda 本身是一个关于时间的一维表格，在表头存储当前的时间，表头之后存储的是 segments 的段落，还提供了几个 API 做存取。</p><p>还有就是如何插入处理表，通过时间片加队列的形式进行查找管理：</p><code lang="scheme">; 向待处理表中添加一项
(define (add-to-agenda! time action agenda)
  ; 检查是否为空 或者当前时间小于 segment 的时间
    (define (belongs-before? segments)
        (or (null? segments)
            (&lt; time (segment-time (car segments)))))
  ; 创建一个新的时间片 生成新的队列 插入过程
    (define (make-new-time-segment time action)
        (let ((q (make-queue)))
              (insert-queue! q action)
              (make-time-segment time q)))
  ; 添加时间片
    (define (add-to-segments! segments)
        ; 找到合适的时间插进去
        (if (= (segment-time (car segments)) time)
            (insert-queue! (segment-queue (car segments))
                           action)
            (let ((rest (cdr segments)))
                (if (belongs-before? rest)
                    (set-cdr!
                        segments
                        (cons (make-new-time-segment time action)
                              (cdr segments)))
                        (add-to-segments! rest)))))
    ; 遍历时间片 找到合适的时间插进去
        (let ((segments (segments agenda)))
            (if (belongs-before? segments)
                (set-segments!
                    agenda
                    (cons (make-new-time-segment time action)
                          segments))
                    (add-to-segments! segments))))</code><p>上面的注释应该是介绍的已经很详细的了，还提供了一些方法去对处理表进行一些管理，无非就是如何消耗处理事件之类的：</p><code lang="scheme">(define (remove-first-agenda-item! agenda)
    (let ((q (segment-queue (first-segment agenda))))
        (delete-queue! q)
        (if (empty-queue? q)
            (set-segments! agenda (rest-segments agenda)))))

(define (first-agenda-item agenda)
    (if (empty-agenda? agenda)
        (error "Agenda is empty -- FIRST-AGENDA-ITEM")
        (let ((first-seg (first-segment agenda)))
            (set-current-time! agenda (segment-time first-seg))
            (front-queue (segment-queue first-seg)))))</code><p>最红我们终于要说到 after-delay 的实现定义了：</p><code lang="scheme">(define (after-delay delay action)
    (add-to-agenda! (+ delay (current-time the-agenda))
                    action
                    the-agenda))</code><p>可见我们为让某个事件延迟执行的本质就是给某个处理表添加一条处理事件，通过获取当前时间增加一定的延迟的把事件放进对应的序列。</p><p>我们还需要一个过程去模拟驱动整个表的执行过程，propagate 对整个待处理表进行操作，不断地处理其中的项目进行处理：</p><code lang="scheme">(define (propagate)
    (if (empty-agenda? the-agenda)
        'done
        (let ((first-item (first-agenda-item the-agenda)))
            (first-item)
            (remove-first-agenda-item! the-agenda)
            (propagate))))</code><h2><b>简单的模拟实例</b></h2><p>Github 仓库中提供了相应的代码，只需要 load 其中的 simular.rkt 就可以进行编写模拟实例的操作。这里面我们添加一个监控器过程放到线路上面，用来监听数字电路模拟中的数字电路模拟的运行情况：</p><code lang="scheme">(define (probe name wire)
    (add-action! wire
                 (lambda ()        
                    (newline)
                    (display name)
                    (display " ")
                    (display (current-time the-agenda))
                    (display "  New-value = ")
                    (display (get-signal wire)))))</code><p>这段代码很简单，就是给某根线注册名字和一个相应的回调，当数据发生变化的时候打印出目前的消息。</p><p>接下来的模拟是为了模拟一个半加器的运行情况：</p><ol><li>初始化待处理表，定义延迟相关的常量：</li></ol><code lang="scheme">(define the-agenda (make-agenda)) (define inverter-delay 2)
(define and-gate-delay 3)
(define or-gate-delay 5)</code><ol><li>定义四条线路，安装监控器：</li></ol><code lang="scheme">(define input-1 (make-wire)) 
(define input-2 (make-wire)) 
(define sum (make-wire)) 
(define carry (make-wire)) 
(probe 'sum sum)
(probe 'carry carry)</code><p>运行一次的效果是：</p><img src="https://pic4.zhimg.com/v2-18b78ab4ae784579e9fc3d37b202c983_r.jpg" data-rawwidth="439" data-rawheight="245"><ol><li>接着我们把线路连接到半加器上，测试半加器的输入：</li></ol><code lang="scheme">(half-adder input-1 input-2 sum carry)</code><img src="https://pic4.zhimg.com/v2-ebe449b11aa9dae246ecd0a4b254f58c_r.jpg" data-rawwidth="379" data-rawheight="67"><p>测试输入，即将 input-1 设置为 1 ，然后将 input-2 设置为 1 ：</p><code lang="scheme">(set-signal! input-1 1)
(propagate)</code><img src="https://pic4.zhimg.com/v2-8998257ce80b89eb3b92000bfd3d1278_r.jpg" data-rawwidth="333" data-rawheight="116"><p>这时候时间片为 8 ，设定值为 1。</p><code lang="scheme">(set-signal! input-2 2)
(propagate)</code><img src="https://pic4.zhimg.com/v2-931c433974bb355f9af3b1d37b3bbf1a_r.jpg" data-rawwidth="295" data-rawheight="123"><p>时间片为 11 时，carry 被设置为 1，之后 sum 被设置为 0，这表明我们在这的模拟是有效的。</p><h2><b>小结</b></h2><p>这篇文章的内容并没有特别多的复杂的理论知识内容没有特别多，主要是进行介绍了书中出现的第一个比较复杂的程序实例，但是这个稍有一些复杂的程序实例中包含了从本书开始就介绍的很多和抽象、模拟相关的知识，也算是用某种实例程序去又温习了之前的知识。</p><p>除了对之前的知识的温习之外，这个关于 数字电路模拟程序 的程序设计更多的是关注变动程序的程序设计，程序中和第二章不同的是整个程序都是基于状态变动建立起来的。基础模块和连线相互绑定，在数据出现变动的时候作出响应，使用待处理表对过程调用进行处理，使用时间排序建立时序性的程序设计，整个程序无处不体现着基于变动的程序模拟的思想。</p><p>上一篇专栏文章中说文章坑了很久，没想到这篇文章坑了更长时间，看了一下居然四个月过去了，这四个月基本上都是在杭州某厂实习，写文章的时间不是很多，而且这篇文章其实其中的很多文字都是很早就写好的，但是迟迟没有写完也是因为在思考怎么介绍这个实例的程序设计比较好，因为很久以前再读书的时候，感觉书里的步骤虽然由浅入深但是失于冗长，到读者能整的跑整个程序之前，对程序的理解仅限于能读懂书上的代码，所以这部最后还是打算从各个部分的代码分析入手，事先提供好能够跑通运行的程序，能让读者一步步的通过运行代码和阅读文章去理解整个程序。</p>
<script async defer="defer" src="https://www.googletagmanager.com/gtag/js?id=UA-7909075-5"></script>
<script>
window.dataLayer = window.dataLayer || [];
function gtag(){ dataLayer.push(arguments); }
gtag('js', new Date());
gtag('config', 'UA-7909075-5');
</script>
<script>
var _hmt = _hmt || [];
</script>
<script async defer="defer" src="https://hm.baidu.com/hm.js?e3d40295e416616ddc21287da9646d31"></script>
</body>
</html>
