<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<title>使用 Haskell 编写灵活的 Parser （下）</title>
</head>
<body>
<p><a href="https://zhuanlan.zhihu.com/p/28162006">原文</a></p>
<div class="title-image"><img src="https://pic2.zhimg.com/v2-5aa5b971b51772baaff0fc0ba7144d3d_r.jpg" alt=""></div><p>原文在我博客：<a href="http://ice1000.org/2017/07/27/HaskellParsers2/">使用 Haskell 编写灵活的 Parser （下）</a></p><p>上一篇也是：<a href="http://ice1000.org/2017/07/26/HaskellParsers/">使用 Haskell 编写灵活的 Parser （上）</a></p><p>本篇主要讲 Parser Combinator 的组合，比如二元运算符的解析以及优先级结合性的处理。</p><h2>some 与 many</h2><p>上篇还剩下一个内容，就是 some 和 many 。这两个函数是 Control.Applicative 内置的，可以直接拿来用。</p><p>比如我们可以有一个解析空字符串的 Parser ：</p><code lang="haskell">-- | one or more
spacesP :: Parser String
spacesP = some $ oneOf " \n\r\t"

-- | zero or more
spaces0P :: Parser String
spaces0P = many $ oneOf " \n\r\t"</code><p>非常简单，对吧。它们看起来也很直观。</p><h2>其他小工具</h2><p>另外，为下文做准备，我们先加上这几个 Parser 。他们的意义看名字就知道，不再介绍。</p><code lang="haskell">import Data.Char

stringP :: String -&gt; Parser String
stringP [      ] = return []
stringP (c : cs) = do
  charP c
  stringP cs
  return $ c : cs
--

reservedP :: String -&gt; Parser String
reservedP = tokenP . stringP

natP :: Parser Int
natP = read &lt;$&gt; some digitP

digitP :: Parser Char
digitP = satisfy isDigit

tokenP :: Parser a -&gt; Parser a
tokenP p = do
  a &lt;- p
  spaces0P
  return a
--</code><h2>chainl1 与 chainr1</h2><p>首先，我们考虑这样两种 Parser 。</p><ol><li>解析一个运算单元</li><li>解析一个运算符</li></ol><p>看起来是个半群，不过这并不是重点。我们以整数和加减法作为具体的例子，然后可以考虑写出对应的 Parser 。</p><code lang="haskell">-- the parsers
numberP :: Parser String
numberP = do
  s &lt;- stringP "-" &lt;|&gt; return []
  cs &lt;- some digitP
  spaces0P
  return $ s ++ cs
--

plusSymbolP :: Parser String
plusSymbolP = do
  stringP "+"
  spaces0P
  return "+"</code><h2>AST</h2><p>沿用一下上次类似的 AST 表述。</p><code lang="haskell">{-# LANGUAGE DeriveFunctor #-}

data OpTree a b = Term b
                | Op (OpTree a b) a (OpTree a b)
  deriving (Show, Eq, Functor) </code><p>这个也很清晰，比如 1+1 的 AST 就是：</p><code lang="text">Op (Term "1") "+" (Term "1") </code><h2>问题</h2><p>但是我们这里有一个巨大的问题。当我们在描述一颗复杂语法树的时候，比如 1+1+1+1 这种好几个二元运算符组合起来，我们需要</p><code lang="haskell">p = do
  n1 &lt;- numberP
  plusSymbolP
  n2 &lt;- numberP
  plusSymbolP
  n3 &lt;- numberP
  plusSymbolP
  n4 &lt;- numberP
  return $ Op (Op (Op (Term "1") "+" (Term "1")) "+" (Term "1")) "+" (Term "1")</code><p>这种复杂而傻逼的 Parser 。</p><h2>抽象</h2><p>为了解决这个问题，我们首先重构一下 plusSymbolP ，把所有的二元运算抽象成一个新的 Parser 。</p><code lang="haskell">-- | binary operators
binOp :: String -&gt; (b -&gt; b -&gt; b) -&gt; Parser (b -&gt; b -&gt; b)
binOp sym func = do
  stringP sym
  return func </code><p>当然你也可以不使用 do notation ，毕竟这个很简单。</p><code lang="haskell">-- | binary operators
binOp :: String -&gt; (b -&gt; b -&gt; b) -&gt; Parser (b -&gt; b -&gt; b)
binOp s = (stringP s &gt;&gt;) . return </code><p>上面是 point-free 的版本。</p><p>这个函数的意思是：</p><ol><li>拿到运算符和 <b>通过结合运算符左右的两个语法树节点生成新的语法树节点的函数</b>（此处是 \l r -&gt; Op l "+" r）</li><li>解析运算符</li><li>如果解析成功，那么返回这个函数</li></ol><h2>重写</h2><p>于是我们的加法符号解析器，以及剩下的四则运算就可以很简单地重写：</p><code lang="haskell">addOp = binOp "+" $ \x y -&gt; Op x "+" y
subOp = binOp "-" $ \x y -&gt; Op x "-" y
mulOp = binOp "*" $ \x y -&gt; Op x "*" y
divOp = binOp "/" $ \x y -&gt; Op x "/" y </code><p>要解析一个 1 op 1 （左边 op 是任意一个二元运算符）也就可以这样：</p><code lang="haskell">operatorP :: Parser (a -&gt; a -&gt; a) -&gt; Parser a
operatorP op = do l &lt;- numberP o &lt;- op r &lt;- numberP return $ o l r </code><p>只需要传入 op 就能构造一个二元运算符。</p><h2>左结合</h2><p>同理。我们可以进行“不断地尝试往右边解析运算符，直到解析不了，返回表达式链”。</p><code lang="haskell">chainl1 :: Parser a -&gt; Parser (a -&gt; a -&gt; a) -&gt; Parser a
chainl1 p op = do
  a &lt;- p
  rest a
  where
    rest a = (do
      f &lt;- op
      b &lt;- p
      rest $ f a b)
      &lt;|&gt; return a
--</code><p>上面这个实现基本上是你能想到的最简单最朴素好像也是唯一的实现。</p><p>比如链状的 + 操作：</p><code lang="text">adds = chainl1 numberP addOp </code><p>它可以这样工作：</p><code lang="haskell">Main&gt; parseCode adds "1+1+1+1"
Op (Op (Op (Term "1") "+" (Term "1")) "+" (Term "1")) "+" (Term "1")
Main&gt; parseCode adds "1+1+1"
Op (Op (Term "1") "+" (Term "1")) "+" (Term "1")
Main&gt; parseCode adds "1+1"
Op (Term "1") "+" (Term "1")</code><p>我们也可以同时加入减法：</p><code lang="text">addAndSubs = chainl1 numberP $ addOp &lt;|&gt; subOp </code><p>这个读者可以自行尝试。</p><h2>右结合</h2><p>这里也有一个最简最智障实现：</p><code lang="haskell">chainr1 :: Parser a -&gt; Parser (a -&gt; a -&gt; a) -&gt; Parser a
chainr1 p op = scan
  where
    scan = do
      a &lt;- p
      rest a
    rest a = (do
      f &lt;- op
      b &lt;- scan
      rest $ f a b)
      &lt;|&gt; return a
--</code><p>举例比如乘方：</p><code lang="text">sq = chainr1 numberP $ binOp "^" $ \x y -&gt; Op x "^" y </code><h2>优先级</h2><p>对应地，要加入优先级，也很简单，只要把优先级高的运算（比如乘法）的结果作为优先级低的运算（比如加法）的元素就好了：</p><code lang="haskell">addP = chainl1 mulP $ binOp "+" $ \x y -&gt; Op x "+" y
mulP = chainl1 oneP $ binOp "*" $ \x y -&gt; Op x "*" y
oneP = numberP </code><p>上面的 Parser 可以正确解析 1+1*1 和 1*1+1 这两个执行顺序不同的表达式。</p><h2>括号</h2><p>这个也很简单，我就不说了，可以当作作业。</p><p>提示一下，它的作用是把优先级低的运算提高，因此它里面是需要解析一个优先级低的运算，然后这个东西本身可以变成一个最基层的运算单元。其实大家看看括号表达式在 bnf 里面的表示就一目了然了。</p><h2>杂话</h2><h2>关于构建</h2><p>根据上一篇文章在水乎的反馈，在 2017 年， Haskell 工程应该使用 Stack 而不是 Cabal 构建，而且也应该使用 Megaparsec 这个 Parser Combinator 库。不过我都不会用就是了。</p><h2>关于 Applicative</h2><p>而且上面的很多事情都可以用 Applicative + Alternative 做，不需要 Monad 。比如我们有使用 Monad 的 Parser ：</p><code lang="haskell">parseUsingMonad = do
  x &lt;- itemP
  o &lt;- operatorP
  y &lt;- itemP
  return $ Op x o y </code><p>其实它等价于这个使用 Applicative 的 Parser ：</p><code lang="haskell">parseUsingApplicative = Op &lt;$&gt; itemP &lt;*&gt; operatorP &lt;*&gt; itemP </code><p>上面的代码远远简洁于 Monad 的版本。为了让 GHC 在处理 do notation 的时候尽可能转化为 Applicative 操作而不是 Monad 操作，可以使用</p><code lang="text">{-# LANGUAGE ApplicativeDo #-} </code><p>这个扩展。</p><h2>CodeWars Challenges</h2><p>这里列出一些我认为很有趣的 CodeWars 的 Parser 题。我建立了<a href="https://preview.codewars.com/collections/parser-combinators">一个 collection 来收藏相关题目</a>， 感兴趣的同学可以去做一下。</p><p>下面分别介绍一下各个题目。</p><h2><a href="https://preview.codewars.com/kata/operator-parser">Operator Parser</a></h2><p>此题非常新颖，给你一个结合性的 List ，里面放的是以优先级排序的二元中缀符号的 Parser ， 然后你需要把它们组合起来成为一个大的表达式 Parser 。这题一看就知道是 foldr/foldl ，哈哈。</p><p>前文所使用的 OpTree 就是这道题里面的。</p><h2><a href="https://preview.codewars.com/kata/writing-applicative-parsers-from-scratch">Writing Applicative Parsers From Scratch</a></h2><p>此题禁止你使用 Control.Applicative, Prelude 的 fmap 和 Data.Functor（我觉得它很 SB 的一点在于它没有禁止你使用 &lt;$&gt;）， 然后让你从 0 开始写一个 Applicative 的 Parser 。</p><h2><a href="https://preview.codewars.com/kata/regular-expression-parser">Regular Expression Parser</a></h2><p>这个题目是让你解析正则表达式的，需要特别注意一些结合性的问题（这道题我的解法比较 dirty ，特判了两组数据）。做这题的时候我不知道有 chainr1 ，结果写出来的代码很伤。</p><h2><a href="https://preview.codewars.com/kata/tiny-three-pass-compiler">Tiny Three Pass Compiler</a></h2><p>老经典的题，支持很多语言，写起来贼爽。</p>
<script async defer="defer" src="https://www.googletagmanager.com/gtag/js?id=UA-7909075-5"></script>
<script>
window.dataLayer = window.dataLayer || [];
function gtag(){ dataLayer.push(arguments); }
gtag('js', new Date());
gtag('config', 'UA-7909075-5');
</script>
<script>
var _hmt = _hmt || [];
</script>
<script async defer="defer" src="https://hm.baidu.com/hm.js?e3d40295e416616ddc21287da9646d31"></script>
</body>
</html>
