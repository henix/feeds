<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<title>如何玩弄Typed Racket</title>
</head>
<body>
<p><a href="https://zhuanlan.zhihu.com/p/35962271">原文</a></p>
<p>要玩弄Typed Racket，首先，你不可避免地要require一大堆东西：</p><code lang="racket">#lang racket
(require racket/require)
(require typed-racket/utils/utils
         typed-racket/standard-inits
         (private parse-type)
         (rep values-rep)
         (types abbrev subtype tc-result numeric-tower)
         (typecheck typechecker tc-funapp)
         (for-template (base-env base-types))
         (rename-in (base-env base-types-extra) [Un t:Un] [-&gt; t:-&gt;] [-&gt;* t:-&gt;*])
         (env global-env)
         )

(do-standard-inits)</code><p>　　然后我们看看怎么parse一个类型：</p><code lang="racket">&gt; (parse-type #'String)
String

&gt; (parse-type #'(Pairof String String))
(Pairof String String)

&gt; (parse-type #'(All (a) (a → String)))
(All (a) (-&gt; a String))</code><p>　　基本上直接照搬TR的语法即可。除了<code class="inline">-&gt;</code>和<code class="inline">-&gt;*</code>，因为上面<code class="inline">base-types-extra</code>和<code class="inline">abbrev</code>的冲突，要改用</p><code lang="racket">&gt; (parse-type #'(String t:-&gt; String))
(-&gt; String String)

&gt; (parse-type #'(t:-&gt;* (String) String))
(-&gt; String String)</code><p>　　好了，学会parse一个类型之后，再看看怎么手动构造一些简单的类型。</p><code lang="racket">&gt; Univ
Any

&gt; -Bottom
Nothing

&gt; (Un -True -False)
(U #f #t)

&gt; (make-Pair -String -Boolean)
(Pairof String (U #f #t))

&gt; (make-Intersection (list -Boolean -True))
(∩ #t (U #f #t))

&gt; (-poly (a) (-&gt; a a))
(All (a) (-&gt; a a))

&gt; (make-Mu 'x (Un -Null (make-Pair -String (make-F 'x))))
(Listof String)

&gt; (-lst Univ)
(Listof Any)</code><p>　　现在来构造一些复杂的类型，例如带latent proposition、latent object的函数类型：</p><code lang="racket">&gt; (-&gt; Univ
      (make-Values
       (list
        (make-Result -Boolean
                     (-PS (-is-type (make-Path null (cons 0 0)) -String)
                          (-not-type (make-Path null (cons 0 0)) -String))
                     -empty-obj))))
(-&gt; Any (U #f #t) : String)

&gt; (make-pred-ty -String)
(-&gt; Any (U #f #t) : String)</code><p>　　此外，latent propositions常用的缩写有<code class="inline">true-propset</code>和<code class="inline">-false-propset</code>等；latent object常用的缩写有<code class="inline">-arg-path</code>等。</p><p>　　说完构造类型了，现在开始进行类型推导：</p><code lang="racket">&gt; (tc-expr #''1)
(tc-results
 (list (tc-result 1 (Top | Bot) -))
 #f)
 
&gt; (tc-expr #''"123")
(tc-results
 (list (tc-result String (Top | Bot) -))
 #f)</code><p>　　要注意的是这个推导是针对<i>Fully Expanded Programs</i>，Typed Racket把程序展开到<i>Fully Expanded Programs</i>的同时做了的事包括有：</p><ul><li>把类型声明记录到一个环境里。</li></ul><code lang="racket">&gt; (register-type #'f1 Univ)  
&gt; (tc-expr #'f1)
 (tc-results  (list (tc-result Any (Top | Top) -))  #f)</code><ul><li>插入一些<i>syntax properties</i>，以便在<i>Fully Expanded Programs</i>里留下足够的辅助信息，主要需要注意的有</li></ul><ol><li><code class="inline">typechecker:plambda</code>：用于多态。</li><li><code class="inline">type-label</code>：用于在引入binding的时候声明类型。</li><li><code class="inline">type-ascription</code>：用于ann。</li></ol><code lang="racket">&gt; (tc-expr (syntax-property #''1 'type-ascription -Integer))
(tc-results
 (list
  (tc-result
   (U
    0
    1
    Byte-Larger-Than-One
    Negative-Fixnum
    Negative-Integer-Not-Fixnum
    Positive-Fixnum-Not-Index
    Positive-Index-Not-Byte
    Positive-Integer-Not-Fixnum)
   (Top | Bot)
   -))
 #f)
 
 &gt; (tc-expr (syntax-property
            #`(#%plain-lambda
               (#,(syntax-property
                   #'x
                   'type-label
                   #'a))
               x)
            'typechecker:plambda
            #'(a)))
(tc-results
 (list
  (tc-result
   (All (a) (-&gt; a a))
   (Top | Bot)
   -))
 #f)</code><p>　　这样基本上就差不多了，然后说一下<i>subtyping</i>：</p><code lang="racket">&gt; (subtype -One -Integer)
#t

&gt; (subtype -True (Un -True -False))
#t

&gt; (subtype (-poly (a) (Un a -String)) -String)
#t</code><p>　　掌握这些东西后，差不多就可以玩Typed Racket了，比如探究latent object对类型推导的影响（参见<a href="https://github.com/racket/typed-racket/issues/678">https://github.com/racket/typed-racket/issues/678</a>）：</p><code lang="racket">&gt; (register-type #'f1 (-poly (a)
                           (-&gt; (make-Intersection (list a top-func))
                               (make-Values
                                (list
                                 (make-Result
                                  (make-Intersection (list a top-func))
                                  -true-propset
                                  -empty-obj
                                  ))))))
&gt; (register-type #'f2 (-poly (a)
                           (-&gt; (make-Intersection (list a top-func))
                               (make-Values
                                (list
                                 (make-Result
                                  (make-Intersection (list a top-func))
                                  -true-propset
                                  (-arg-path 0)
                                  ))))))
                                  
&gt; (tc-expr #`(#%plain-app f1 f1))
(tc-results
 (list
  (tc-result
   (-&gt; Nothing (case-&gt;))
   (Top | Bot)
   -))
 #f)
 
&gt; (tc-expr #`(#%plain-app f2 f2))
(tc-results
 (list
  (tc-result
   (All
    (a)
    (-&gt; (∩ (case-&gt;) a) (∩ (case-&gt;) a)))
   (Top | Bot)
   -))
 #f)</code><p></p>
<script async defer="defer" src="https://www.googletagmanager.com/gtag/js?id=UA-7909075-5"></script>
<script>
window.dataLayer = window.dataLayer || [];
function gtag(){ dataLayer.push(arguments); }
gtag('js', new Date());
gtag('config', 'UA-7909075-5');
</script>
<script>
var _hmt = _hmt || [];
</script>
<script async defer="defer" src="https://hm.baidu.com/hm.js?e3d40295e416616ddc21287da9646d31"></script>
</body>
</html>
